#CONTROLCODES 1
#SYMFILE "@:CONFMENU.SYM"
DEF FN SIGNED.BYTE(N) = N+256*(N>127)
OB = PEEK(1):BANK PEEK(0),0
ROM.VERSION=FN SIGNED.BYTE(PEEK($FF80))
## IF ROM.VERSION < 0 THEN PRINT "PRE-RELEASE ROM"
GOOD.ROM = (ROM.VERSION < -45) OR (ROM.VERSION >= 45)
ROM.VERSION = ABS(ROM.VERSION)
BANK PEEK(0), OB


IF NOT GOOD.ROM THEN PRINT "{GRN}ROM VERSION:{WHT}";ROM.VERSION:DM$="HANGMAN REQUIRES A R45 RELEASE ROM OR LATER!":GOTO ERRORHALT


STARTUP:
DIM S$,F$,DCHAR%(9),NV$(18),NN$(18),NV%,FV%,TC%(2,8),PC%(8,4),MC$(7),DI$(3),PL%(2,1),PL$(2),WN%(2)
PF$="DPAL.BIN":GOSUB SETREGVARS:GOSUB POKENOFLICKER

POKE C.REG,1:SYS $FF5F:IF PEEK(A.REG)<>$80 THEN GOSUB HIDEALL:MYMODE=$80:GOSUB NOFLICKERSCREEN

DIM MUSIC.NAME$(3),ZNUM%(3),USING.LST%(3),ZSM.RANDOM%(3),MUSIC.PAUSED%(3):LS%=-1:X=RND(-RND(.))
GOSUB SETUPTEXTMODE

POKE C.REG,1:SYS $FF99:LAST.BANK = PEEK(A.REG):IF LAST.BANK=0 THEN LAST.BANK=256
LAST.BANK = LAST.BANK-1:BANKS.AVAILABLE = LAST.BANK-10:NEXT.BANK=2
GOSUB CLRHIBANKSIG

MAIN2:
  PF$="DPAL.BIN":GOSUB SETDEFPAL

## CLEAR OUR FLAGS STUFF OUT OF GOLDENRAM
  FOR I=$400 TO $40A:POKE I,0:NEXT I:GOSUB POKEBVSAVE:GOSUB VARINIT



CONFMENU:
  MOUSE 1
  GOSUB HIDEALL:CLS:GOSUB SETDEFPAL:GOSUB SETDRAWPAL:S$="HANGMAN - Configuration Options":GOSUB GMENUBACK
  IF CH<1 OR CH>6 THEN CH=1
  X=5:Y=4:DI=2
  FOR I=1 TO 7
    Y=Y+2:LOCATE Y,X:COLOR 1,0:PRINT RIGHT$(STR$(I),1);:PRINT ":{PUR}"MC$(I);
  NEXT I
  W=34:H=14:C1=1:C2=$17:C3=$C2:C4=$1E:L$="ENTER":X=28:Y=163:GOSUB DRAWBTN:LOCATE 22,9:S$="{PUR}To Confirm Choice":GOSUB PETCHK
  PRINT S$;
  GOSUB SHOWALL

MENUSHOW:
  IF CH=7 THEN Y=18
  IF CH=6 THEN Y=16
  IF CH=5 THEN Y=14
  IF CH=4 THEN Y=12
  IF CH=3 THEN Y=10
  IF CH=2 THEN Y=8
  IF CH=1 THEN Y=6
  LOCATE Y,5:COLOR 7,4:PRINT CHR$(DCHAR%(9));" ";:PRINT"{WHT}";MC$(CH);RPT$(32,27-LEN(MC$(CH)));:COLOR 1,0

MENUGET:
  GET X$:RA=MWHEEL:IF MB<>1 AND RA=0 THEN SKIPMOUSE3
  CX=MX:CY=MY:I=1:MC%=0

CHKMOUSE:
  IF RA<0 THEN X$=CHR$(145)
  IF RA>0 THEN X$=CHR$(17)
  IF X$<>"" THEN ENDMOUSE
  MC%=(CX>=PC%(I,1)) AND (CX<=PC%(I,3)) AND (CY>=PC%(I,2)) AND (CY<=PC%(I,4))
  IF NOT MC% THEN I=I+1:IF I>8 THEN X$="Z":GOTO ENDMOUSE
  IF NOT MC% THEN X$="":GOTO CHKMOUSE
  IF I<8 THEN X$=RIGHT$(STR$(I),1)
  IF I=8 THEN X$="{CR}"
ENDMOUSE:
  IF MB<>0 THEN ENDMOUSE

SKIPMOUSE3:
  IF X$="" THEN MENUGET
  CX=ASC(X$)
  LOCATE Y,5:COLOR 1,0:PRINT RIGHT$(STR$(CH),1);
  PRINT ":{PUR}"MC$(CH);RPT$(32,28-LEN(MC$(CH)));
  SAVECH=CH
  IF CH=7 AND CX=27 THEN CX=13
  IF CX=27 THEN CH=7
  IF CX>=49 AND CX<=55 THEN CH=VAL(X$)
  IF CX=145 THEN CH=CH-1
  IF CX=17 THEN CH=CH+1
  IF CX=13 THEN L$="ENTER":W=34:H=14:X=28:Y=163:GOSUB PUSHBTN:GOTO DOMENUCHOICE
  IF CH>7 THEN CH=1
  IF CH<1 THEN CH=7
  IF DI>3 THEN DI=1
  IF DI<1 THEN DI=3
  IF SAVECH=CH THEN GOSUB BOING
  IF SAVECH<>CH THEN GOSUB CLICK
  GOTO MENUSHOW

DOMENUCHOICE:
  IF CH=7 THEN CLEANUP
  IF CH=4 THEN ROUND=1:CP%=1:WN%(1)=0:WN%(2)=0:GOSUB PLAYDEMO:GOTO CONFMENU
  IF CH=3 THEN GOTO PALLOADER
  IF CH=2 THEN GOSUB EDITHANGINI:GOTO CONFMENU
  IF CH=1 THEN TARGET$="README.PRG"
  IF CH=5 THEN TARGET$="HANGMAN.PRG"
  IF CH=6 THEN TARGET$="CREDITS.PRG"
  IF CH=5 OR CH=6 OR CH=1 THEN GOTO LOADER

  GOSUB BOING:GOTO MENUSHOW

EDITHANGINI:
  GOSUB SETDEFPAL
  SAVE.ISO.STATE = ISO.ON
  IF ROM.VERSION < 47 AND ISO.ON THEN GOSUB PETSCIIMODE:GOSUB THICKFONT
  MYMODE=1:GOSUB HIDEALL:GOSUB NOFLICKERSCREEN:GOSUB SETDEFPAL:GOSUB SHOWALL
  EDIT "HANGMAN.INI"
  IF ROM.VERSION < 47 AND SAVE.ISO.STATE THEN GOSUB SETISOMODE
  GOSUB SETUPTEXTMODE
  MYMODE=$80:GOSUB HIDEALL:GOSUB NOFLICKERSCREEN
  GOSUB HIDEALL:GOSUB CLSGRAPH:GOSUB SETDEFPAL
  RETURN

PALLOADER:
  TARGET$="PALEDITOR.PRG"
  SCREEN 1:COLOR 1,0:CLS
  DOS "CD:ALTPALETTES"
  DOS "$"
  PRINT "{CR}{CR}{YEL}  PRESS ANY KEY"
  GOSUB FLUSHKEYS
WAIT2LOAD:
  GET X$:IF X$="" AND MB=0 THEN WAIT2LOAD
  GOTO SKIPCLRGRAPH

LOADER:
  GOSUB CLSGRAPH:GOSUB SHOWALL
SKIPCLRGRAPH:
  GOSUB PUTTOMENU:LOAD TARGET$

ADJUSTPETLOWER:
 IF CC>=193 AND CC<=218 THEN CC=CC-128:X$=CHR$(CC)
 RETURN

PLAYDEMO:
   FORCEDRAW=TRUE
   CP%=1:PL$(1)="DEMO HANGMAN":GOSUB CLSGRAPH
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB SHOWALL:GOSUB SKIPLOAD:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWGALLOWS:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWNOOSE:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWFACE:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWTORSO:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWRIGHTARM:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWLEFTARM:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWRIGHTLEG:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWLEFTLEG:GET X$
   IF X$="{ESC}" THEN DONEDEMO
   GOSUB LOSSSCREEN
DONEDEMO:
   FORCEDRAW=FALSE:PL$(1)="":GOSUB CLSGRAPH:RETURN

GMENUBACK:
  RECT 0,0,XLIMIT,YLIMIT,0:GOSUB FRAMESCRN:RECT 10,15,307,214,6:FRAME 13,27,304,211,12:RECT 15,29,302,209,12:CHAR 18,25,1,CHR$($0C)+S$:RETURN

FRAMESCRN:
  X1=5:Y1=9:X2=312:Y2=219:CC=$1C:FOR J=1 TO 6:FRAME X1,Y1,X2,Y2,CC:X1=X1+1:X2=X2-1:Y1=Y1+1:Y2=Y2-1:CC=CC-2:NEXT J:RETURN


PUSHBTN:
  C1=$17:C2=1:C3=$61:C4=$10:GOSUB DRAWBTN:GOSUB CLICK:SLEEP 5:C1=1:C2=$17:C3=$C2:C4=$1E:GOTO DRAWBTN


DRAWBTN:
     IF H=0 THEN H=W:I=-1
     X1=X+1:XW=X+W:YW=Y+H:Y1=Y+1:Y5=YW-1:X5=XW-1:LINE X,Y,XW,Y,C1:LINE X1,Y1,X5,Y1,C1:LINE XW,Y,XW,YW,C1:LINE X5,Y1,X5,Y5,C1
     LINE X,Y,X,YW,C2:LINE X1,Y1,X1,Y5,C2:LINE X,YW,X,YW,C2:LINE X,YW,X5,YW,C2:LINE X,Y5,XW-2,Y5,C2:RECT X+2,Y+2,XW-2,YW-2,C3:AX=4
## POSITION BUMPS TO CENTER THESE LETTERS ON THE BTN
     IF L$="M" THEN AX=3
     IF L$="W" THEN AX=2
     IF L$="F" OR L$="L" THEN AX=5
     IF L$="I" THEN AX=6
## DRAW THE LETTER ON THE BTN
     CHAR X+AX,YW-3,C4,L$
## DRAW A SERIF ON I AND J FOR APPEARANCE SAKE
     IF L$="I" THEN LINE X+6,YW-9,X+8,YW-9,C4:LINE X+6,YW-3,X+8,YW-3,C4
     IF L$="J" THEN LINE X+7,YW-9,X+9,YW-9,C4
     IF H=W AND I THEN H=0
    RETURN

RESETCIRCLE:
  XSQUISH=1:YSQUISH=1:Q1=1:Q2=1:Q3=1:Q4=1:FILL=0:RETURN

  TEXTBOX:
     LOCATE Y,X:PRINT CHR$(DCHAR%(1));RPT$(DCHAR%(2),W-2);CHR$(DCHAR%(3));
     IF H=2 THEN SKIPMID
     FOR RA=1 TO H-2:LOCATE Y+RA,X:PRINT CHR$(DCHAR%(4));RPT$(32,W-2);CHR$(DCHAR%(5));:NEXT RA
SKIPMID:
     LOCATE Y+H-1,X:PRINT CHR$(DCHAR%(6));RPT$(DCHAR%(7),W-2);CHR$(DCHAR%(8));:RETURN

SETUPTEXTMODE:
  GOSUB GETCHARMODE:IF NOT ISO.ON THEN SKIPISO
ISOFONTS:
  PETSCII=FALSE
  GOSUB LOADFONTS
  F$=FONT1$:GOSUB FILEEXISTS:F=FE%:F$=FONT2$:GOSUB FILEEXISTS:F2=FE%
  IF NOT (F2 OR F) THEN SKIPISO
  IF F AND (NOT F2) THEN FONT2$=FONT1$
  IF F2 AND (NOT F) THEN FONT1$=FONT2$
  FONTFILE$=FONT1$
  GOTO LOADISOFONT

LOADFONTS:
  RESTORE FONTNAMES:READ FONT1$:READ FONT2$:RETURN


SKIPISO:
  PETSCII=TRUE
PETSCIIMODE:
  PRINT "{ISO OFF}{UPPER}":GOSUB PETDRAW:ISO.ON=FALSE:GOTO THINFONT

SETISOMODE:
  PRINT "{ISO ON}":ISO.ON=TRUE:GOTO ISODRAW

LOADISOFONT:
  BVLOAD FONTFILE$,8,1,$F000:GOSUB FIXCURSOR

COPYDIAMOND:
 OB=PEEK(1):BANK PEEK(0),6:CA=$C2D0:VA=$F748:FOR I=0 TO 7:X=PEEK(CA+I):VPOKE 1,VA+I,X:NEXT I:BANK PEEK(0),OB:RETURN

FIXCURSOR:
  FBASE=$9F*8+$F000
  FOR X=0 TO 5:VPOKE 1,FBASE+X,0:NEXT X
  VPOKE 1,FBASE+6,$FF:VPOKE 1,FBASE+7,$FF

SETDRAWCHARS:
  IF ISO.ON THEN ISODRAW
  GOTO PETDRAW

ISODRAW:
  RESTORE ISODATA:GOTO READCHAR

PETDRAW:
  RESTORE PETDATA
READCHAR:
  FOR I=1 TO 9:READ DCHAR%(I):NEXT I:RETURN

PETDATA:
  DATA $6F,$A3,$70,$A5,$A7,$6C,$A4,$BA,$7A

ISODATA:
  DATA $C9,$CD,$BB,$BA,$BA,$C8,$CD,$BC,$E9

GETCHARMODE:
   ISO.ON=(PEEK($372) AND %01000000) <> 0:RETURN

LOSSSCREEN:
  F$="ALTPALETTES/HANGMAN.IMG":GOSUB SAVESCREEN
  IF FORCEDRAW THEN SKIPL
  P1=$3B:P2=$0A:GOSUB COPYPALET
  FOR I=0 TO 7:TC%(1,I)=$0A:TC%(2,I)=0:NEXT I
SKIPL:
  GOSUB THICKFONT:NEWZS=3:ZSM.RANDOM%(3)=TRUE:GOSUB SWAPSONGTO:GOSUB DEADFACE:S$=" Any Key to Continue ":GOSUB PLAINMSG:P1=$FE:G%=0:B%=0:GOSUB FLUSHKEYS
KEYLIMITOOP2:
  GET X$:IF MB<>0 THEN X$="A"
EYESDONE:
  IF X$<>"" THEN ENDLOST
  R%=RND(1)*15+1:B%=RND(1)*6:IF MB<>0 THEN X$="A":GOTO EYESDONE
  J=2:GOSUB PALFADE:GOTO KEYLIMITOOP2
ENDLOST:
  CLS:GOSUB THINFONT:RETURN

GETFROMMENU:
  RA=PEEK(0):S$=""
  ADDR=$BF00:BANK LAST.BANK
  FOR I=0 TO 7:S$=S$+CHR$(PEEK(ADDR+I)):NEXT I
  FROM.MENU = (S$="HANGMENU")
  IF FROM.MENU THEN FOR I=0 TO 7:POKE ADDR+I,0:NEXT I
  BANK RA:RETURN

PUTTOMENU:
  RA=PEEK(0):S$="HANGMENU"
  ADDR=$BF00:BANK LAST.BANK
  FOR I=0 TO 7:POKE ADDR+I,ASC(MID$(S$,I+1,1)):NEXT I
  BANK RA:RETURN

CLRHIBANKSIG:
  RA=PEEK(0):S$="HANGMENU"
  ADDR=$BF00:BANK LAST.BANK
  FOR I=0 TO 7:POKE ADDR+I, 0:NEXT I
  BANK RA:RETURN

DEADFACE:
  S$=PL$(CP%)+" Has Died":GOSUB PLAINMSG:P1=$24:P2=$FF:J=4:GOSUB PAL2PALFADE:P1=$3B:P2=$FE:J=4:GOSUB PAL2PALFADE
  P1=1:J=3:GOSUB PAL2PALFADE:P1=$3A:J=1:GOSUB PAL2PALFADE:P1=$15:J=3:GOSUB PAL2PALFADE
  P1=$3B:J=4:GOSUB PAL2PALFADE:P1=$23:P2=$FF:J=6:GOSUB PAL2PALFADE:P1=$B8:J=5:GOSUB PAL2PALFADE
  P1=$B9:GOSUB PAL2PALFADE:P1=$BF:GOSUB PAL2PALFADE:P1=$C7:GOSUB PAL2PALFADE:RETURN

AREACLR:
  LOCATE Y,X:PRINT RPT$(32,ML);:LOCATE Y,X:RETURN

CLICK:
  FMDRUM 7,26:RETURN

BOING:
  FMDRUM 7,87:RETURN

FLUSHKEYS:
   GET X$:IF X$<>"" THEN FLUSHKEYS
   RETURN

BACKGROUND:
     F$="HANGBG.DAT":GOSUB FILEEXISTS
     IF NOT FE% THEN SKIPLOAD
     IF COMPLETE.DRAW THEN GOSUB HIDEGRAPHICS
     BVLOAD F$,8,0,0:GOSUB SETDRAWPAL
     IF COMPLETE.DRAW THEN GOSUB SHOWGRAPHICS
     GOTO BGEND
SKIPLOAD:
     J=3:XLIMIT=319:YLIMIT=239:XSQUISH=1:YSQUISH=1
     P1=$C0:P2=$F2:GOSUB COPYPALET:R%=0:G%=0:B%=1:GOSUB RGBPAL:S$="The Sky":GOSUB DRAWMSG
     RECT 0,0, XLIMIT, YLIMIT,$C0:P1=$F2:P2=$C0:GOSUB PAL2PALFADE:P1=$C0:P2=$10:GOSUB COPYPALET
     P1=7:P2=$F1:GOSUB COPYPALET:P1=$C0:P2=7:GOSUB COPYPALET:CX=5:CY=4:CC=$10:FILL=1:XSQUISH=1:YSQUISH=1:Q1=1:Q2=1
     Q3=1:Q4=1:RA=32:GOSUB BCIRCLE:CC=7:CX=5:CY=4:RA=30:FILL=1:RA=30:GOSUB BCIRCLE:FILL=0:C=7

     LINE 2,37,2,44,C:LINE 6,37,6,44,C:LINE 9,37,10,44,C:LINE 12,36,14,43,C:LINE 15,35,17,42,C:LINE 18,34,21,41,C
     LINE 20,33,24,39,C:LINE 22,31,27,37,C:LINE 24,28,29,35,C:LINE 27,25,32,32,C:LINE 30,23,35,29,C:LINE 32,21,37,26,C
     LINE 33,19,39,23,C:LINE 35,17,41,20,C:LINE 36,15,43,17,C:LINE 37,12,44,14,C:LINE 38,10,45,11,C:LINE 39,7,45,8,C
     LINE 39,4,45,5,C:LINE 39,1,46,2,C:P1=0:P2=$10:J=3:GOSUB PAL2PALFADE:P1=$F1:P2=7:GOSUB PAL2PALFADE

     FILL=1:GOSUB DRWCLOUDS:GOSUB DRWCLOUDS:GOSUB DRWCLOUDS:S$="Grass":GOSUB DRAWMSG
     P1=105:P2=$F2:GOSUB COPYPALET:P2=P1:P1=$C0:GOSUB COPYPALET:P1=$85:P2=$F3:GOSUB COPYPALET:P2=P1:P1=$C0:GOSUB COPYPALET
     RECT 0,180,XLIMIT,YLIMIT,$85
     FOR Y=161 TO 179:LINE 0,Y,40,179,105:NEXT Y
     FOR Y=180 TO 150 STEP -1:LINE 288,179,XLIMIT,Y,105:NEXT Y
     J=5:P2=$85:P1=$F3:GOSUB PAL2PALFADE:P2=105:P1=$F2:GOSUB PAL2PALFADE:COLOR 1

     FOR I=1 TO 550
         X1=INT(RND(.)*310)+5
       NEWY:
         Y1=INT(RND(.)*60)+180:IF Y1>YLIMIT THEN NEWY
         GOSUB GRASSCLUMP
     NEXT I
     IF NOT LS% THEN BGEND
     F$="BACKGROUND SCREEN":GOSUB SAVESCREEN
BGEND:
     PUTIMAGE=FALSE:X1=290:Y1=166:X2=304:Y2=211:F$="UNDER.DAT":GOTO BITBLT
     RETURN

SAVESCREEN:
    S$="Saving: "+F$:GOSUB PLAINMSG
    IF F$="BACKGROUND SCREEN" THEN FF$="@:HANGBG.DAT,P,W":GOTO DOIT
    FF$="@:"+F$ + ",P,W"
DOIT:
    OPEN 2,8,2,FF$:POKE $30C,2:POKE$9F25,0:POKE$9F20,0:POKE$9F21,0:POKE$9F22,$10:SYS$700:CLOSE 2:RETURN

GRASSCLUMP:
     GOSUB GRASSCOLOR:LINE X1,Y1,X1-4,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1-3,Y1-3,GC:GOSUB GRASSCOLOR:LINE X1,Y1,X1,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+3,Y1-3,GC:GOSUB GRASSCOLOR:LINE X1,Y1,X1+4,Y1-5,GC:RETURN
GRASSCOLOR:
     GC=INT(RND(.)*24)+$60:RETURN

DRWCLOUDS:
     S$="Clouds":GOSUB DRAWMSG:H=INT(RND(.)*(200))+45:HL=INT(RND(.)*30)+30:V=INT(RND(.)*30)+6:VB=INT(RND(.)*11)+7
     FOR Y=V TO V+VB STEP 3
         FOR CX=H TO H+HL STEP 4
           TRYCLOUD:
             RA=INT(RND(.)*5)+3:CC=INT(RND(.)*4)+$1C:CY=INT(RND(.)*4)+(Y-4):IF (CY-RA)<0 THEN GOSUB BCIRCLE:GOTO SKIPPERB
             GOSUB CCIRCLE
           SKIPPERB:
         NEXT CX
     NEXT Y:RETURN

CCIRCLE:
     WR=RA:G=0: D=2*(1-RA):W=INT(2*320/240)
REM  WHILE WR<0
CCLOOP:
       IF WR < 0 THEN CCDONE
         LINE CX-G,CY-WR,CX+G,CY-WR,CC:LINE CX-G,CY+WR,CX+G,CY+WR,CC
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF G>D THEN G=G+1: D=D+2*G+1
       GOTO CCLOOP
REM WEND
CCDONE:
    RETURN

DRAWGALLOWS:
     GOSUB CLRMSG:FRAME 189,195,285,208,$10:FRAME 188,194,285,211,$10:LINE  189,210,285,210,$10:PSET 189,209,$10:PSET 284,206,$10:PSET 284,209,$10:FRAME 275,21,285,207,$10
     FRAME 276,22,284,205,$10:FRAME 67,20,285,30,$10:FRAME 68,21,284,29,$10:RECT 71,30,79,33,$10:RECT 190,196,283,209,$53:RECT 277,23,283,209,$53:RECT  69,22,283,28,83:RETURN

DRAWNOOSE:
  S$="The  NOOSE":GOSUB DRAWMSG:F$="NOOSE.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:GOTO NOOSEFACEBLIT
  LINE 73,33,73,53,16:LINE 77,33,77,53,16:RECT 74,34,76,53,87:FOR Y=38 TO 53 STEP 3:LINE 73,Y,77,Y-3,$10:NEXT Y
  FRAME 71,53,80, 68,16:RECT 72,54,79,68,87:FOR Y=56 TO 68 STEP 4:LINE 72,Y,79, Y-4,BLACK2:NEXT Y
  FILL=0:XSQUISH=1:RA=24:CX=75:CY=79:CC=BLACK2:YSQUISH=.38:Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=.38
  GOSUB BCIRCLE:RA=25:GOSUB BCIRCLE:RA=24:GOSUB BCIRCLE:RA=23:GOSUB BCIRCLE:RA=19:GOSUB BCIRCLE:RA=18:GOSUB BCIRCLE
  CC=87:FOR RA=20 TO 23 STEP .6:GOSUB BCIRCLE:NEXT RA:PUTIMAGE=FALSE:GOTO NOOSEFACEBLIT

DRAWFACE:
  S$="A Troubled Face":GOSUB DRAWMSG:F$="FACE.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:GOTO NOOSEFACEBLIT
  CX=58:CY=60:RA=6:XSQUISH=.4:YSQUISH=1:CC=BLACK2:Q1=1:Q2=0:Q3=1:Q4=0:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:FILL=1:CC=$25:GOSUB BCIRCLE
  FILL=0:CX=90:RA=6:CC=BLACK2:Q1=0:Q2=1:Q3=0:Q4=1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:FILL=1:CC=$25:GOSUB BCIRCLE
  LINE 93,58,93,62,BLACK2:Q1=1:Q2=Q1:Q3=Q1:Q4=Q1:LINE 67,76,67,84,$10:LINE 66,76,66,84,BLACK2
  LINE 67,87,67,90,BLACK2:LINE 66,87,66,90,$10:LINE 82,76,82,84,BLACK2:LINE 83,76,83,84,BLACK2:LINE 82,87,82,90,$10:LINE 83,87,83,90,BLACK2
  RECT 68,76,81,84,$25:RECT 68,89,81,90,$25:RA=20:XSQUISH=.8:FILL=1:CC=BLACK2:CX=74:CY=63
  GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-2:CY=CY+1:CC=$FF:FILL=1:GOSUB BCIRCLE:XSQUISH=1:YSQUISH=XSQUISH:CY=CY-6:CX=CX-6:RA=3:CC=$FE:GOSUB CCIRCLE
  PSET CX+4,CY,$FF:GOSUB PUPILS:CX=CX+12:GOSUB CCIRCLE:PSET CX+4,CY,$FF:GOSUB PUPILS:CX=74:CY=63:LINE CX-1,CY,CX-2,CY+6,$10:LINE CX+1,CY,CX+2,CY+6,$10
  LINE CX,CY,CX-1,CY+6,$23:LINE CX,CY,CX+1,CY+6,$23:LINE CX,CY+3,CX,CY+6,$22:Q3=0:Q4=0:CC=$10:CY=CY+13:YSQUISH=.35:RA=6:FILL=0:GOSUB BCIRCLE
  CY=CY+1:CC=$31:GOSUB BCIRCLE:CY=CY+1:CC=$10:GOSUB BCIRCLE:Q3=1:Q4=1:YSQUISH=1:PUTIMAGE=FALSE:GOSUB NOOSEFACEBLIT:RETURN
PUPILS:
  PSET CX,CY,$10:PSET CX-1,CY,$10:PSET CX,CY+1,$10:PSET CX-1,CY+1,$10:RETURN

NOOSEFACEBLIT:
  BLITWIDTH=57:BLITLINES=75:X1=46:Y1=33:X2=102:Y2=107:GOTO BITBLT

DRWTORSO:
  S$="Torso":GOSUB DRAWMSG
  LINE 82,90,105,93,BLACK2:LINE 83,91,105,94,BLACK2:LINE 68,90,42,93,BLACK2:LINE 69,91,42,94,BLACK2:LINE 67,90,74,105,BLACK2:LINE 83,90,74,105,BLACK2
  FOR X=81 TO 68 STEP -1:LINE 75,103,X,90, $25:NEXT X
  LINE 74,105,72,110,BLACK2:LINE 72,110,72,141,BLACK2:LINE 72,141,53,144,BLACK2:LINE 53,144,53,108,BLACK2
  RECT 71,140,54,103,8:LINE 54,141,68,141,8:LINE 54,142,62,142,8:LINE 54,143,56,143,8
  PSET 65,91,$10:LINE 72,102,72,108,8:LINE 73,104,73,106,8:PSET 74,109,8:RECT 47,107,68,94,8:RECT 69,96,69,99,8
  RECT 42,107,48,95,8:LINE 56,93,67,93,8:PSET 55,92,$10:PSET 64,91,BLACK2:LINE 65,92,67,92, 8:RECT 68,100,71,102,8:LINE 70,98,70,100,8
  RECT 73,110,91,140,8:RECT 75,105,91,109,8:RECT 88,94,101,106,8:RECT 81,95,105,106,8:LINE 80,96,80,105,8
  LINE 79,98,79,105,8:LINE 78,100,78,105,8:LINE 77,101,77,105,8:LINE 76,103,76,105,8:LINE 74,103,75,103,BLACK2
  PSET 76,101,BLACK2:PSET 73,101,$10:PSET 72,99,$10:PSET 71,97,BLACK2:PSET 70,95,BLACK2:PSET 69,93,$10:PSET 81,92,$10:PSET 80,94,BLACK2
  PSET 80,96,BLACK2:PSET 78,97,$10:PSET 77,99,$10:LINE 76,100,76,103,$10:RECT 82,94,93,93, 8:LINE 83,92,86,92,8
  LINE 74,107,74,108,8:PSET 73,109,$10:PSET 73,141,$10:LINE 76,141,91,141,8:LINE 81,142,91,142,8:LINE 89,143,91,143,8:LINE 74,141,92,144,$10:LINE 92,144,92,107,$10:PSET 73,141,$10
  GOSUB RESETCIRCLE:CC=16:RA=1.2:CX=75:FILL=1:FOR CY=113 TO 143 STEP 8:GOSUB BCIRCLE:NEXT CY
  LINE 58,108,68,108,BLACK2:LINE 58,108,58,116,BLACK2:LINE 68,108,68,116,BLACK2:Q1=0:Q2=0:Q3=1:Q4=1:RA=4.5:XSQUISH=1:YSQUISH=.6
  CX=63:CY=116:CC=$10:FILL=0:GOSUB BCIRCLE:X$="P-"+RIGHT$(STR$(CP%),LEN(STR$(CP%))-1):CHAR 57,106,$10,X$:RETURN

DRWLEFTARM:
  S$="Left Arm":GOSUB DRAWMSG:F$="LEFTARM.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:X1=91:Y1=92:BLITWIDTH=22:BLITLINES=57:GOTO BITBLT
  Q1=0:Q2=1:Q3=0:Q4=0:FILL=0:XSQUISH=.52:RA=16:CC=$10:CY=116:CX=92
  GOSUB BCIRCLE:RA=RA+.6:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:XSQUISH=.28:CX=106:CY = 105:FILL=0:GOSUB BCIRCLE
  FOR L=1 TO 3:RA=RA+.5:GOSUB BCIRCLE:NEXT L
  CC=8:Q1=0:Q2=1:Q3=0:Q4=0:FILL=1:RA=RA-2.5:GOSUB BCIRCLE:RECT CX-1,CY-3,CX+2,CY-7,8
  LINE 99,113,99,133,BLACK2:LINE 111,102,111,133,BLACK2:RECT 100,102,110,133,8:LINE 99,107,99,109,8:LINE 98,108,101,108,8:LINE 97,107,100,107,8 :PSET 94,107,$C0
  LINE 99,134,111,134,BLACK2:LINE 101,134,101,143,BLACK2:LINE 101,143,103,143,BLACK2:LINE 102,142,102,139,$25:LINE 103,144,103,140,BLACK2
  LINE 103,145,105,145,BLACK2:LINE 104,144,104,139,$25:LINE 105,146,105,140,BLACK2:LINE 105,146,107,146,BLACK2:LINE 106,145,106,139,$25:LINE 107,146,107,140,BLACK2
  LINE 107,145,109,145,BLACK2:LINE 108,144,108,139,$25:LINE 109,145,109,134,BLACK2:RECT 102,139,108,135,$25:PUTIMAGE=FALSE:X1=91:Y1=92:X2=112:Y2=148:GOTO BITBLT

DRWRIGHTARM:
  S$="Right Arm":GOSUB DRAWMSG:F$="RIGHTARM.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:X1=33:Y1=92:BLITWIDTH=22:BLITLINES=57:GOTO BITBLT
  Q1=1:Q2=0:Q3=0:Q4=0:FILL=0:XSQUISH=.52:RA=16:CC=$10:CY=118:CX=54:GOSUB BCIRCLE:RA=RA+.6:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
  LINE 54,107,54,109,8:XSQUISH=.43:CX = 42:CY = 105:FILL=0:GOSUB BCIRCLE
  FOR L=1 TO 3:RA=RA+.3:GOSUB BCIRCLE:NEXT L
  CC=8:Q1=1:Q2=0:Q3=0:Q4=0:FILL=1:RA=RA-3:GOSUB BCIRCLE:FILL=0:RECT CX-4,CY-8,CX+3,CY+6,8:RECT CX-4,CY-4,CX,CY+6,8
  LINE 34,102,34,133,BLACK2:LINE 46,115,46,133,BLACK2:RECT 35,102,45,133,8:LINE 34,134,46,134,BLACK2:LINE 46,CY,46,CY+6,8:LINE 47,CY,47,CY+5,8
  RECT 47,CY,49,CY+3,8:LINE 51,CY+4,52,CY+4,$C0:PSET 48,CY+4, 8:LINE 44,134,44,143,$10:LINE 44,143,42,143,$10
  LINE 43,142,43,139,$25:LINE 42,140,42,145,BLACK2:LINE 42,145,40,145,BLACK2:LINE 41,144,41,139,$25
  LINE 40,146,40,140,BLACK2:LINE 40,146,38,146,BLACK2:LINE 39,145,39,139,$25:LINE 38,146,38,140,BLACK2
  LINE 38,145,36,145,BLACK2:LINE 37,144,37,139,$25:LINE 36,145,36,134,BLACK2:RECT 43,139,37,135,$25
  PUTIMAGE=FALSE:X1=33:Y1=92:X2=54:Y2=148:GOTO BITBLT

DRWRIGHTLEG:
  S$="Right Leg":GOSUB DRAWMSG:F$="RIGHTLEG.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN X1=52:Y1=142:PUTIMAGE=TRUE:BLITWIDTH=24:BLITLINES=79:GOTO BITBLT
  Q1=1:Q2=0:Q3=0:Q4=0:FILL=0:CC=$10:CX=74:CY=159:RA=8:XSQUISH=.43:YSQUISH=.6:GOSUB BCIRCLE
  LINE 70,159,70,192,BLACK2:LINE 55,145,53,192,BLACK2:LINE 53,193,70,193,BLACK2:RECT 56,145,69,192,LEGCOLOR
  LINE 55,157,55,192,LEGCOLOR:LINE 54,181,54,192,LEGCOLOR:LINE 57,144,71,144,LEGCOLOR:RECT 69,142,71,154,LEGCOLOR
  RECT 72,142,73,153,LEGCOLOR:LINE 63,143,68,143,LEGCOLOR:PSET 70,155,LEGCOLOR:Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XSQUISH=.35:CC=$10:FILL=1:CX=62:CY=208
  GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1:XSQUISH=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE:RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
  LINE 57,193,57,202,$10:LINE 66,193,66,202,$10:RECT 58,194,65,200,$1C:PUTIMAGE=FALSE:X1=52:Y1=142:X2=75:Y2=220:GOTO BITBLT

DRWLEFTLEG:
  S$="Left Leg":GOSUB DRAWMSG:F$="LEFTLEG.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN X1=73:Y1=142:PUTIMAGE=TRUE:BLITWIDTH=21:BLITLINES=79:GOTO BITBLT
  Q1=0:Q2=1:Q3=0:Q4=0:FILL=0:CC=$10:CX=74:CY=159:RA=8:XSQUISH=.43:YSQUISH=.6:GOSUB BCIRCLE
  LINE 77,157,77,192,BLACK2:LINE 90,145,92,192,BLACK2:LINE 77,193,92,193,BLACK2:RECT 78,145,89,192,LEGCOLOR
  LINE 90,157,90,192,LEGCOLOR:LINE 91,181,91,192,LEGCOLOR:LINE 76,144,88,144,LEGCOLOR:RECT 76,143,79,154,LEGCOLOR
  RECT 74,142,76,153,LEGCOLOR:LINE 75,143,82,143,LEGCOLOR:LINE 77,154,77,156,LEGCOLOR:Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XSQUISH=.35:CC=$10:FILL=1
  CX=85:CY=208:GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1:XSQUISH=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE:RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
  LINE 80,193,80,202,$10:LINE 89,193,89,202,$10:RECT 81,194,88,200,$1C:PUTIMAGE=FALSE:X1=73:Y1=142:X2=93:Y2=220:GOTO BITBLT

SUNSMILE:
  COUNT=0
STARTSMILE:
  F$="SMILEY.DAT":GOSUB FILEEXISTS:IF FE% THEN PUTIMAGE=TRUE:BLITWIDTH=34:BLITLINES=34:X1=0:Y1=0:GOSUB BITBLT:GOTO BLINK
  Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=1:XSQUISH=1:RA=4:CX=17:CY=6:CC=1:FILL=1:GOSUB BCIRCLE
  RA=1:CC=$10:GOSUB BCIRCLE:RA=5:FILL=0:GOSUB BCIRCLE:CX=1:CY=15:YSQUISH=.4:Q1=0:Q2=0:RA=20:GOSUB BCIRCLE:CY=16:GOSUB BCIRCLE:CY=17:GOSUB BCIRCLE:Q1=1:Q2=1
  PUTIMAGE=FALSE:X1=0:Y1=0:X2=33:Y2=33:GOSUB BITBLT
BLINK:
  IF COUNT>0 THEN RETURN
  COUNT=COUNT+1:Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=1:XSQUISH=1:RA=4:CX=17:CY=6:CC=$53:FILL=1:GOSUB BCIRCLE:GOTO STARTSMILE

BCIRCLE:
  WR=RA:X=0: D=2*(1-RA):W=INT(2*320/240)
##     IF XSQUISH<0 OR XSQUISH>1 THEN XSQUISH=1
##     IF YSQUISH<0 OR YSQUISH>1 THEN YSQUISH=1
FOR J=0 TO 10 STEP 0
  IF WR < 0 THEN J=10:GOTO JNEXT
  DX=X*XSQUISH:DY=WR*YSQUISH
  ZX=CX-DX:ZY=CY-DY:AX=CX+DX:AY=CY+DY
  IF FILL=1 THEN DOFILL
  IF ZX<0 OR ZX>XLIMIT OR ZY<0 OR ZY>YLIMIT OR Q1=0 THEN SKIP4
  PSET ZX,ZY,CC
SKIP4:
  IF AX<0 OR AX>XLIMIT OR ZY<0 OR ZY>YLIMIT OR Q2=0 THEN SKIP2
  PSET AX,ZY,CC
SKIP2:
  IF ZX<0 OR ZX>XLIMIT OR AY<0 OR AY>YLIMIT OR Q3=0 THEN SKIP3
  PSET ZX,AY,CC
SKIP3:
  IF AX<0 OR AX>XLIMIT OR AY<0 OR AY>YLIMIT OR Q4=0 THEN SKIPFILL
  PSET AX,AY,CC:GOTO SKIPFILL
DOFILL:
  X1=ZX:X2=AX
  IF (Q1=0 AND Q2=O) OR X1>XLIMIT THEN FILL2
  IF ZY<0 OR ZY>YLIMIT THEN FILL2
  IF X1<0 THEN X1=0
  IF X2>XLIMIT THEN X2=XLIMIT
  IF Q1=0 THEN X1=CX
  IF Q2=0 THEN X2=CX
  LINE X1,ZY,X2,ZY,CC
FILL2:
  IF (Q3=0 AND Q4=0) OR ZX>XLIMIT THEN SKIPFILL
  IF AY<0 OR AY>YLIMIT THEN SKIPFILL
  IF ZX<0 THEN ZX=0
  IF AX>XLIMIT THEN AX=XLIMIT
  IF Q3=0 THEN ZX=CX
  IF Q4=0 THEN AX=CX
  LINE ZX,AY,AX,AY,CC
SKIPFILL:
  IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
  IF X>D THEN X=X+1:D=D+2*X+1
 JNEXT:
NEXT J
BCLOOPEND:
  RETURN

FONTNAMES:
  DATA "FONTS/THINSS.PF","FONTS/READABLE.PF"


POKEBVSAVE:
   RESTORE BVSAVE
   FOR A = $700 TO $74A:READ I:POKE A, I:NEXT A
   RETURN

BVSAVE:
   DATA $AA,$20,$C9,$FF,$9C,$4B,$07,$A9,$2C,$8D,$4C,$07,$A9,$01,$8D,$4D,$07,$A2
   DATA $23,$A0,$9F,$AD,$4B,$07,$38,$20,$B1,$FE,$B0,$29,$8E,$4E,$07,$AD,$4B,$07
   DATA $38,$ED,$4E,$07,$8D,$4B,$07,$AD,$4C,$07,$8C,$4E,$07,$ED,$4E,$07,$8D,$4C
   DATA $07,$AD,$4D,$07,$E9,$00,$8D,$4D,$07,$0D,$4B,$07,$0D,$4C,$07,$D0,$CA,$20
   DATA $CC,$FF,$60



SWAPSONGTO:
  RETURN


PLAYFANFARE:
  RETURN
  F$="FANFARE.DAT":GOSUB FILEEXISTS
  IF NOT FE% THEN GOTO NFMSG
  OPEN 10,8,10,"FANFARE.DAT,S,R"
  INPUT# 10, X$
  I=VAL(X$):X=INT(RND(1)*I)+1
  IF X=1 THEN SKIPFF
  X=X-1:POKE A.REG,10:POKE Y.REG,INT(X/256):POKE X.REG, X-PEEK(Y.REG)*256:SYS $650
SKIPFF:
  INPUT# 10, F$, ZTIMER
  CLOSE 10
  GOSUB FILEEXISTS:IF NOT FE% THEN GOTO NFMSG
  RETURN

NFMSG:
  S$=F$+" Not Found":GOTO PLAINMSG


## BITBLIT IS CALLED WITH PUTIMAGE = 0 (FALSE) TO GET THE IMAGE
## IMAGE IS BUFFERED TO FILE PASSED IN F$
## SCREEN RECTANGLE TO CAPTURE IS PASSED IN X1, Y1, X2, Y2
## X2 and Y2  ** MUST ** be greater than/= X1,Y1.  IF < FUNNY
## THINGS WILL HAPPEN
##
## BITBLIT IS CALLED WITH PUTIMAGE = -1 (TRUE) TO PUT THE IMAGE
## ON THE SCREEN F$ WILL HOLD FILE NAME.
## PUT STARTING AT X1, Y1
##
## BLITWIDTH AND BLITLINES NEED TO BE SET TO THE WIDTH AND HEIGHT
## OF THE IMAGE RESPECTIVELY, NO SUCH INFO IS IN THE FILE.
##
## IF IMMEDIATELY FOLLOWING A BITBLIT GETIMAGE CALL THEN
## BLITWIDTH AND BLITLINES WILL ALREADY BE SET.
## IN THAT INSTANCE ONLY X1, Y1 NEED TO BE CHANGED
##
## X1 AND Y1 CAN BE LEFT UNCHANGED IF THE USE IS SAVING A
## SECTION OF SCREEN TO RESTORE AFTER SOMETHING ELSE IS DRAWN
##
##
## FOR THAT CASE CALL BITBLIT WITH PUTIMAGE = FALSE
## DO YOUR SCREEN STUFF
## SET PUTIMAGE TO TRUE,  CALL BITBLIT AGAIN TO RESTORE ALREADY


BITBLT:
  IF PUTIMAGE THEN X$="":S$=",S,R":GOTO SKIPPER
  IF X1>X2 THEN X=X1:X1=X2:X2=X
  IF Y1>Y2 THEN X=Y1:Y1=Y2:Y2=X
  S$=",P,W":X$="@:"
SKIPPER:
  FF$=X$+F$+S$
  IF PUTIMAGE THEN SET.IO=CHKIN:DO.FILEIO=MACPTR:GOTO DOSCREENBLIT
  SET.IO=CHKOUT:DO.FILEIO=MCIOUT:BLITWIDTH=(X2-X1)+1:BLITLINES=(Y2-Y1)+1
DOSCREENBLIT:
  IF Y1+BLITLINES > YLIMIT THEN BLITLINES=YLIMIT-Y1
  OPEN 2,8,2,FF$:POKE X.REG, 2:SYS SET.IO
  IF X1 > 255 THEN BYTESPLIT
  POKE R0L, X1:POKE R0H, 0:GOTO POKEY
BYTESPLIT:
  POKE R0L, X1 AND $00FF:POKE R0H, INT(X1/256)
POKEY:
  POKE R1L, Y1:POKE R1H, 0:SYS FB.CURSOR.POSITION

  FOR Y=1 TO BLITLINES
    I=0
    FOR X=1 TO 3 STEP 0
      BYTES=BLITWIDTH-I:IF BYTES>$FF THEN BYTES=$FF
      POKE A.REG, BYTES:POKE X.REG, $23:POKE Y.REG, $9F:POKE C.REG,1:SYS DO.FILEIO:I=I+PEEK(X.REG):IF I>=BLITWIDTH THEN X=3
    NEXT X
    SYS FB.CURSOR.NEXTLINE
  NEXT Y
  CLOSE 2:SYS CLRCHN:RETURN

FILEEXISTS:
  X$=F$:FE%=0:X$=X$+",S,R":OPEN 2,8,2,X$:CLOSE 2:OPEN 15,8,15:INPUT#15,X,X$:CLOSE 15:FE%=(X<=20):RETURN

DRAWMSG:
  S$=" DRAWING: "+S$
PLAINMSG:
##  S$="FRE:"+STR$(FRE(0))+"  "+S$
  IF LEN(S$)>38 THEN S$=LEFT$(S$,38)
  GOSUB PETCHK:COLOR 1,0:GOSUB CLRMSG:LOCATE 29,2:PRINT S$;:RETURN

CLRMSG:
  LOCATE 29,2:PRINT RPT$(32,38);:RETURN

BIGWORD:
  L=LEN(WD$):SX=X
  OB=PEEK(1):BANK PEEK(0),6
  FOR K=1 TO L
    CC=ASC(MID$(WD$,K,1))
    IF (CC>=64 AND CC<=90) THEN AA=64:GOTO SKIP0
    IF (CC>=97 AND CC<=122) THEN AA=-32:GOTO SKIP0
    AA=0
SKIP0:
    CA=$C000+8*(CC-AA)
    FOR I=1 TO 8
      CM(I)=PEEK(CA+(I-1)):CM$(I)=""
    NEXT I
    IF DC$="" THEN DC$=CHR$(CC)
    TD=8:IF CC=32 THEN TD=5
    FOR J=1 TO TD
        RESTORE BITMAPS
        FOR CT=1 TO 8
            READ CP
            IF (CP AND CM(J)) THEN CM$(J)=CM$(J)+DC$:GOTO SKIPSPC
            CM$(J)=CM$(J)+CHR$(32)
      SKIPSPC:
        NEXT CT
        LOCATE Y+(J-1),X:IF UC=1 THEN COLOR TC%(1,J),TC%(2,J)
        PRINT CM$(J);
    NEXT J
    IF DC$=CHR$(CC) THEN DC$=""
    X=X+TD
  NEXT K
  X=SX:BANK PEEK(0),OB:RETURN

HIDEALL:
  GOSUB HIDEGRAPHICS:GOTO HIDETEXT
SHOWALL:
  GOSUB SHOWGRAPHICS:GOTO SHOWTEXT

HIDEGRAPHICS:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) AND LAYER0.OFF:RETURN

SHOWGRAPHICS:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) OR LAYER0.ON:RETURN

HIDETEXT:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) AND LAYER1.OFF:RETURN

SHOWTEXT:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) OR LAYER1.ON:RETURN

RGBPAL:
  A=P1*2:X=(G%*16)+B%:A=A+$FA00:SLEEP:VPOKE 1,A,X:A=A+1:SLEEP:VPOKE 1,A,R%:RETURN

READPAL:
  A=$FA00+(P1*2):R%=VPEEK(1,A+1):GB%=VPEEK(1,A):G%=GB%/16:B%=GB% AND $0F:RETURN

PAL2PALFADE:
  GOSUB READPAL:P3=P1:P1=P2:GOSUB PALFADE:P1=P3:RETURN

PALFADE:
  DR%=R%:DG%=G%:DB%=B%:GOSUB READPAL
  RI=1:IF DR%<R% THEN RI=-1
  GI=1:IF DG%<G% THEN GI=-1
  BI=1:IF DB%<B% THEN BI=-1
  FOR X=1 TO 3 STEP 0
    IF DR%<>R% THEN R%=R%+RI
    IF DG%<>G% THEN G%=G%+GI
    IF DB%<>B% THEN B%=B%+BI
    GOSUB RGBPAL:SLEEP J
    IF DR%=R% AND DG%=G% AND DB%=B% THEN X=3
  NEXT X
FADEDONE:
  RETURN

SWAPPALET:
  A=$FA00+(P1*2):X=$FA00+(P2*2):B1=VPEEK(1,A):B2=VPEEK(1,A+1):B3=VPEEK(1,X):B4=VPEEK(1,X+1)
  VPOKE 1,A,B3:VPOKE 1,A+1,B4:VPOKE 1,X,B1:VPOKE 1,X+1,B2:RETURN

COPYPALET:
  VPOKE 1,$FA00+(P2*2),VPEEK(1,$FA00+(P1*2)):VPOKE 1,$FA00+(P2*2)+1,VPEEK(1,$FA00+(P1*2)+1):RETURN

SETDEFPAL: 
  COUNT=0
  F$=PF$:GOSUB FILEEXISTS
  IF FE% THEN BVLOAD PF$,8,1,$FA00:RETURN
  COUNT=COUNT+1
  IF COUNT>1 THEN DM$="MISSING DPAL.BIN":GOTO ERRORHALT
  GOSUB CREATEDPALBIN:GOTO SETDEFPAL

CREATEDPALBIN:
   RESTORE PALDATA:F$="DPAL.BIN":OPEN 5,8,5,"@:"+F$+",S,W":RESTORE PALDATA: COLOR 5:FOR I=1 TO 512:READ B:PRINT# 5,CHR$(B);:NEXT I:CLOSE 5:RETURN


PALDATA:
DATA 0,0,255,15,0,8,254,10,76,12,197,0,10,0,231,14,133,13,64,6,119,15,51,3,119,7,246,10,143,0,187,11,0,0,17,1,34,2,51,3,68,4,85,5,102,6,119,7,136,8,153,9,170,10,187,11,204,12,221,13,238
DATA 14,255,15,17,2,51,4,68,6,102,8,136,10,153,12,187,15,17,2,34,4,51,6,68,8,85,10,102,12,119,15,0,2,17,4,17,6,34,8,34,10,51,12,51,15,0,2,0,4,0,6,0,8,0,10,0,12,0,15,33,2,67,4,100,6,134,8
DATA 168,10,201,12,235,15,17,2,50,4,83,6,116,8,149,10,182,12,215,15,16,2,49,4,81,6,98,8,130,10,163,12,195,15,16,2,48,4,64,6,96,8,128,10,144,12,176,15,33,1,67,3,100,5,134,7,168,9,201,11,251
DATA 13,33,1,66,3,99,4,132,6,165,8,198,9,247,11,32,1,65,2,97,4,130,5,162,6,195,8,243,9,32,1,64,2,96,3,128,4,160,5,192,6,240,7,33,1,67,3,101,4,134,6,168,8,202,9,252,11,33,1,66,2,100,3,133
DATA 4,166,5,200,6,249,7,32,0,65,1,98,1,131,2,164,2,197,3,246,3,32,0,65,0,97,0,130,0,162,0,195,0,243,0,34,1,68,3,102,4,136,6,170,8,204,9,255,11,34,1,68,2,102,3,136,4,170,5,204,6,255,7,34
DATA 0,68,1,102,1,136,2,170,2,204,3,255,3,34,0,68,0,102,0,136,0,170,0,204,0,255,0,18,1,52,3,86,4,104,6,138,8,172,9,207,11,18,1,36,2,70,3,88,4,106,5,140,6,159,7,2,0,20,1,38,1,56,2,74,2,92
DATA 3,111,3,2,0,20,0,22,0,40,0,42,0,60,0,63,0,18,1,52,3,70,5,104,7,138,9,156,11,191,13,18,1,36,3,54,4,72,6,90,8,108,9,127,11,2,1,20,2,22,4,40,5,42,6,60,8,63,9,2,1,4,2,6,3,8,4,10,5,12,6
DATA 15,7,18,2,52,4,70,6,104,8,138,10,156,12,190,15,17,2,35,4,53,6,71,8,89,10,107,12,125,15,1,2,19,4,21,6,38,8,40,10,58,12,60,15,1,2,3,4,4,6,6,8,8,10,9,12,11,15


VARINIT:
  GOSUB SETDRAWCHARS:GOSUB SETREGVARS:FMINIT:XLIMIT=319:YLIMIT=239:UC%=-1:ZCVOLUME=15:VOLUME=100

  I$="SHOWCLUE":GOSUB GETINIVALUE:IF I$="0" THEN UC%=FALSE

  Q1=1:Q2=1:Q3=1:Q4=1:XSQUISH=1:YSQUISH=1:MW%=20:MC%=35:PUTIMAGE=FALSE:LEGCOLOR=$46:BLACK2=$10

  RESTORE GAMEMENUTEXT
    FOR CX=1 TO 7
       READ S$:IF PETSCII THEN GOSUB UCASE
       MC$(CX)=S$:NEXT CX
    DI$(1)="EASY":DI$(2)="MEDIUM":DI$(3)="HARD"

   RESTORE CONTROLS:FOR I=1 TO 8:FOR C=1 TO 4:READ PC%(I,C):NEXT C:NEXT I

CONTROLS:
  DATA 32,40,96,48
  DATA 32,55,190,62
  DATA 32,72,152,80
  DATA 32,88,248,96
  DATA 32,104,160,110
  DATA 32,120,104,128
  DATA 32,136,216,144
  DATA 28,163,62,177

GAMEMENUTEXT:
  DATA "README","Edit - HANGMAN.INI","Edit PALETTES","Drawing Demo/Make Bitmaps","Load - HANGMAN","CREDITS" ,"EXIT To Commander X16"

SETDRAWPAL:
  P1=$25:P2=$FF:GOSUB COPYPALET:P1=1:P2=$FE:GOSUB COPYPALET:P1=$3B:P2=$FD:GOSUB COPYPALET:P1=$10:P2=4:GOSUB COPYPALET:P1=$1A:P2=12:GOSUB COPYPALET:RETURN

NOFLICKERSCREEN:
 POKE A.REG, MYMODE
 SYS $410
 IF MYMODE=$80 THEN RECT 0,0,319,239,0
 RETURN

POKENOFLICKER:
  RESTORE NOFLICKER
  FOR A=$410 TO $443
      READ C:POKE A,C
  NEXT A
  RETURN

## 52 BYTES
NOFLICKER:
 DATA $5A,$DA,$48,$A9,$FA,$8D,$21,$9F,$A9,$11,$8D,$22,$9F,$A9,$00,$8D
 DATA $20,$9F,$A2,$04,$A0,$80,$8D,$23,$9F,$88,$D0,$FA,$CA,$D0,$F5,$20,$DE,$FF
 DATA $85,$29,$CB,$20,$DE,$FF,$C9,$FF,$F0,$F8,$68,$18,$20,$5F,$FF,$FA,$7A,$60


SETREGVARS:
  A.REG=$030C:X.REG=$030D:Y.REG=$030E:C.REG=$030F:R0L=2:R0H=3:R0=R0L:R1L=4:R1H=5:R1=R1L
  R15L=$20:R15H=$21:R15=R15L:CHKOUT=$FFC9:CHKIN=$FFC6:CLRCHN=$FFCC:MCIOUT=$FEB1:MACPTR=$FF44
  VERA.DCVIDEO=$9F29:LAYER0.ON=16:LAYER0.OFF=239:LAYER1.ON=32:LAYER1.OFF=223:FB.CURSOR.POSITION=$FEFF:FB.CURSOR.NEXTLINE=$FF02:TRUE=-1:FALSE=0:RETURN

GETZPSTRING:
  X=$500:I=X+32:ZP.STRING$=""
ZLOOP:
  Z=PEEK(X):IF Z=0 THEN RETURN
  ZP.STRING$=ZP.STRING$ + CHR$(Z):X=X+1
  IF X < I THEN ZLOOP
  RETURN


PUTZPSTRING:
   IF LEN(ZP.STRING$)<32 AND LEN(ZP.STRING$)>0 THEN DOZPUT
   IF LEN(ZP.STRING$)=0 THEN POKE R0, 0:RETURN
   PRINT " STRING TO LONG FOR ZERO REGISTERS (MAX 31 CHARS)":END
DOZPUT:
   ZP1 = $500
   FOR I=1 TO LEN(ZP.STRING$)
       POKE ZP1, ASC(MID$(ZP.STRING$,I,1))
       ZP1=ZP1 + 1
   NEXT I
   POKE ZP1, 0
   RETURN

GETINIVALUE:
   I=1
GETILOOP:
   IF NN$(I)=I$ THEN I$=NV$(I):RETURN
   I=I+1:IF I>NV% THEN I$="":RETURN
   GOTO GETILOOP

READINI:
  NV%=0:FV%=0
  GOSUB FILEEXISTS:IF NOT FE% THEN RETURN
  FF$=F$+ ",S,R":OPEN 4,8,4,FF$
  DOINILOOP:
    LINPUT#4, S$
    IF LEFT$(S$,2)="##" THEN CONTLOOP
    IF LEFT$(S$,3)="REM" THEN CONTLOOP
    GOSUB TRIM:GOSUB UCASE
    IF LEFT$(S$,4)="REM " OR LEFT$(S$,2)="##" OR S$="" OR S$="REM" OR S$="##" THEN CONTLOOP
    PRINT ".";
    GOSUB GETINIVAR
    IF TN$="" THEN CONTLOOP
    IF TV$="ON" OR TV$="YES" THEN TV$="1"
    IF TV$="OFF" OR TV$="NO" THEN TV$="0"
    NV%=NV%+1:NN$(NV%)=TN$:NV$(NV%)=TV$
  CONTLOOP:
  IF ST=0 AND NV%<18 THEN DOINILOOP
  CLOSE 4:FV%=(NV%>0):PRINT:RETURN

GETINIVAR:
  SC$="":TN$="":TV$="":J=1
DOINIPARSE:
  X$=MID$(S$,J,1):IF X$="=" THEN GOTINI
  SC$=SC$+X$:J=J+1:IF J>LEN(S$) THEN ENDINI
  GOTO DOINIPARSE
GOTINI:
  J=J+1:TT$=S$:S$=SC$:SC$=""
  GOSUB RTRIM:TN$=S$:S$=TT$
VALUEPARSE:
  X$=MID$(S$,J,1):SC$=SC$+X$
  J=J+1:IF J>LEN(S$) THEN ENDINI
  GOTO VALUEPARSE
ENDINI:
  TT$=S$:S$=SC$:GOSUB TRIM:TV$=S$:S$=TT$:RETURN

PETCHK:
  IF ISO.ON THEN RETURN

UCASE:
  IF S$="" THEN RETURN
  X$="":FOR I=1 TO LEN(S$):I$=MID$(S$,I,1):X=ASC(I$):IF X>=97 AND X<=122 THEN X=X-32
  X$=X$+CHR$(X):NEXT I:S$=X$:RETURN

TRIM:
  GOSUB LTRIM:GOTO RTRIM

LTRIM:
   IF S$="" THEN RETURN
   I=1:C1=0
DOLOOP:
   X$=MID$(S$,I,1):X=ASC(X$):IF X > 32 THEN LSTR
   C1=C1+1:I=I+1:IF I>LEN(S$) THEN LSTR
   GOTO DOLOOP
LSTR:
   IF C1=LEN(S$) THEN S$=""
   IF C1>0 AND C1<LEN(S$) THEN S$=MID$(S$,C1+1,LEN(S$)-C1)
   RETURN

RTRIM:
  IF S$="" THEN RETURN
  I=LEN(S$):C1=0
DOLOOPR:
  X$=MID$(S$,I,1):X=ASC(X$):IF X>32 THEN RSTR
  C1=C1+1:I=I-1:IF I<1 THEN RSTR
  GOTO DOLOOPR
RSTR:
  IF C1=LEN(S$) THEN S$=""
  IF C1>0 AND C1<LEN(S$) THEN S$=MID$(S$,1,LEN(S$)-C1)
  RETURN

CLSGRAPH:
  GOSUB HIDEALL:COLOR 1,0:CLS:RECT 0,0,XLIMIT,YLIMIT,$10:RETURN

ERRORHALT:
  GOSUB CLSGRAPH:P1=5:R%=0:G%=14:B%=2:GOSUB RGBPAL:P1=15:R%=2:G%=2:GOSUB RGBPAL:GOSUB SHOWALL
  PRINT "{CR}{CR}{YEL}  ERROR ENCOUNTERED{WHT}!":PRINT ":{GRN} {CR}";S$;"{WHT}{CR}":END

CLEANUP:
  LOAD "WIPER"

THINFONT:
 IF ISO.ON THEN FONTFILE$=FONT1$:GOSUB LOADISOFONT:GOTO FONTEND
 POKE A.REG,4:GOTO FONTSYS

THICKFONT:
 IF ISO.ON THEN FONTFILE$=FONT2$:GOSUB LOADISOFONT:GOTO FONTEND
 POKE A.REG,2
FONTSYS:
 SYS $FF62
FONTEND:
 RETURN

CLEARMEM:
  FOR A=R0L TO R15H:POKE A,0:NEXT A
  FOR A=$400 TO $40A:POKE A,0:NEXT A
  RETURN
