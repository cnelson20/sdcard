  SCREEN $80:RECT 0,0,319,239,$C2
  COLOR $01:PRINT CHR$($0D);" HANGMAN INITIALIZATION...":MOUSE 1

   PRINT " CHECKING ZSMKIT"
   F$="ZSMKIT8C00.BIN":GOSUB FILEEXISTS
   IF FE% THEN PRINT " ZSMKIT FOUND"
   IF FE% THEN ADJUSTRAM
   CLR
   GOTO MAIN

ADJUSTRAM:
    PRINT " ADJUSTING RAM"
    POKE $30F,1: SYS $FF99
    POKE $30D,$00:POKE $30E,$8C
    POKE $30F,0:SYS $FF99
    CLR
    UZSM%=-1
    PRINT " LOADING ZSMKIT"
    BLOAD"ZSMKIT8C00.BIN",8,1,$8C00
    PRINT " DONE..."

MAIN:
   PRINT " INITIALIZING VARIABLES"
   DIM TC%(2,8),AL%(4,26),PC%(10,4),MC$(5),DI$(3),PL%(2,1),PL$(2)
   LS%=-1
   GOSUB VARINIT
   PRINT " DONE..";CHR$($0D);" CHECKING NECESSARY FILES"
   GOSUB CHECKFILES
   PRINT " DONE...";CHR$($0D);" BASIC RAM FREE:";FRE(0)
   SLEEP 240

   COLOR 1,0:CLS
   RECT 0,0,319,239,$3B
   RECT 3,3,316,236,$10:GOSUB INTROPAL
   DC$=CHR$(228):GOSUB HBANNER
   DC$=CHR$(162):GOSUB HBANNER
   DC$=CHR$(113):GOSUB HBANNER
   CHAR 75,165,$A9,CHR$($0C)+"THE CLASSIC WORD GAME"
   CHAR 62,180,$AB,CHR$($0C)+"NOW ON THE COMMANDER X16"
   CHAR 4,222,1,"CODED IN BASIC"
   CHAR 4,232,1,"2023..   ANTHONY HENRY"
   CHAR 185,222,1,"MUSIC CONTRIBUTED BY"
   CHAR 205,232,1,"MOOINGLEMUR"
   GOSUB STICKMAN
   GOSUB PALANIMST:GOSUB PALANIM1

MUSTART:
    RESTORE FUNMARCH:GW=0
MUCONT:
    READ PS$,DL
    IF DL=0 THEN MUSTART
    FMCHORD 0,PS$
    FOR I=1 TO DL
      GET X$
      IF X$ <>"" OR MB<>0 THEN GW=1
      GOSUB PALANIM1
    NEXT I
    IF GW=1 THEN GW=0:GOTO SCROLL
    GOTO MUCONT

SCROLL:
   FOR X=1 TO 31
        LOCATE 1,1:PRINT CHR$(145)
REPREAD:
        READ PS$,DL:IF DL=0 THEN RESTORE FUNMARCH:GOTO REPREAD
        FMCHORD 0,PS$
        FOR I = 1 TO DL:GOSUB PALANIM1:NEXT I
        GET X$:IF X$=CHR$(27) THEN X=31
    NEXT X

    IF X$=CHR$(27) THEN ENDINTRO
FINPLAY:
    READ PS$,DL
    IF DL=0 THEN ENDINTRO
    FMCHORD 0,PS$
    SLEEP DL
    GET X$:IF X$=CHR$(27) THEN ENDINTRO
    GOTO FINPLAY
ENDINTRO:
    FMINIT
    GOSUB CLSGRAPH:GOSUB SETDRAWPAL:GOTO GAMEMENU

HBANNER:
    X=5:Y=2:UC=1:WD$="H"+CHR$(193)+CHR$(206)+CHR$(199)
    GOSUB BIGWORD
    X=8:Y=11:WD$="M"+CHR$(193)+CHR$(206)
    GOSUB BIGWORD
    RETURN

PALANIMST:
    P1=13
PALANIM1:
    IF P1=2 THEN P2=13:GOTO SKIP1
    P2=P1-1
SKIP1:
    GOSUB SWAPPALET
    P1=P1-1:IF P1<2 THEN P1=13
    RETURN

STICKMAN:
    LINE 180,210,300,210,$53:LINE 180,209,299,209,$53
    LINE 300,210,300,17,$53:LINE 299,209,299,18,$53
    LINE 300,17,100,17,$53:LINE 299,18,100,18,$53:LINE 102,19,102,38,$57
    FILL=0:CC=$57:YS=.4:XS=1:RA=13:CX=102:CY=45:GOSUB BCIRCLE
    YS=1:XS=.8:CY=35:RA=10:CC=$25:FILL=1:GOSUB BCIRCLE
    CX=CX-3:RA=2:CY=CY-2:CC=2:GOSUB BCIRCLE:CX=CX+6:GOSUB BCIRCLE
    LINE 102,44,102,49,$25:LINE 102,51,102,78,$25:LINE 102,53,88,64,$25
    LINE 102,53,116,64,$25:LINE 102,78,87,99,$25:LINE 102,78,117,99,$25
    RETURN

INTROPAL:
    R%=15:G%=0:B%=2
    P1=$3B:P2=10:GOSUB COPYPALET
    P1=1:P2=11:GOSUB COPYPALET
    P1=$C7:P2=12:GOSUB COPYPALET
    P1=$A0:P2=13:GOSUB COPYPALET
    FOR I= 2 TO 9
         TC%(2,I-1) = 0
         TC%(1,I-1) = I
         P1=I:GOSUB RGBPAL:R%=R%-1
         IF I/3 = INT(I/3) THEN R%=R%-1:G%=G%+1
    NEXT I
    RETURN

GAMEMENU:
    GOSUB SETDEFPAL:GOSUB SETDRAWPAL
    DM$="HANGMAN GAME PARAMETERS":GOSUB GMENUBACK

    X=5:Y=4:CH=1:DI=2
    FOR I = 1 TO 5
        Y=Y+2:LOCATE Y,X
        COLOR 1,0:PRINT RIGHT$(STR$(I),1);
        COLOR 4:PRINT ":"MC$(I);
    NEXT I

    LOCATE 21,5:COLOR 4,0:PRINT "<";
    COLOR 1,4:PRINT "ENTER";:COLOR 4,0:PRINT ">";
    COLOR 4,0:PRINT "  CONFIRMS YOUR CHOICE";
    COLOR 4:LOCATE 25,20:PRINT "DIFFICULTY:";
    COLOR 1:PRINT DI$(DI);
    COLOR 1,4:LOCATE 24,26:PRINT "<-";:LOCATE 24,30:PRINT "->";:COLOR 4,0

FLUSHKEY1:
    GET X$:IF X$<> "" THEN FLUSHKEY1

MENUSHOW:
    IF CH=5 THEN Y=14
    IF CH=4 THEN Y=12
    IF CH=3 THEN Y=10
    IF CH=2 THEN Y=8
    IF CH=1 THEN Y=6
    LOCATE Y,5:COLOR 7,4:PRINT CHR$($7A);" ";
    COLOR 1:PRINT MC$(CH);RPT$(32,27-LEN(MC$(CH)));
    COLOR 1,0:LOCATE 25,31:PRINT RPT$(32,10);:LOCATE 25,31:PRINT DI$(DI);

MENUGET:
    GET X$
    LOCATE 26,4:PRINT RPT$(32,30);
    IF MB=1 THEN LOCATE 26,4:COLOR 1:PRINT "MX:";MX;"    ";"MY:";MY;
    IF X$="" THEN MENUGET
    K=ASC(X$)
    LOCATE Y,5:COLOR 1,0:PRINT RIGHT$(STR$(CH),1);
    COLOR 4:PRINT ":"MC$(CH);RPT$(32,28-LEN(MC$(CH)));:FMDRUM 7,26
    IF K>=49 AND K<=53 THEN CH=VAL(X$)
    IF K=145 THEN CH=CH-1
    IF K=17 THEN CH=CH+1
    IF K=157 THEN DI=DI-1
    IF K=29 THEN DI=DI+1
    IF K=13 THEN GOSUB CLSGRAPH:GOTO STARTGAME
    IF CH>5 THEN CH=1
    IF CH<1 THEN CH=5
    IF DI>3 THEN DI=1
    IF DI<1 THEN DI=3
    GOTO MENUSHOW

STARTGAME:
    IF CH=5 THEN RESET
    IF CH=4 THEN SKIPNAMES
    GOSUB GETPLAYERNAMES
SKIPNAMES:
    ROUND=1:CP%=1
LETSGO:
    IF CH=1 OR CH=2 THEN PLAYCOMP
    IF CH=3 THEN HEAD2HEAD
    IF CH=4 THEN PLAYDEMO:GOTO GAMEMENU

PLAYCOMP:
    GOSUB WORDFROMFILE:GOTO HANGMAN
HEAD2HEAD:
    GOSUB PLAYERWORD

HANGMAN:
    GOSUB GAMEBOARD
    GOSUB SCORECARD

HANDLEINPUT:
    GET X$
    IF X$=CHR$(27) THEN COLOR 1,0:CLS:GOTO GAMEMENU
    IF MB=1 THEN HNDLMOUSE
    IF X$="" THEN HANDLEINPUT
    GOTO SKIPMOUSE2

HNDLMOUSE:
    CX=MX:CY=MY:X=1:H1=0:X$="":IF CY>131 THEN X=15
NEXTBTN:
    X1=AL%(3,X):X2=X1+14:Y1=AL%(4,X):Y2=Y1+14
    IF CX>=X1 AND CX<=X2 AND CY>=Y1 AND CY<=Y2 THEN X$=CHR$(AL%(2,X)):H1=1
    IF H1=0 AND X<26 THEN X=X+1:GOTO NEXTBTN
RELEASEMOUS:
    IF MB<>0 THEN RELEASEMOUS
    IF H1 THEN SKIPMOUSE2
    LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT

SKIPMOUSE2:
    LL=ASC(X$)
    IF LL<65 OR LL>90 THEN LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT
    A=LL-64
    IF AL%(1,A)=1 THEN LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT
    CL$=CHR$(LL)
    GOSUB PUSHBTN
    HF%=0
    GOSUB CHECKLETTER
    IF HF% THEN GOSUB WORDBOX
    IF HF% THEN GOSUB DRUMROLL
    IF HF% THEN IF HW$=DW$ THEN WONROUND
    IF NOT HF% THEN GOSUB BODYPART:GOSUB PARTSCOUNT
    IF LF% THEN LOSTROUND
    SLEEP 10:GOSUB DISABLEBTN
    GOTO HANDLEINPUT

WONROUND:
    PL%(CP%,1) = PL%(CP%,1)+1
    GOSUB ADVROUND
    GOSUB CLSGRAPH
    GOTO LETSGO

LOSTROUND:
    DW$=HW$:GOSUB WORDBOX
    FMDRUM 7,38:SLEEP 10:FMDRUM 7,38
    GOSUB LOSSSCREEN
    ROUND=ROUND+1
    LF%=0:HF%=0
    GOTO LETSGO

ADVROUND:
    LF%=0:HF%=0
    IF CH=1 THEN ROUND=ROUND+1
    IF CH=1 THEN RETURN
    IF CP%=2 THEN ROUND=ROUND+1
    IF CP%=2 THEN CP%=1:RETURN
    IF CP%=1 THEN CP%=2
    RETURN

BODYPART:
    FMDRUM 7,38:FMDRUM 7,39
    LF%=0
    IF BP=8 THEN GOSUB DRAWGALLOWS
    IF BP=7 THEN GOSUB DRAWNOOSE
    IF BP=6 THEN GOSUB DRAWFACE
    IF BP=5 THEN GOSUB DRWTORSO
    IF BP=4 THEN GOSUB DRWRIGHTARM
    IF BP=3 THEN GOSUB DRWLEFTARM
    IF BP=2 THEN GOSUB DRWRIGHTLEG
    IF BP=1 THEN GOSUB DRWLEFTLEG
    GOSUB SHOWCLUE
    BP=BP-1
    IF BP=0 THEN LF%=-1
    RETURN

GAMEBOARD:
     COLOR 1,0:CLS:GOSUB SETDRAWPAL
     IF DI=1 THEN BP=8
     IF DI=2 THEN BP=7
     IF DI=3 THEN BP=6
     GOSUB BACKGROUND
     IF DI>1 THEN GOSUB DRAWGALLOWS
     IF DI=3 THEN GOSUB DRAWNOOSE
     DW$=""
     FOR I= 1 TO LEN(HW$)
         X$=MID$(HW$,I,1)
         IF ASC(X$)>=65 AND ASC(X$)<=90 THEN DW$=DW$+CHR$($7A):GOTO SKIP5
         DW$=DW$+X$
SKIP5:
     NEXT I
     GOSUB WORDBOX
     GOSUB PARTSCOUNT
     GOSUB SHOWCLUE
     GOSUB LETTERPANL
     RETURN

WORDBOX:
     COLOR 5,6:X=17+(7-(LEN(HW$)/2)):Y=5:H=3:W=LEN(HW$)+2
     GOSUB TEXTBOX:COLOR 1
     LOCATE 6,18+(7-(LEN(HW$)/2)):PRINT DW$;
     RETURN

SHOWCLUE:
     DM$=HC$:GOSUB PLAINMSG
     RETURN

CHECKLETTER:
     L=LEN(HW$)
     S$=""
     FOR X=1 TO L
        IF MID$(HW$,X,1)=CL$ THEN S$=S$+CL$:HF%=-1:GOTO SKIP8
        S$=S$ + MID$(DW$,X,1)
SKIP8:
     NEXT X
     DW$=S$:RETURN

SCORECARD:
     LOCATE 1,14:COLOR 6:PRINT PL$(CP%);
     COLOR 4:PRINT " PLAYING";
     LOCATE 2,14
     COLOR 4
     PRINT "WINS:";:COLOR 2:PRINT PL%(CP%,1);"  ";
     COLOR 4
     PRINT "LOSSES:";:COLOR 2:PRINT (ROUND-1)-PL%(CP%,1);
     COLOR 1
     LOCATE 26,25:PRINT "ROUND:";:COLOR 7:PRINT ROUND;
     RETURN

PLAYDEMO:
     RESET

DRUMROLL:
    FMDRUM 6,25
    SLEEP 10
    FMDRUM 7,25
    SLEEP 50
    FMDRUM 5,44
    SLEEP 10
    FMDRUM 6,49
    SLEEP 20
    FMDRUM 7,38
    RETURN


PLAYERWORD:
     IF CP%=1 THEN AP%=2
     IF CP%=2 THEN AP%=1
     DM$="GAME WORD AND CLUE FOR YOUR OPPONENT"
     GOSUB GMENUBACK
     DF$=""
     COLOR 13,0:LOCATE 7,3:PRINT PL$(AP%);
     COLOR 4:LOCATE 8,3:PRINT "ENTER THE FOLLOWING.";
     IY=12:IX=8:IT=1
     LOCATE 10,5:COLOR 4,0:PRINT "ENTER WORD TO PLAY";
     LOCATE 12,4:COLOR 1,0:PRINT "==";:COLOR 2,0:PRINT "> ";
PENTERWORD:
     ML=15:COLOR 7,4:GOSUB STRINGGET
     IF LEN(IS$)<>0 THEN GOTAWORD
     PRINT CHR$(7):LOCATE 25,4:PRINT "* MUST ENTER WORD *";:GOTO PENTERWORD
GOTAWORD:
     COLOR 4,0:CLS
     COLOR 13,0:LOCATE 7,3:PRINT PL$(AP%);
     COLOR 4:LOCATE 8,3:PRINT "ENTER THE FOLLOWING.";
     COLOR 4,0:HW$=IS$:LOCATE 10,5:PRINT "HANG WORD IS: ";
     COLOR 1:PRINT RPT$($7A,LEN(HW$));
     LOCATE 14,5:COLOR 4,0:PRINT "ENTER CLUE FOR WORD. (OPTIONAL)";
     ML=34:IY=17:IX=3:IT=4:COLOR 7,4:GOSUB STRINGGET
     HC$=IS$
     RETURN

GETPLAYERNAMES:
    DM$="PLAYER NAMES"
    GOSUB GMENUBACK
    DF$=""
    IY=8:IX=8:IT=4:ML=18
    LOCATE 6,5:COLOR 4,0:PRINT "PLAYER 1 NAME";
    LOCATE 8,4:COLOR 1,0:PRINT "==";:COLOR 2,0:PRINT "> ";
    COLOR 7,4:GOSUB STRINGGET
    IF IS$="" THEN IS$="PLAYER 1"
    PL$(1)=IS$
    COLOR 4,0:LOCATE 14,6:PRINT "PLAYER 1 IS: ";
    COLOR 7,0:PRINT PL$(1);
    IF CH=1 THEN DF$="":GOTO NAMESDONE
    LOCATE 6,5:COLOR 4,0:PRINT "PLAYER 2 NAME";
    LOCATE 8,4:COLOR 1,0:PRINT "==";:COLOR 2,0:PRINT "> ";
    COLOR 7,4:GOSUB STRINGGET
    IF IS$="" THEN IS$="PLAYER 2"
    PL$(2)=IS$:DF$=""
    COLOR 4,0:LOCATE 16,6:PRINT "PLAYER 2 IS: ";
    COLOR 7,0:PRINT PL$(2);
NAMESDONE:
    SLEEP 60:CLS
    RETURN


WORDFROMFILE:
      DM$="GETTING RANDOM WORD":GOSUB GMENUBACK
      LOCATE 8,4:COLOR 4,0:PRINT "WORKING...."
      IF FC%=1 THEN F=1:GOTO SKIPRF
      F=INT(RND(1)*FC%)+1
SKIPRF:
      S$=RIGHT$(STR$(F),LEN(STR$(F))-1)
      F$="WORDS/HWORDS"+S$+".DAT"+",S,R"
      OPEN 4,8,4,F$
      TL=INT(RND(1)*300)+1
      IF TL=1 THEN SKIP7
      FOR X=1 TO TL-1
          INPUT#4,X$
          INPUT#4,X$
      NEXT X
SKIP7:
      INPUT#4, HW$
      INPUT#4, HC$
      I=FRE(0)
      CLOSE 4
      RETURN

GMENUBACK:
     RECT 0,0,XLIMIT,YLIMIT,$3A
     FRAME 0,0,XLIMIT,YLIMIT,5
     RECT 4,4,XLIMIT-4,YLIMIT-4, $10
     RECT 10,15,307,214,6
     FRAME 13,27,304,211,12
     RECT 15,29,302,209,12
     CHAR 18,25,15,CHR$($0C)+DM$
     RETURN

PARTSCOUNT:
     COLOR 5,6:X=15:Y=8:H=3:W=20:GOSUB TEXTBOX
     COLOR 1:LOCATE 9,16:PRINT "BODY PARTS LEFT:";
     COLOR 7:PRINT BP;
     RETURN

PUSHBTN:
     X=AL%(3,A):Y=AL%(4,A):W=14:H=14:C1=$17:C2=1:C3=$61:C4=$10
     L$=CHR$(LL):GOSUB DRAWBTN:FMDRUM 7,26:SLEEP 5
     C1=1:C2=$17:C3=$C2:C4=$1E:GOSUB DRAWBTN
     REM
     RETURN

DISABLEBTN:
     X=AL%(3,A):Y=AL%(4,A):W=14:H=14:C1=$14:C2=$12:C3=$11:C4=$10
     L$=CHR$(LL):GOSUB DRAWBTN
     AL%(1,A)=1
     RETURN

LETTERPANL:
     L=65
     X=131:Y=97:W=14:H=14:C1=1:C2=$17:C3=$C2:C4=$1E
     FRAME 123,90,261,171,$10
     RECT 124,91,260,170,$C0:RECT 126,94,258,168,$06
     SX=X
     FOR KK = 1 TO 3
         FOR J = 1 TO 7
           L$=CHR$(L)
           GOSUB DRAWBTN
            I=L-64
            AL%(1,I)=0
            AL%(2,I)=L
            AL%(3,I)=X
            AL%(4,I)=Y
           X = X + W + 4
           L=L+1
       NEXT J
      Y = Y + W + 4:X=SX
     NEXT KK
     X = SX
     FOR JK = 1 TO 5
        L$=CHR$(L)
        GOSUB DRAWBTN
            I=L-64
            AL%(1,I)=0
            AL%(2,I)=L
            AL%(3,I)=X
            AL%(4,I)=Y
        X = X + W + 4
        L=L+1
     NEXT JK
    RETURN


DRAWBTN:
     IF H=0 THEN H=W
     X1=X+1:XW=X+W:YW=Y+H:Y1=Y+1
     Y5=YW-1:X5=XW-1
     LINE X,Y,XW,Y,C1
     LINE X1,Y1,X5,Y1,C1
     LINE XW,Y,XW,YW,C1
     LINE X5,Y1,X5,Y5,C1
     LINE X,Y,X,YW,C2
     LINE X1,Y1,X1,Y5,C2
     LINE X,YW,X,YW,C2
     LINE X,YW,X5,YW,C2
     LINE X,Y5,XW-2,Y5,C2
     RECT X+2,Y+2,XW-2,YW-2,C3
     CHAR X+3,YW-3,C4,L$
     IF H=W THEN H=0
    RETURN

BCIRCLE:
     WR=RA:X=0: D=2*(1-RA):W=INT(2*320/240)
BCLOOP:
       IF WR < 0 THEN BCLOOPEND
       DX=X*XS:DY=WR*YS
       ZX=CX-DX
       ZY=CY-DY
       AX=CX+DX
       AY=CY+DY
       IF FILL=1 THEN DOFILL
         IF ZX<0 OR ZX>XL OR ZY<0 OR ZY>YL OR Q1=0 THEN SKIP4
         PSET ZX, ZY, CC
SKIP4:
         IF AX<0 OR AX>XL OR ZY<0 OR ZY>YL OR Q2=0 THEN SKIP2
         PSET AX, ZY, CC
SKIP2:
         IF ZX<0 OR ZX>XL OR AY<0 OR AY>YL OR Q3=0 THEN SKIP3
         PSET ZX, AY, CC
SKIP3:
         IF AX<0 OR AX>XL OR AY<0 OR AY>YL OR Q4=0 THEN SKIPFILL
         PSET AX, AY, CC
       GOTO SKIPFILL
DOFILL:
       X1=ZX:X2=AX
       IF (Q1=0 AND Q2=O) OR X1>XL THEN FILL2
       IF ZY<0 OR ZY>YL THEN FILL2
       IF X1<0 THEN X1=0
       IF X2>XL THEN X2=XL
       IF Q1=0 THEN X1=CX
       IF Q2=0 THEN X2=CX
          LINE X1,ZY,X2,ZY,CC
FILL2:
       IF (Q3=0 AND Q4=0) OR ZX>XL THEN SKIPFILL
       IF AY<0 OR AY>YL THEN SKIPFILL
       IF ZX<0 THEN ZX=0
       IF AX>XL THEN AX=XL
       IF Q3=0 THEN ZX=CX
       IF Q4=0 THEN AX=CX
          LINE ZX,AY,AX,AY,CC
SKIPFILL:
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF X>D THEN X=X+1:D=D+2*X+1
       GOTO BCLOOP
BCLOOPEND:
       RETURN

CCIRCLE:
     WR=RA:G=0: D=2*(1-RA):W=INT(2*320/200)
REM  WHILE WR<0
CCLOOP:
       IF WR < 0 THEN CCDONE
         LINE CX-G,CY-WR,CX+G,CY-WR,CC
         LINE CX-G,CY+WR,CX+G,CY+WR,CC
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF G>D THEN G=G+1: D=D+2*G+1
       GOTO CCLOOP
REM WEND
CCDONE:
    RETURN

TEXTBOX:
     LOCATE Y,X
     PRINT CHR$($6F);RPT$($A3,W-2);CHR$($70);
     IF H=2 THEN SKIPMID
     FOR II = 1 TO H-2
         LOCATE Y+II,X
         PRINT CHR$($A5);RPT$(32,W-2);CHR$($A7);
     NEXT II
SKIPMID:
     LOCATE Y+H-1,X
     PRINT CHR$($6C);RPT$($A4,W-2);CHR$($BA);
     RETURN

LOSSSCREEN:
     GOSUB DEADFACE
REM EYE PULSING WHILE WAIT FOR KEY
     DM$=" HIT ANY KEY TO CONTINUE ":GOSUB PLAINMSG
     P1=$FE:G%=0:B%=0
     RESTORE FUNMARCH
KEYFLUSH2:
     GET X$:IF X$<>"" THEN KEYFLUSH2
KEYLOOP2:
     GET X$
GETMUS2:

     IF MB<>0 THEN X$="A"
EYESDONE:
     IF X$<>"" THEN ENDLOST
     R%=RND(1)*15+1
     B%=RND(1)*6
     IF MB<>0 THEN X$="A":GOTO EYESDONE
     J=5
     GOSUB PALFADE
     GOTO KEYLOOP2
ENDLOST:
     FMINIT:GOSUB CLSGRAPH:RETURN

DEADFACE:
     P1=$24:P2=$FF:J=4:GOSUB PAL2PALFADE
     P1=$3B:P2=$FE:J=4:GOSUB PAL2PALFADE
     P1=$01:J=3:GOSUB PAL2PALFADE
     P1=$3A:J=1:GOSUB PAL2PALFADE
     P1=$15:J=3:GOSUB PAL2PALFADE
     P1=$3B:J=4:GOSUB PAL2PALFADE
     P1=$23:P2=$FF:J=6:GOSUB PAL2PALFADE
     P1=$B8:J=5:GOSUB PAL2PALFADE
     P1=$BF:GOSUB PAL2PALFADE
     P1=$C0:GOSUB PAL2PALFADE
     RETURN

STRINGGET:
     GET X$:IF X$<>"" THEN STRINGGET
     IS$=DF$:ID=0
     GOSUB AREACLR
     IF IS$<> "" THEN PRINT IS$;
GETAKEY:
     GET X$:IF X$="" THEN GETAKEY
     C=ASC(X$)
     IF WL=ML THEN
     AC=0
     IF (IT<>2 OR IT=3) AND C>=65 AND C<=90 THEN AC=1
     IF (IT=2 OR IT=4 OR IT=3) AND C>=48 AND C<=57 THEN AC=1
     IF (IT=2) AND C=46 AND ID=0 THEN AC=1:ID=1
     IF (IT=4) AND C=32 THEN AC = 1: REM ALLOW SPACES WHEN INPUTTING A LINE
     IF (IT=4) AND (C>=35 AND C<=47) THEN AC=1
     IF (IT=4) AND (C>=58 AND C<=63) THEN AC=1  : REM ALLOW PUNCS IN STRING.
     IF C=13 THEN RETURN
     IF AC=1 AND LEN(IS$)<ML THEN PRINT CHR$(C);:IS$=IS$+CHR$(C):FMDRUM 7,26
     IF AC=1 AND LEN(IS$)<ML THEN GETAKEY
     IF C<>20 THEN TOLONG
     IF LEN(IS$) = 0 THEN FMDRUM 7,87
     IF LEN(IS$)=1 OR LEN(IS$)=0 THEN IS$="":GOSUB AREACLR:GOTO GETAKEY
     I = LEN(IS$)-1
     IF RIGHT$(IS$,1)="." AND IT=2 THEN ID=0
     IS$=LEFT$(IS$,I)
     GOSUB AREACLR:PRINT IS$;:FMDRUM 7,26:GOTO GETAKEY
TOLONG:
     FMDRUM 7,87:GOTO GETAKEY

AREACLR:
     LOCATE IY,IX:FOR I = 1 TO ML+1:PRINT " ";:NEXT I
     LOCATE IY,IX
     RETURN

BACKGROUND:
     F$="HANGBG.DAT":GOSUB FILEEXISTS
     IF NOT FE% THEN SKIPLOAD
     GOSUB PAL2BLACK
     BVLOAD F$,8,0,0
     GOSUB SETDEFPAL:GOSUB SETDRAWPAL:GOTO BGEND
SKIPLOAD:
     DM$="THE SKY":GOSUB DRAWMSG
     RECT 0,0, XLIMIT, YLIMIT, 14
     P1=14:P2=$10:GOSUB COPYPALET
     P1=7:P2=$F1:GOSUB COPYPALET
     P1=14:P2=7:GOSUB COPYPALET
     CX=5:CY=4:CC=$10:FILL = 1:XS=1:YS=1:RA=32
     GOSUB BCIRCLE
     CC=$07
     FILL=1:RA= 30:GOSUB BCIRCLE:FILL=0:C=7


     LINE 2,37,2,44,C:LINE 6,37,6,44,C:LINE 9,37,10,44,C
     LINE 12,36,14,43,C:LINE 15,35,17,42,C:LINE 18,34,21,41,C
     LINE 20,33,24,39,C:LINE 22,31,27,37,C:LINE 24,28,29,35,C
     LINE 27,25,32,32,C:LINE 30,23,35,29,C:LINE 32,21,37,26,C
     LINE 33,19,39,23,C:LINE 35,17,41,20,C:LINE 36,15,43,17,C
     LINE 37,12,44,14,C:LINE 38,10,45,11,C:LINE 39,7,45,8,C
     LINE 39,4,45,5,C:LINE 39,1,46,2,C

     P1=0:P2=$10:J=3:GOSUB PAL2PALFADE
     P1=$F1:P2=7:GOSUB PAL2PALFADE

     GOSUB DRWCLOUDS:GOSUB DRWCLOUDS:GOSUB DRWCLOUDS

     DM$="GRASS":GOSUB DRAWMSG
     RECT 0, 180, XLIMIT, YLIMIT, $85
     FOR Y=161 TO 179
         LINE 0,Y,40,179,105
     NEXT Y
     FOR Y=180 TO 150 STEP -1
         LINE 288,179,XLIMIT,Y,105
     NEXT Y
     COLOR 1

     FOR I=1 TO 400
         X1=INT(RND(1)*310) + 5
       NEWY:
         Y1=INT(RND(1)*60) + 180:IF Y1 > YLIMIT THEN NEWY
         GOSUB GRASSCLUMP
     NEXT I
     IF NOT LS% THEN BGEND
     DM$="SAVING BACKGROUND SCREEN":GOSUB PLAINMSG
     OPEN 2,8,2,"@:HANGBG.DAT,P,W"
     POKE$30C,2:POKE$30D,$00:POKE$30E,$96
     POKE$9F25,0:POKE$9F20,$00:POKE$9F21,$00:POKE$9F22,$10:SYS$700
     CLOSE 2
BGEND:
     RETURN

GRASSCLUMP:
     GOSUB GRASSCOLOR
     LINE X1,Y1,X1-4,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1-3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+4,Y1-5,GC
     RETURN
GRASSCOLOR:
     GC=INT(RND(1)*24)+$60:RETURN

DRWCLOUDS:
     DM$="CLOUDS":GOSUB DRAWMSG
     H = INT(RND(1)*(200))+ 45
     HL = INT(RND(1)*30)+30
     V = INT(RND(1)*30) + 6
     VB = INT(RND(1)*11) + 7
     FOR Y = V TO V+VB STEP 3
         FOR X = H TO H + HL STEP 4
           TRYCLOUD:
             RA = INT(RND(1)*5)+3
             CC = INT(RND(1)*4)+ $1C
             CX=X
             CY= INT(RND(1)*4) + (Y-4):IF (CY - RA) < 0 THEN TRYCLOUD
             GOSUB CCIRCLE
         NEXT X
     NEXT Y
     RETURN

DRAWGALLOWS:
     DM$="THE GALLOWS":GOSUB DRAWMSG
     FRAME 189,195,285,208,$10
     FRAME 188,194,285,211,$10
     LINE  189,210,285,210,$10
     PSET  189,209,$10
     PSET  284,206,$10
     PSET  284,209,$10
     FRAME 275,21,285,207,$10
     FRAME 276,22,284,205,$10
     FRAME 67,20,285,30,$10
     FRAME 68,21,284,29,$10
     RECT  71,30,79,33,$10
     RECT  190,196,283,209,$53
     RECT  277,23,283,209,$53
     RECT  69,22,283,28,83
     RETURN


DRAWNOOSE:
     DM$="THE NOOSE":GOSUB DRAWMSG
     FILL=0:XS=1
     LINE 73,33,73,53,16
     LINE 77,33,77,53,16
     RECT 74,34,76,53,87
     FOR Y=38 TO 53 STEP 3
         LINE 73, Y, 77, Y - 3, $10
     NEXT Y
     FRAME 71,53,80, 68, 16
     RECT 72,54,79,68,87
     FOR Y=56 TO 68 STEP 4
         LINE 72,Y,79, Y-4, $10
     NEXT Y

     RA=24:CX=75:CY=79:CC=$10
     YS=.38
     Q1=1:Q2=1:Q3=1:Q4=1:YS=.38
     GOSUB BCIRCLE:RA=25:GOSUB BCIRCLE
     RA=24:GOSUB BCIRCLE:RA=23:GOSUB BCIRCLE
     RA=19:GOSUB BCIRCLE:RA=18:GOSUB BCIRCLE
     CC=87
     FOR X=20 TO 23 STEP .6
         RA=X:GOSUB BCIRCLE
     NEXT X
     RETURN


DRAWFACE:
     DM$="A TROUBLED FACE":GOSUB DRAWMSG
     CX=58:CY=60:RA=6:XS=.4:YS=1:CC=$10
     Q1=1:Q2=0:Q3=1:Q4=0
     GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
     FILL=1:CC=$25:GOSUB BCIRCLE
     FILL=0:CX=90:RA=6:CC=$10
     Q1=0:Q2=1:Q3=0:Q4=1
     GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
     FILL=1:CC=$25:GOSUB BCIRCLE
     LINE 93,58,93,62,$10
     Q1=1:Q2=Q1:Q3=Q1:Q4=Q1
     LINE 67,76,67,84,$10:LINE 66,76,66,84,$10
     LINE 67,87,67,90,$10:LINE 66,87,66,90,$10:LINE 82,76,82,84,$10
     LINE 83,76,83,84,$10:LINE 82,87,82,90,$10:LINE 83,87,83,90,$10
     RECT 68,76,81,84,$25:RECT 68,89,81,90,$25

     RA=20:XS=.8:FILL=1
     CC=$10:CX=74:CY=63
     GOSUB BCIRCLE:RA = RA - 1:GOSUB BCIRCLE
     RA=RA-2:CY=CY+1:CC=$FF:FILL=1:GOSUB BCIRCLE
     XS=1:YS = XS

     CY=CY-6:CX = CX-6:RA=3:CC=$FE:GOSUB CCIRCLE
     PSET CX+4,CY,$FF:GOSUB PUPILS

     CX=CX+12:GOSUB CCIRCLE
     PSET CX+4,CY,$FF:GOSUB PUPILS:CX = 74:CY = 63
     LINE CX-1,CY, CX-2, CY + 6, $10:LINE CX+1,CY,CX+2,CY+6,$10
     LINE CX,CY,CX-1, CY + 6, $23:LINE CX,CY,CX+1,CY+6,$23
     LINE CX,CY+3,CX,CY+6,$22
     Q3=0:Q4=0:CC=$10:CY=CY+13:YS=.35:RA=6:FILL=0
     GOSUB BCIRCLE
     CY=CY+1:CC=$31:GOSUB BCIRCLE
     CY=CY+1:CC=$10:GOSUB BCIRCLE
     Q3=1:Q4=1:YS=1
     RETURN

PUPILS:
     PSET CX,CY,$10:PSET CX-1,CY,$10:PSET CX,CY+1,$10:PSET CX-1,CY+1,$10
     RETURN

DRWTORSO:
      DM$="TORSO":GOSUB DRAWMSG
      LINE 82,90,105,93,$10:LINE 83,91,105,94,$10
      LINE 68,90,42,93, $10:LINE 69,91,42,94, $10
      LINE 67,90,74,105,$10:LINE 83,90,74,105,$10
      FOR X=81 TO 68 STEP -1
          LINE 75,103,X, 90, $25
      NEXT X
      LINE 74,105,72,110,$10:LINE 72,110,72,141,$10
      LINE 72,141,53,144,$10:LINE 53,144,53,108,$10
      RECT 71,140,54,103,$08:LINE 54,141,68,141,$08
      LINE 54,142,62,142,$08:LINE 54,143,56,143,$08
      PSET 65,91,$10
      LINE 72,102,72,108,$08:LINE 73,104,73,106,$08
      PSET 74,109,$08
      RECT 47,107,68,94,$08:RECT 69,96,69,99,$08
      RECT 42,107,48,95,$08:LINE 56,93,67,93, $08:PSET 55,92,$10:PSET 64,91,$10
      LINE 65,92,67,92, $08:RECT 68,100,71,102,$08:LINE 70,98,70,100, $08
      RECT 73,110,91,140,$08:RECT 75,105,91,109,$08:RECT 88, 94, 101, 106, $08
      RECT 81, 95, 105, 106, $08:LINE 80, 96, 80, 105, $08
      LINE 79,98,79,105, $08:LINE 78,100,78,105,$08:LINE 77,101,77,105,$08
      LINE 76,103,76,105,$08:LINE 74,103,75,103, $10
      PSET 76,101,$10:PSET 73,101,$10:PSET 72,99,$10:PSET 71,97,$10
      PSET 70,95,$10:PSET 69,93,$10:PSET 81,92,$10:PSET 80,94,$10
      PSET 80,96,$10:PSET 78,97,$10:PSET 77,99,$10
      LINE 76,100,76,103,$10:RECT 82, 94, 93, 93, $08:LINE 83, 92, 86, 92, $08
      LINE 74, 107,74, 108,$08:PSET 73,109,$10:PSET 73,141,$10:LINE 76,141,91,141,$08
      LINE 81,142,91,142,$08:LINE 89,143,91,143,$08:LINE 74,141,92,144,$10
      LINE 92,144,92,107,$10:PSET 73,141,$10
      CC = 16:RA = 1.2:CX=75:FILL=1
      FOR CY=113 TO 143 STEP 8
          GOSUB BCIRCLE
      NEXT CY
      LINE 58,108,68,108,$10:LINE 58,108,58,116,$10
      LINE 68,108,68,116,$10:Q1=0:Q2=0:Q3=1:Q4=1:RA=4.5:XS=1:YS=.6
      CX=63:CY=116:CC=$10:FILL=0
      GOSUB BCIRCLE
      X$="P-"+RIGHT$(STR$(CP%),LEN(STR$(CP%))-1)
      CHAR 57,106,$10,X$
      RETURN

DRWLEFTARM:
      Q1=0:Q2=1:Q3=0:Q4=0:FILL = 0
      DM$="LEFT ARM":GOSUB DRAWMSG
      XSQUISH=.52
      RA=16:CC=$10
      CY=116:CX=92
      GOSUB BCIRCLE:RA=RA+.6
      GOSUB BCIRCLE
      RA=RA-1:GOSUB BCIRCLE
      XSQUISH=.28:CX = 106:CY = 105:FILL=0
      GOSUB BCIRCLE
      FOR L=1 TO 3:RA=RA+.5:GOSUB BCIRCLE:NEXT L
      CC=$08:Q1=0:Q2=1:Q3=0:Q4=0:FILL=1:RA=RA-2.5
      GOSUB BCIRCLE
      RECT CX-1,CY-3,CX+2,CY-7,$08
      LINE 99,113,99,133,$10
      LINE 111,102,111,133,$10
      RECT 100,102,110,133,$08
      LINE 99,107,99,109,$08
      LINE 98,108,101,108,$08
      LINE 97,107,100,107,$08 :PSET 94,107,$0E
      LINE 99,134,111,134,$10
      LINE 101,134,101,143,$10:LINE 101,143,103,143,$10
      LINE 102,142,102,139,$25:LINE 103,144,103,140,$10
      LINE 103,145,105,145,$10:LINE 104,144,104,139,$25
      LINE 105,146,105,140,$10:LINE 105,146,107,146,$10
      LINE 106,145,106,139,$25:LINE 107,146,107,140,$10
      LINE 107,145,109,145,$10:LINE 108,144,108,139,$25
      LINE 109,145,109,134,$10:RECT 102,139,108,135,$25
      RETURN

DRWRIGHTARM:
      Q1=1:Q2=0:Q3=0:Q4=0:FILL=0
      XSQUISH=.52
      RA=16:CC=$10
      CY=118:CX=54
      DM$="RIGHT ARM":GOSUB DRAWMSG
      GOSUB BCIRCLE:RA=RA+.6
      GOSUB BCIRCLE
      RA=RA-1:GOSUB BCIRCLE
      LINE 54,107,54,109,$08
       XSQUISH=.43:CX = 42:CY = 105:FILL=0
       GOSUB BCIRCLE
       FOR L=1 TO 3:RA=RA+.3:GOSUB BCIRCLE:NEXT L
       CC=$08:Q1=1:Q2=0:Q3=0:Q4=0:FILL=1:RA=RA-3
       GOSUB BCIRCLE:FILL=0
       RECT CX-4,CY-8,CX+3,CY+6,$08
       RECT CX-4,CY-4,CX,CY+6,$08
       LINE 34,102,34,133,$10
       LINE 46,115,46,133,$10
       RECT 35,102,45,133,$08
       LINE 34,134,46,134,$10
       LINE 46,CY,46,CY+6,$08
       LINE 47,CY,47,CY+5,$08
       RECT 47,CY,49,CY+3,$08
       LINE 51,CY+4,52,CY+4,$0E
       PSET 48,CY+4, $08

      LINE 44,134,44,143,$10:LINE 44,143,42,143,$10
      LINE 43,142,43,139,$25:LINE 42,140,42,145,$10
      LINE 42,145,40,145,$10:LINE 41,144,41,139,$25
      LINE 40,146,40,140,$10:LINE 40,146,38,146,$10
      LINE 39,145,39,139,$25:LINE 38,146,38,140,$10
      LINE 38,145,36,145,$10:LINE 37,144,37,139,$25
      LINE 36,145,36,134,$10:RECT 43,139,37,135,$25
      RETURN

DRWRIGHTLEG:
      DM$="RIGHT LEG":GOSUB DRAWMSG
      Q1=1:Q2=0:Q3=0:Q4=0:FILL=0
      CC=$10:CX=74:CY=159:RA=8:XS=.43:YS=.6
      GOSUB BCIRCLE
      LINE 70,159,70,192,$10:LINE 55,145,53,192,$10
      LINE 53,193,70,193,$10:RECT 56,145,69,192,$46
      LINE 55,157,55,192,$46:LINE 54,181,54,192,$46
      LINE 57,144,71,144,$46:RECT 69,142,71,154,$46
      RECT 72,142,73,153,$46:LINE 63,143,68,143,$46:PSET 70,155,$46

      Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XS=.35:CC=$10:FILL=1:CX=62:CY=208
      GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1
      XS=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE
      RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
      LINE 57,193,57,202,$10:LINE 66,193,66,202,$10
      RECT 58,194,65,200,$1C
      RETURN


DRWLEFTLEG:
      Q1=0:Q2=1:Q3=0:Q4=0
      CC=$10:CX=74:CY=159:RA=8
      DM$="LEFT LEG":GOSUB DRAWMSG:GOSUB BCIRCLE
      LINE 77,157,77,192,$10:LINE 90,145,92,192,$10
      LINE 77,193,92,193,$10:RECT 78,145,89,192,$46
      LINE 90,157,90,192,$46:LINE 91,181,91,192,$46
      LINE 74,144,88,144,$46:RECT 76,143,79,155,$46
      RECT 74,142,76,153,$46:LINE 74,143,82,143,$46
      LINE 77,154,77,156,$46

      Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XS=.35:CC=$10:FILL=1
      CX=85:CY=208
      GOSUB BCIRCLE
      RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1
      XS=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE
      RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
      LINE 80,193,80,202,$10:LINE 89,193,89,202,$10
      RECT 81,194,88,200,$1C
      RETURN

FILEEXISTS:
      X$=F$:FE%=0:X$=X$+",S,R"
      OPEN 2,8,2,X$
      CLOSE 2
      OPEN 15,8,15
      INPUT#15,X,X$
      CLOSE 15
      FE%=(X<=20)

      RETURN

DRAWMSG:
      DM$=" DRAWING: "+DM$
PLAINMSG:
      COLOR 1,0:GOSUB CLRMSG
      LOCATE 30,1:PRINT DM$;
      RETURN
CLRMSG:
      LOCATE 30,1:PRINT RPT$(32,39);
      RETURN

BIGWORD:
      L=LEN(WD$):SX = X
      OB=PEEK(1):BANK PEEK(0),6
      FOR K=1 TO L
         CC=ASC(MID$(WD$,K,1))
         IF (CC>=64 AND CC<=90) OR (CC>=193 AND CC<=218) THEN AA=64:GOTO SKIP0
         AA=0
SKIP0:
         CA=$C000 + 8*(CC-AA)
         FOR I=1 TO 8
             CM(I) = PEEK(CA+(I-1)):CM$(I)=""
         NEXT I
         IF DC$="" THEN DC$=CHR$(CC)
          FOR J=1 TO 8
             RESTORE BITMAPS
             FOR CT=1 TO 8
                READ CP
                IF (CP AND CM(J)) THEN CM$(J)=CM$(J)+DC$:GOTO SKIPSPC
                CM$(J)=CM$(J)+CHR$(32)
          SKIPSPC:
             NEXT CT
             LOCATE Y+(J-1),X:IF UC=1 THEN COLOR TC%(1,J),TC%(2,J)
             PRINT CM$(J);
          NEXT J
          IF DC$=CHR$(CC) THEN DC$=""
          X=X+8
      NEXT K
      X=SX
      BANK PEEK(0),OB
      RETURN

RGBPAL:
      VPOKE 1,$FA00+(P1*2),(G%*16) + B%:VPOKE 1,$FA00+((P1*2)+1),R%
      RETURN

READPAL:
      A1=$FA00+(P1*2)
      R%=VPEEK(1,A1+1)
      GB%=VPEEK(1,A1)
      G%=GB%/16
      B%=GB% AND $0F
      RETURN

PAL2PALFADE:
      GOSUB READPAL
      P3=P1
      P1=P2
      GOSUB PALFADE
      P1=P3
      RETURN

PALFADE:
      DR%=R%:DG%=G%:DB%=B%
      GOSUB READPAL
      RI=1:IF DR%<R% THEN RI=-1
      GI=1:IF DG%<G% THEN GI=-1
      BI=1:IF DB%<B% THEN BI=-1
INCRGB:
      IF DR%<>R% THEN R%=R%+RI
      IF DG%<>G% THEN G%=G%+GI
      IF DB%<>B% THEN B%=B%+BI
      GOSUB RGBPAL
      SLEEP J
      IF DR%=R% AND DG%=G% AND DB%=B% THEN FADEDONE
      GOTO INCRGB
FADEDONE:
      RETURN

SWAPPALET:
      A1=$FA00+(P1*2):A2=$FA00+(P2*2)
      B1=VPEEK(1,A1):B2=VPEEK(1,A1+1):B3=VPEEK(1,A2):B4=VPEEK(1,A2+1)
      VPOKE 1,A1,B3:VPOKE 1,A1+1, B4
      VPOKE 1,A2,B1:VPOKE 1,A2+1, B2
      RETURN

COPYPALET:
      VPOKE 1,$FA00+(P2*2),VPEEK(1,$FA00+(P1*2))
      VPOKE 1,$FA00+(P2*2)+1,VPEEK(1,$FA00+(P1*2)+1)
      RETURN

PAL2BLACK:
      FOR A=$FA00 TO $FBFF:VPOKE 1,A,0:NEXT A
      RETURN

SETDEFPAL: 
      BVLOAD "DPAL.BIN",8,1,$FA00:RETURN

BITMAPS:
      DATA 128,64,32,16,8,4,2,1,0

FUNMARCH:
      DATA "O4V63I0CO3V50I18CV50I18E-V50I18G",60,"O4CO3E-G-B-",45,"O4C",15
      DATA "O4CO3CE-G",60,"RE-G-B-",60,"O4CO3CE-G",60,"O4CO3E-G-B-",45
      DATA "O4C",15,"O4CO3CE-G",60,"RE-G-B-",60,"O4CO3CE-G",60
      DATA "O4CO3E-G-B-",45,"O4C",15,"O4CO3CE-G",60,"O4E-O3E-G-B-",45,"O4D",12
      DATA "E-",3,"O4DO3CE-G",45,"O4C",15,"O4CO3E-G-B-",45,"O4C",15
      DATA "O4CO3CE-G",60,"RE-G-B-",60,"",0

VARINIT:
      X=RND(-TI):FMINIT:XLIMIT=319:YLIMIT=239
      GOSUB SETDEFPAL

      POKE$30C,4:SYS$FF62
      Q1=1:Q2=1:Q3=1:Q4=1:XS=1:YS=1
      MW%=20:MC%=35
      GOSUB INITALPHA

      MC$(1)="PLAYER VS COMPUTER"
      MC$(2)="2 PLAYER. COMPUTER PICKS"
      MC$(3)="HEAD TO HEAD"
      MC$(4)="DEMO MODE  (NOT AVAILABLE)"
      MC$(5)="EXIT TO COMMANDER X16"

      DI$(1)="EASY"
      DI$(2)="MEDIUM"
      DI$(3)="HARD"

SETDRAWPAL:
      P1=$25:P2=$FF:GOSUB COPYPALET
      P1=1:P2=$FE:GOSUB COPYPALET
      P1=$3B:P2=$FD:GOSUB COPYPALET
      P1=$C0:P2=5:GOSUB COPYPALET
      P1=$10:P2=4:GOSUB COPYPALET
      P1=$1A:P2=12:GOSUB COPYPALET
      RETURN

INITALPHA:
      FOR I=65 TO 90
        AL%(1,I-64)=0:AL%(2,I-64)=I
      NEXT I
      RETURN

CHECKFILES:
      F$="BVSAVE700.BIN":GOSUB CMSG
      GOSUB FILEEXISTS
      IF FE% THEN PRINT " LOADING ";F$
      IF FE% THEN BLOAD "BVSAVE700.BIN",8,1,$0700
      LS%=FE%
      F$="DPAL.BIN":GOSUB CMSG
      GOSUB FILEEXISTS
      IF FE% THEN COUNTEM
      DM$=" MISSING DPAL.BIN"
      GOTO ERRORHALT
COUNTEM:
      GOSUB COUNTWORDFILES
      IF FC% >= 1 THEN NFILE2
      DM$=" HWORD*.DAT FILES ARE MISSING"
      GOTO ERRORHALT
NFILE2:
      RETURN

COUNTWORDFILES:
    FC%=0
CNTINC:
    FC%=FC%+1
    SF$=RIGHT$(STR$(FC%),LEN(STR$(FC%))-1)
    SF$=SF$+".DAT"
    F$="WORDS/HWORDS"+SF$
    GOSUB FILEEXISTS
    IF FE% THEN PRINT " FOUND:";:GOSUB CMSG:GOTO CNTINC
    FC%=FC%-1
    PRINT " ";FC%;" WORD FILES FOUND!"
    RETURN

CMSG:
   PRINT " ";F$:RETURN

AWAITKEY:
      GET X$:IF X$<>"" THEN AWAITKEY
GETIT:
      GET X$:IF X$="" AND MB=0 THEN GETIT
      RETURN

CLSGRAPH:
      COLOR 1,0:CLS
      RECT 0,0,XLIMIT,YLIMIT,$10
      RETURN

ERRORHALT:
     P1=5:R%=0:G%=14:B%=2
     GOSUB RGBPAL
     P1=15:R%=2:G%=2
     GOSUB RGBPAL
     COLOR 5,15:PRINT:PRINT
     PRINT " ERROR ENCOUNTERED"
     PRINT " ";DM$
     END

CLEANUP:
     GOSUB AWAITKEY:GOSUB SETDEFPAL:RESET


