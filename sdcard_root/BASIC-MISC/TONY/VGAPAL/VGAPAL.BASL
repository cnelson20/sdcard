SCREEN 1:COLOR 1,0:CLS
PRINT:PRINT

   SCREEN $80
   GOSUB SETDEFPAL
   DEF FNRO(X) = INT(X+.5)
   C = 0:TC=1
   FOR Y = 0 TO 225 STEP 15
     FOR X = 0 TO 300 STEP 20
        RECT X, Y, X+19, Y + 14, C
        FRAME X, Y, X+19, Y + 14, 0
        REM CHAR X+4,Y+9,TC,HEX$(C)
        C = C + 1
      NEXT X
    NEXT Y
    PRINT
    COLOR 1,11:PRINT "HIT ANY KEY TO PROCEED";
    GOSUB FLUSHGET
    COLOR 1,0:CLS

    OPEN 4,8,4,"VGAPALETTE,S,R"
PALENTRY:
    INPUT#4,P1,RM,GM,BM
    R% = FNRO((RM/100)*15)
    G% = FNRO((GM/100)*15)
    B% = FNRO((BM/100)*15)
    GOSUB RGBPAL
    IF P1<255 THEN PALENTRY
    CLOSE 4

    F$="MODE13H.PAL"
    GOSUB SAVEPAL2FILE

    GOSUB FLUSHGET

    GOSUB SETDEFPAL
    SCREEN 1
    PRINT:PRINT "DONE":PRINT:PRINT
    END

FLUSHGET:
   GET X$:IF X$ <> "" THEN FLUSHGET
GETAKEY:
   GET X$:IF X$="" THEN GETAKEY
   RETURN





REM SET OF GOSUBS FOR DIRECT VERA PALLETTE MANIPULATION
REM USES VPOKE & VPEEK
REM FOR THE OTHER ROUTINES TO WORK RELIABLY SHOULD GOSUB 45000
REM FIRST.    ALSO GOSUB 45000 IS USED AT ANY TIME TO RESET
REM THE VERA PALLETTE TO DEFAULT VALUES.
REM ..
REM ...
REM WHEN THE VERA IS INITIALLY SET TO GRAPHICS MODE IT USES THE
REM DEFAULT PALLETTE BUT IF YOU USE VPEEK TO READ IT RETURNS
REM RANDOM GARBAGE...    AFTER YOU VPOKE THEM IT ALL WORKS
REM AS EXPECTED.
REM .
REM ..
REM ...
REM THIS CODE DOES ABSOLUTELY NO ERROR CHECKING !!!!!!
REM IF USING R%,G% OR B% VALUES SHOULD ALWAYS BE BETWEEN
REM 0-15
REM .
REM ..
REM ...
REM WHEN USING P1 AND P2 TO POINT AT PALLETTE ENTRYS THEY SHOULD
REM ALWAYS BE BETWEEN 0 AND 255
REM I HAD ERROR CHECKING CODE WHEN WRITING THESE ROUTINES
REM BUT ERROR CHECKING TAKES CPU CYCLES....   JUST KEEP THE VALUES
REM CORRECT BEFORE CALLING.
REM .
REM ..
REM ...
REM ..
REM GOSUB SETDEFPAL..   POKES IN THE DEFAULT PALLETTE VALUES
REM                 SHOULD BE CALLED AT INIT.   AND THEN ANY TIME
REM                 TO RESET THE VERA TO DEFAULT VALUES
REM ..
REM GOSUB RGBPAL..   SETS PALLETTE ENTRY P1 TO R%,G%,B%
REM ..
REM GOSUB READPAL..   READS VALUES AT P1 RETURNS IN R%,G%,B%
REM ..
REM GOSUB PAL2PALFADE..   CHANGES THE PALLETTE COLOR P2 TO P1. BUT DIFFERENT THAN 41000
REM                       41000 CHANGES IT FAST.   40200 IMPLEMENTS A FADE WITH SPEED
REM                       CONTROLLED BY THE VALUE OF J (BIGGER VALUE=SLOWER)
REM                       ACTUALLY MOST OF THE WORK DONE BY GOSUB TO 40300
REM ..
REM GOSUB PALFADE   FADES PALLETTE COLOR AT P1 TO R%,G%,B%
REM                 SPEED CONTROLLED BY THE VALUE OF J.    1 TO 10
REM                 IS SO FAR BEST FOR ME BUT I DIDN'T RESTRICT VALUES
REM                 THE BIGGER J IS.. THE SLOWER THE FADE.
REM ..
REM GOSUB SWAPPAL     SWAPS THE PALLETTE VALUES BETWEEN P1 AND P2
REM                   IF AND ITS A BIG IF...  I MAKE FURTHER CHANGES
REM                   THE NEXT ROUTINE I MAKE WILL BE TO DO THIS AS A
REM                  ..FADE.. USING A SIMILIAR ALGORITHM I USED IN GOSUB 40300
REM ..
REM GOSUB COPYPAL   COPYS PALLETTE ENTRY P1 TO P2.   P1 IS UNCHANGED
REM ..
REM ..
REM GOSUB PAL2BLACK ... SETS THE PALLETTE TO ALL BLACK.   ABOUT THE RIGHT SPEED FOR A FADE SCREEN


REM SET PALLETTE ENTRY P1 TO R%,G%,B%
RGBPAL:
      A=P1*2:GB%=(G%*16)+B%
      A=A+$FA00
      SLEEP:VPOKE 1,A,GB%
      A=A+1
      SLEEP:VPOKE 1,A,R%
      RETURN

REM READ PALLETTE ENTRY AT P1
READPAL:
      A1=$FA00+(P1*2)
      R%=VPEEK(1,A1+1)
      GB%=VPEEK(1,A1)
      G%=GB%/16
      B%=GB% AND $0F
      RETURN

REM FADE P2 FROM CURRENT COLOR TO P1 COLOR, J IS JIFFY DELAY
PAL2PALFADE:
      GOSUB READPAL
      P3=P1:P1=P2
      GOSUB PALFADE
      P1=P3
      RETURN

REM FADE P1 TO R%,G%,B%, J IS JIFFY DELAY
PALFADE:
      DR%=R%:DG%=G%:DB%=B%
      GOSUB READPAL
      RI=1:IF DR%<R% THEN RI=-1
      GI=1:IF DG%<G% THEN GI=-1
      BI=1:IF DB%<B% THEN BI=-1
INCRGB:
      IF DR%<>R% THEN R%=R%+RI
      IF DG%<>G% THEN G%=G%+GI
      IF DB%<>B% THEN B%=B%+BI
      GOSUB RGBPAL
      SLEEP J
      IF DR%=R% AND DG%=G% AND DB%=B% THEN FADEDONE
      GOTO INCRGB
FADEDONE:
      RETURN

REM SWAP PALLETTE COLORS AT P1 & P2
SWAPPAL:
      A1=$FA00+(P1*2):A2=$FA00+(P2*2)
      B1=VPEEK(1,A1):B2=VPEEK(1,A1+1):B3=VPEEK(1,A2):B4=VPEEK(1,A2+1)
      VPOKE 1,A1,B3:VPOKE 1,A1+1, B4
      VPOKE 1,A2,B1:VPOKE 1,A2+1, B2
      RETURN

REM COPY P1 PALLETTE ENTRY TO P2.. P1 IS LEFT UNCHANGED.
COPYPAL:
      VPOKE 1,$FA00+(P2*2),VPEEK(1,$FA00+(P1*2))
      VPOKE 1,$FA00+(P2*2)+1,VPEEK(1,$FA00+(P1*2)+1)
      RETURN

REM SET THE PALLETTE TO ALL BLACK
PAL2BLACK:
    FOR A=$FA00 TO $FBFF:VPOKE 1,A,0:NEXT A:RETURN


REM SET THE VERA DEFAULT PALETTE USE FILE LOAD IF THERE.
SETDEFPAL:
      F$="DPAL.BIN":GOSUB FILEEXISTS
      IF FE% THEN BVLOAD "DPAL.BIN",8,1,$FA00:RETURN
      RESTORE PALDATA
REM LOOP THROUGH THE PALLETTE ADDRESS SPACE
REM AND POKE THE VERA DEFAULT PALLETTE
      FOR PE = $FA00 TO $FBFE STEP 2
          READ R:READ GB
          VPOKE 1,PE,GB
          VPOKE 1,PE+1, R
      NEXT PE
      RETURN

SAVEPAL2FILE:
     FF$="@:"+F$+",S,W"
     OPEN 5,8,2,FF$
     FOR PE=$FA00 TO $FBFE STEP 2
         GB=VPEEK(1,PE)
          R=VPEEK(1,PE+1)
         PRINT#5,CHR$(GB);
         PRINT#5,CHR$(R);
     NEXT PE
     CLOSE 5
     RETURN

REM DEFAULT VERA PALLETTE AS DATA. FROM 0 TO 255 (2 BYTES EACH ENTRY)RGB
PALDATA:
      DATA $00,$00,$0F,$FF,$08,$00,$0A,$FE,$0C,$4C,$00,$C5,$00,$0A,$0E,$E7,$0D
      DATA $85,$06,$40,$0F,$77,$03,$33,$07,$77,$0A,$F6,$00,$8F,$0B,$BB,$00,$00
      DATA $01,$11,$02,$22,$03,$33,$04,$44,$05,$55,$06,$66,$07,$77,$08,$88,$09
      DATA $99,$0A,$AA,$0B,$BB,$0C,$CC,$0D,$DD,$0E,$EE,$0F,$FF,$02,$11,$04,$33
      DATA $06,$44,$08,$66,$0A,$88,$0C,$99,$0F,$BB,$02,$11,$04,$22,$06,$33,$08
      DATA $44,$0A,$55,$0C,$66,$0F,$77,$02,$00,$04,$11,$06,$11,$08,$22,$0A,$22
      DATA $0C,$33,$0F,$33,$02,$00,$04,$00,$06,$00,$08,$00,$0A,$00,$0C,$00,$0F
      DATA $00,$02,$21,$04,$43,$06,$64,$08,$86,$0A,$A8,$0C,$C9,$0F,$EB,$02,$11
      DATA $04,$32,$06,$53,$08,$74,$0A,$95,$0C,$B6,$0F,$D7,$02,$10,$04,$31,$06
      DATA $51,$08,$62,$0A,$82,$0C,$A3,$0F,$C3,$02,$10,$04,$30,$06,$40,$08,$60
      DATA $0A,$80,$0C,$90,$0F,$B0,$01,$21,$03,$43,$05,$64,$07,$86,$09,$A8,$0B
      DATA $C9,$0D,$FB,$01,$21,$03,$42,$04,$63,$06,$84,$08,$A5,$09,$C6,$0B,$F7
      DATA $01,$20,$02,$41,$04,$61,$05,$82,$06,$A2,$08,$C3,$09,$F3,$01,$20,$02
      DATA $40,$03,$60,$04,$80,$05,$A0,$06,$C0,$07,$F0,$01,$21,$03,$43,$04,$65
      DATA $06,$86,$08,$A8,$09,$CA,$0B,$FC,$01,$21,$02,$42,$03,$64,$04,$85,$05
      DATA $A6,$06,$C8,$07,$F9,$00,$20,$01,$41,$01,$62,$02,$83,$02,$A4,$03,$C5
      DATA $03,$F6,$00,$20,$00,$41,$00,$61,$00,$82,$00,$A2,$00,$C3,$00,$F3,$01
      DATA $22,$03,$44,$04,$66,$06,$88,$08,$AA,$09,$CC,$0B,$FF,$01,$22,$02,$44
      DATA $03,$66,$04,$88,$05,$AA,$06,$CC,$07,$FF,$00,$22,$01,$44,$01,$66,$02
      DATA $88,$02,$AA,$03,$CC,$03,$FF,$00,$22,$00,$44,$00,$66,$00,$88,$00,$AA
      DATA $00,$CC,$00,$FF,$01,$12,$03,$34,$04,$56,$06,$68,$08,$8A,$09,$AC,$0B
      DATA $CF,$01,$12,$02,$24,$03,$46,$04,$58,$05,$6A,$06,$8C,$07,$9F,$00,$02
      DATA $01,$14,$01,$26,$02,$38,$02,$4A,$03,$5C,$03,$6F,$00,$02,$00,$14,$00
      DATA $16,$00,$28,$00,$2A,$00,$3C,$00,$3F,$01,$12,$03,$34,$05,$46,$07,$68
      DATA $09,$8A,$0B,$9C,$0D,$BF,$01,$12,$03,$24,$04,$36,$06,$48,$08,$5A,$09
      DATA $6C,$0B,$7F,$01,$02,$02,$14,$04,$16,$05,$28,$06,$2A,$08,$3C,$09,$3F
      DATA $01,$02,$02,$04,$03,$06,$04,$08,$05,$0A,$06,$0C,$07,$0F,$02,$12,$04
      DATA $34,$06,$46,$08,$68,$0A,$8A,$0C,$9C,$0F,$BE,$02,$11,$04,$23,$06,$35
      DATA $08,$47,$0A,$59,$0C,$6B,$0F,$7D,$02,$01,$04,$13,$06,$15,$08,$26,$0A
      DATA $28,$0C,$3A,$0F,$3C,$02,$01,$04,$03,$06,$04,$08,$06,$0A,$08,$0C,$09
      DATA $0F,$0B


REM SET F$ TO FILENAME:GOSUB FILEEXISTS,FE% IS -1 IF FILE IS THERE.
FILEEXISTS:
      X$=F$:FE%=0:X$=X$+",S,R"
      OPEN 2,8,2,X$
      CLOSE 2
      OPEN 15,8,15
      INPUT#15,X,X$
      CLOSE 15
      FE%=(X<=20)
      RETURN


