5 REM EXAMPLE PROGRAM FOR INTERACTING WITH KERNAL CALLS FROM BASIC
6 REM IN THIS EXAMPLE, HOW TO REQUEST FOR KEY MODIFIERS

10 POKE $030F,1:REM SET .C TO 1 (MEANS "GET" FOR UPCOMING KERNAL CALL)
20 SYS  $FF5F  :REM JSR $FF5F GET SCREEN MODE (TO RESTORE IT LATER)
25 OS=PEEK($030C):REM SAVE REG .A (SAVE CURRENT "ORIGINAL" SCREEN MODE)

30 REM SET UP DIMS AT TOP (CAN'T REDIM LATER)
30 DIM KM(6):REM HOLD KEYBOARD MODIFIES

45 SCREEN 7:CLS

46 REM ---- START MAIN LOOP PROCESSING
50 GET X$:X=ASC(X$)
60 REM NOTE: CAN NOT DO "GET ASC(X$)" OR "X=ASC(GET X$)"

70 SYS $FEC0:REM JSR $FEC0 GET_MODIFIERS (NO REG SETUP NEEDED)
80 KM=PEEK($030C):REM READ .A REG RESULT FROM KERNAL CALL
90 REM KM = KEY MODIFIER RESULT

110 LOCATE 2,1
115 REM ASSUME OFF(0) UNLESS DETECTED
120 KM(0)=0:IF (KM AND  1) THEN KM(0)=-1:PRINT "SHIFT";:GOTO 130
125 PRINT "     ";:REM X16 BASIC V2 "IF" HAS NO "ELSE" SUPPORT
130 KM(1)=0:IF (KM AND  2) THEN KM(1)=-1:PRINT "ALT1";:GOTO 140
135 PRINT "    ";:REM ELSE CLEAR "ALT1"
140 KM(2)=0:IF (KM AND  4) THEN KM(2)=-1:PRINT "CTRL";:GOTO 150
145 PRINT "    ";:REM ELSE CLEAR "CTRL"
150 KM(3)=0:IF (KM AND  8) THEN KM(3)=-1:PRINT "ALT2";:GOTO 160
155 PRINT "    ";:REM ELSE CLEAR "ALT2"
160 KM(4)=0:IF (KM AND 16) THEN KM(4)=-1:PRINT "CAPS";:GOTO 170
165 PRINT "    ";:REM ELSE CLEAR "CAPS"
170 LOCATE 3,1:?"     ";:LOCATE 3,1:? X

175 IF X=0 THEN GOTO 50:REM WAIT FOR A KEY PRESS

180 REM  ELSE X<>0 ... SHOW VALUE OF KEY PRESSED
190 LOCATE 5,1:PRINT "    ";:REM ERASE OLD VALUE (INC. SIGN)
200 LOCATE 5,1:PRINT X;
210 IF (X=133) AND (KM(2)<>0) THEN GOTO 1000:REM CTRL+F1 PRESSED, EXIT
215 IF (X=27) THEN GOTO 1000:REM OR ESCAPE TO EXIT
220 GOTO 50:REM START ALL OVER...
230 REM ---- END MAIN LOOP PROCESSING

1000 SCREEN OS:REM GRACEFUL EXIT, RETURN TO "ORIGINAL SCREEN" MODE
1005 PRINT X
1010 END
