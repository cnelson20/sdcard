1000 ' "Wheel of Fortune" game
1010 ' originally uploaded to EXEC-PC RBBS (the best) Milwaukee, WI
1020 ' program written by Phil Katz
1030 KEY OFF:DEFINT A-Z:FALSE=0:TRUE=NOT FALSE:SCREEN 0,0:WIDTH 80
1040 CLS:LOCATE 2,1,1:PRINT"IBM PC  Wheel of Fortune":OPTION BASE 1
1050 ON ERROR GOTO 1060:OPEN "I",#1,"fortune.dat":ON ERROR GOTO 0:GOTO 1080
1060 IF ERR<>53 THEN ON ERROR GOTO 0:RESUME
1070 PRINT:PRINT"FORTUNE.DAT file is needed to run this program.":END
1080 INPUT #1,MAX:DIM TYPE$(MAX),PUZZLE$(MAX)
1090 FOR I=1 TO MAX:INPUT #1,TYPE$(I),PUZZLE$(I):NEXT:CLOSE #1
1100 DEF SEG=0:POKE &H417,PEEK(&H417) OR 64 'turn on caps lock
1110 LOCATE 5,1:PRINT"Do you want intsructions (y/n) ?";
1120 A$=INKEY$:IF A$="" THEN 1120 ELSE IF A$="Y" THEN GOSUB 2170
1130 LOCATE 5,1
1140 INPUT"How many players are there (1-5) ? ",NUMOFPLAYERS
1150 IF NUMOFPLAYERS<1 THEN 1140
1160 IF NUMOFPLAYERS>5 THEN PRINT:PRINT"Maximum of 5 players":PRINT:GOTO 1140
1170 DIM MONEY(NUMOFPLAYERS),PNAME$(NUMOFPLAYERS),TOTAL(NUMOFPLAYERS)
1180 RANDOMIZE VAL(RIGHT$(TIME$,2))*VAL(RIGHT$(TIME$,2))
1190 FOR I=1 TO 10:KEY I,"":NEXT:PRINT
1200 FOR I=1 TO NUMOFPLAYERS:PRINT:PRINT"Player";I;"what is your name ";
1210 INPUT PNAME$(I):NEXT:PLAYER=1:ROUND=1
1220 ON KEY(1) GOSUB 1600:ON KEY(2) GOSUB 1740:ON KEY(3) GOSUB 1830:VOWELS$="AEIOU"
1230 VOWELSBOUGHT=0:ALREADYGUESSED$=" "
1240 FOR I=1 TO NUMOFPLAYERS:MONEY(I)=0:NEXT
1250 CLS:PLAY"MF":LOCATE 1,1,0:PRINT"Round";ROUND;:LOCATE ,14:PRINT"Used letters:"
1260 LOCATE 25,8:PRINT"F1-Spin the wheel  ,  F2-Buy a vowel  ,  F3-Guess the puzzle";
1270 I=INT(RND*MAX)+1:TYPE$=TYPE$(I):PUZZLE$=PUZZLE$(I)
1280 LOCATE 4,1:PRINT TYPE$:PRINT:CONSTS=0
1290 FOR I=1 TO LEN(PUZZLE$):LETTER$=MID$(PUZZLE$,I,1)
1300 IF LETTER$=" " THEN PRINT"  "; ELSE PRINT"_ ";:IF INSTR(1,VOWELS$,LETTER$)=0 THEN CONSTS=CONSTS+1
1310 NEXT:IF ROUND=1 THEN 1340
1320 FOR I=1 TO NUMOFPLAYERS:LOCATE 11,I*15-14:A$=STR$(TOTAL(I))
1330 MID$(A$,1,1)="$":PRINT A$;:NEXT
1340 FOR I=1 TO NUMOFPLAYERS:IF I=PLAYER THEN COLOR 16,7
1350 LOCATE 12,I*15-14:PRINT PNAME$(I);:COLOR 7,0:NEXT
1360 FOR I=1 TO NUMOFPLAYERS:LOCATE 13,I*15-14
1370 PRINT USING"$#####";MONEY(I);:NEXT
1380 LOCATE 21,33:PRINT SPC(9):BUYVOWEL=FALSE:LOCATE 18,1:PRINT SPC(42)
1390 FOR I=1 TO 3:KEY(I) ON:NEXT
1400 IF INKEY$="" THEN 1400 ELSE SOUND 523,6:SOUND 32767,1:GOTO 1400
1410 LOCATE 18,1:PRINT"What letter would you like ?":GOTO 1430
1420 SOUND 523,6:SOUND 32767,1
1430 LETTER$=INKEY$:IF LEN(LETTER$)<>1 THEN 1430
1440 IF (LETTER$>="a") AND (LETTER$<="z") THEN LETTER$=CHR$(ASC(LETTER$)-32)
1450 IF BUYVOWEL XOR INSTR(1,VOWELS$,LETTER$)<>0 THEN 1420
1460 IF INSTR(1,ALREADYGUESSED$,LETTER$)<>0 THEN 1420
1470 IF (LETTER$<"A") OR (LETTER$>"Z") THEN 1420
1480 ALREADYGUESSED$=ALREADYGUESSED$+LETTER$+" "
1490 POSITN=INSTR(1,PUZZLE$,LETTER$):LOCATE 1,27:PRINT ALREADYGUESSED$
1500 IF POSITN=0 THEN 1560
1510 IF NOT BUYVOWEL THEN MONEY(PLAYER)=MONEY(PLAYER)+SPIN:CONSTS=CONSTS-1
1520 LOCATE 6,(POSITN)*2-1:BEEP:PRINT MID$(PUZZLE$,POSITN,1):SOUND 32767,1
1530 POSITN=INSTR(POSITN+1,PUZZLE$,LETTER$)
1540 IF POSITN<>0 THEN 1510
1550 IF CONSTS>0 OR BUYVOWEL THEN 1360 ELSE GOSUB 1620
1560 SOUND 37,20:SOUND 32767,1
1570 PLAYER=PLAYER+1:IF PLAYER>NUMOFPLAYERS THEN PLAYER=1
1580 GOTO 1340
1590 REM spin wheel routine
1600 FOR I=1 TO 3:KEY(I) OFF:NEXT
1610 IF CONSTS>0 THEN 1640
1620 LOCATE 18,1:COLOR 15
1630 PRINT"There are no more consonants in the puzzle";:GOTO 1770
1640 FOR I=1 TO 11:SPIN=(CINT((ROUND*11+10)*RND)-ROUND)*50:LOCATE 21,33
1650 SOUND 32767,2:SOUND 200,1:PRINT SPC(9):LOCATE ,33
1660 IF SPIN=50 THEN IF ROUND=3 THEN SPIN=5000
1670 IF SPIN>0 THEN PRINT USING"$$####";SPIN;:GOTO 1690
1680 IF SPIN=0 THEN PRINT"LOSE TURN"; ELSE IF SPIN<0 THEN PRINT"BANKRUPT";
1690 NEXT
1700 IF SPIN>0 THEN RETURN 1410
1710 IF SPIN<0 THEN MONEY(PLAYER)=0:SOUND 74,20
1720 RETURN 1560
1730 REM buy vowel routine
1740 FOR I=1 TO 3:KEY(I) OFF:NEXT
1750 IF VOWELSBOUGHT<5 THEN GOTO 1780
1760 LOCATE 18,1:COLOR 15:PRINT"All the vowels have been bought";
1770 COLOR 7:SOUND 32767,35:SOUND 32767,1:RETURN 1360
1780 IF MONEY(PLAYER)>=250 THEN 1800 ELSE LOCATE 18,1:COLOR 15
1790 PRINT"You need at least $250 to buy a vowel";:GOTO 1770
1800 VOWELSBOUGHT=VOWELSBOUGHT+1
1810 MONEY(PLAYER)=MONEY(PLAYER)-250:BUYVOWEL=TRUE:RETURN 1410
1820 REM guess the puzzle routine
1830 FOR I=1 TO 3:KEY(I) OFF:NEXT:GUESS$=""
1840 LOCATE 18,1,1:PRINT"What is your guess ? ";
1850 A$=INKEY$:IF LEN(A$)<>1 THEN 1850
1860 IF A$=CHR$(13) THEN 1920
1870 IF A$=CHR$(8) THEN GUESS$=LEFT$(GUESS$,(1-LEN(GUESS$))*(LEN(GUESS$)>0)):GOTO 1900
1880 IF (A$>="a") AND (A$<="z") THEN A$=CHR$(ASC(A$)-32)
1890 IF (A$>="A") OR (A$<="Z") OR (A$=" ") THEN GUESS$=GUESS$+A$ ELSE 1850
1900 LOCATE 18,22:PRINT GUESS$+" ";:LOCATE ,POS(0)-1
1910 GOTO 1850
1920 IF GUESS$<>PUZZLE$ THEN 2010 ELSE PLAY"MB"
1930 TOTAL(PLAYER)=TOTAL(PLAYER)+MONEY(PLAYER):COLOR 15:PRINT:PRINT
1940 FOR I=1 TO 10:BEEP:SOUND 32767,1:NEXT
1950 PRINT"Congradulations!! ";PNAME$(PLAYER);
1960 PRINT" you have won ";:COLOR 23:PRINT "$";MONEY(PLAYER):COLOR 7:LOCATE ,,0
1970 PRINT:PRINT"Strike any key to continue...":ROUND=ROUND+1
1980 IF INKEY$="" THEN 1980 ELSE IF ROUND=4 THEN 2030 ELSE SOUND 32767,0
1990 PLAYER=PLAYER+1:IF PLAYER>NUMOFPLAYERS THEN PLAYER=1
2000 RETURN 1230
2010 LOCATE 18,1,0:PRINT SPC(79)
2020 LOCATE ,1:PRINT"Sorry, that's not it.":RETURN 1560
2030 CLS:LOCATE 9,1:PRINT"After 3 rounds the standings are:":MMAX=1
2040 FOR I=2 TO NUMOFPLAYERS:IF TOTAL(I)>TOTAL(MMAX) THEN MMAX=I
2050 NEXT
2060 FOR I=1 TO NUMOFPLAYERS:COLOR 15
2070 LOCATE 12,I*15-14:PRINT PNAME$(I):IF I=MMAX THEN COLOR 31
2080 LOCATE 13,I*15-14:PRINT USING"$#####";TOTAL(I):NEXT:SOUND 37,0
2090 FOR I=7 TO 24:FOR J=0 TO I:OUT 66,J:OUT 66,J:OUT 97,(INP(97) OR 3)
2100 NEXT:NEXT:SOUND 37,0
2110 LOCATE 16,1:COLOR 15:PRINT PNAME$(MMAX);" Wins!!!":COLOR 7
2120 LOCATE 18,1,1:PRINT"Play again (y/n) ?";
2130 A$=INKEY$:IF A$="" THEN 2130 ELSE SOUND 32767,0
2140 IF A$="n" OR A$="N" THEN CLS:SYSTEM
2150 FOR I=1 TO NUMOFPLAYERS:TOTAL(I)=0:NEXT
2160 ROUND=1:PLAYER=1:RETURN 1230
2170 CLS:PRINT"This is a game, for 1 to 5 players, similar to the Wheel of"
2180 PRINT"Fortune TV game show. Each game consists of three rounds or"
2190 PRINT"puzzles to solve and whoever has won the most money after three"
2200 PRINT"rounds wins the game.":PRINT
2210 PRINT"Each round consists of solving a puzzle given the hint on the"
2220 PRINT"type of puzzle it is, such as a quote or a persons' name etc.":PRINT
2230 PRINT"When your name is in blinking reverse video it is your turn and"
2240 PRINT"you can 'spin the wheel', buy a vowel, or guess the puzzle.":PRINT
2250 PRINT"After spinning the wheel you then guess a consonant. If the"
2260 PRINT"consonant you guess is in the puzzle you will get the amount of"
2270 PRINT"money on the wheel for each occurance in the puzzle and it will"
2280 PRINT"still be your turn. If it is not you will lose your turn and it"
2290 PRINT"will become the next players turn. If you enter a vowel or a letter"
2300 PRINT"that has previously been guessed, the computer will beep and wait"
2310 PRINT"for you to enter another letter. If all the consonants in the"
2320 PRINT"puzzle have been guessed then a message will be displayed.":PRINT
2330 PRINT:PRINT"Strike any key to continue...";
2340 IF INKEY$="" THEN 2340 ELSE CLS
2350 PRINT"Buying a vowel is similar to guessing a consonant, except it will"
2360 PRINT"cost you $250 to buy a vowel, no matter how many times it is in"
2370 PRINT"the puzzle.":PRINT
2380 PRINT"If you think you know the answer, you can guess what the puzzle"
2390 PRINT"is. You must enter the entire answer, and spelling does count."
2400 PRINT"In addition, you should enter only 1 blank between words. If you"
2410 PRINT"guess correctly you will win the round. If you don't then you will"
2420 PRINT"lose your turn and play will continue with the next player."
2430 PRINT:PRINT"Strike any key to start the game...";
2440 IF INKEY$="" THEN 2440 ELSE CLS:RETURN
