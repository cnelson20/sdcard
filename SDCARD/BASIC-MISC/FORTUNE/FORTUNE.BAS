1000 REM "WHEEL OF FORTUNE" CHR$ATNFORVAL
1010 REM ORIGINALLY UPLOADED TO EXEC PC RBBS (THE BEST) MILWAUKEE, WI
1020 REM PROGRAM WRITTEN BY PHIL KATZ 1984 (X16 PORT VOIDSTAR 2023 V0.3)
1023 RR=0:REM USED TO ADJUST RND
1024 O=0:PSGINIT:REM O USED FOR PSGPLAY
1025 COLOR 15,0
1030 FALSE=0:TRUE=NOT FALSE
1040 CLS:LOCATE 2,1:PRINT"X16     WHEEL OF FORTUNE":
1050 OPEN 1,8,2,"FORTUNE.DAT,S,R"
1060 REM IF ERR<>53 THEN ON ERROR GOTO 0:RESUME
1065 IF (ST<>2) THEN GOTO 1080
1070 PRINT:PRINT"FORTUNE.DAT FILE IS NEEDED TO RUN THIS PROGRAM.":END
1080 INPUT#1,MAX:DIM TYPE$(MAX),PUZZLE$(MAX)
1090 FOR I=1 TO MAX:INPUT#1,TYPE$(I),PUZZLE$(I):NEXT:CLOSE 1
1100 REM DEF SEG=0:POKE &H417,PEEK(&H417) OR 64 TURN ON CAPS LOCK
1110 LOCATE 5,1:PRINT"DO YOU WANT INSTRUCTIONS (Y/N) ?";
1120 REM A$=INKEY$:IF A$="" THEN 1120 ELSE IF A$="Y" THEN GOSUB 2170
1125 GET A$:IF A$="" THEN 1125
1127 IF A$="\XD9" OR A$="Y" THEN GOTO 1129
1128 CLS:GOTO 1130
1129 GOSUB 2170
1130 LOCATE 5,1
1140 REM INPUT"H\XCF\XD7 \XCD\XC1\XCE\XD9 \XD0\XCC\XC1\XD9\XC5\XD2\XD3 \XC1\XD2\XC5 \XD4\XC8\XC5\XD2\XC5 (1-5) ? ",NUMOFPLAYERS
1141 PRINT "HOW MANY PLAYERS ARE THERE (1-5) ?";:INPUT NU
1150 IF NU<1 THEN 1140
1160 IF NU>5 THEN PRINT:PRINT"MAXIMUM OF 5 PLAYERS":PRINT:GOTO 1140
1170 DIM FUNDS(NU),PNAME$(NU),TT(NU):REM TT IS TOTALS ALL ROUNDS
1180 REM RANDOMIZE VAL(RIGHT$(TIME$,2))*VAL(RIGHT$(TIME$,2))
1190 REM FOR I=1 TO 10:KEY I,"":NEXT:PRINT
1200 FOR I=1 TO NU:PRINT:PRINT"PLAYER";I;"WHAT IS YOUR NAME ";
1210 INPUT PNAME$(I):NEXT:PLAYER=1:ROUND=1
1220 REM GET A$
1221 REM A=ASC(A$)
1222 REM F1=133, F2=137, F3=134
1224 VOWELS$="AEIOU"
1230 VOWELSBOUGHT=0:AG$=" "
1240 FOR I=1 TO NU:FUNDS(I)=0:NEXT
1250 CLS:LOCATE 1,1:PRINT"ROUND";ROUND;:LOCATE 1,14:PRINT"USED LETTERS:"
1260 LOCATE 25,8:PRINT"F1-SPIN THE WHEEL  F2-BUY A VOWEL  F3-GUESS THE PUZZLE";
1270 I=INT(RND(RR)*MAX)+1:TYPE$=TYPE$(I):PUZZLE$=PUZZLE$(I)
1280 LOCATE 4,1:PRINT TYPE$:PRINT:CS=0
1290 FOR I=1 TO LEN(PUZZLE$):LTR$=MID$(PUZZLE$,I,1)
1300 IF LTR$=" " THEN PRINT"  ";
1301 IF LTR$<>" " THEN PRINT"? ";
1302 REM: IF INSTR(1,VOWELS$,LTR$)=0 THEN CS=CS+1
1303 FOR II=1 TO LEN(VOWELS$)
1304 IF MID$(VOWELS$,II,1)=LTR$ THEN GOTO 1310
1305 NEXT II
1306 CS=CS+1
1310 NEXT I:IF ROUND=1 THEN 1340
1320 FOR I=1 TO NU
1325 LOCATE 11,I*15-14
1328 A$=STR$(TT(I))
1330 PRINT"$";:PRINT A$;:NEXT
1340 FOR I=1 TO NU:IF I=PLAYER THEN COLOR 15,7
1350 LOCATE 12,I*15-14:PRINT PNAME$(I);:COLOR 7,0:NEXT
1360 FOR I=1 TO NU:LOCATE 13,I*15-14
1370 PRINT "$";FUNDS(I);:NEXT
1380 BUYVOWEL=0:LOCATE 18,1
1382 PRINT "                                          "
1390 REM FOR I=1 TO 3:KEY(I) ON:NEXT
1400 GET A$:IF A$="" THEN 1400:IF A$<>"" THEN PSGPLAY O,"T180A20"
1401 A=ASC(A$)
1402 IF A=133 THEN GOTO 1600:REM SPIN
1403 IF A=137 THEN GOTO 1740:REM VOWEL
1404 IF A=134 THEN GOTO 1830:ROM SOLVE
1405 GOTO 1400
1410 LOCATE 18,1:PRINT"WHAT LETTER WOULD YOU LIKE ?":GOTO 1430
1420 PSGPLAY O,"T180A20":PSGPLAY O,"T180A20"
1430 GET LTR$:IF LTR$="" THEN 1430
1440 IF (LTR$>="\XC1") AND (LTR$<="\XDA") THEN LTR$=CHR$(ASC(LTR$)-32)
1445 ZZ=0:FOR II=1 TO LEN(VOWELS$)
1446 IF MID$(VOWELS$,II,1)=LTR$ THEN ZZ=II:GOTO 1450
1447 NEXT II
1450 REM IF BUYVOWEL XOR INSTR(1,VOWELS$,LTR$)<>0 THEN 1420
1451 IF (BUYVOWEL=1) AND (ZZ=0) THEN GOTO 1420:REM NOT A VOWEL
1460 REM IF INSTR(1,AG$,LTR$)<>0 THEN 1420
1462 FOR II=1 TO LEN(AG$):REM CHECK AVAILABLE LETTER
1463 IF MID$(AG$,II,1)=LTR$ THEN GOTO 1420
1464 NEXT II
1470 IF (LTR$<"A") OR (LTR$>"Z") THEN 1420:REM JUST IN CASE
1480 AG$=AG$+LTR$+" "
1490 REM PN=INSTR(1,PUZZLE$,LTR$):LOCATE 1,27:PRINT AG$
1492 PN=0:FOR II=1 TO LEN(PUZZLE$)
1494 IF MID$(PUZZLE$,II,1)=LTR$ THEN PN=II:GOTO 1498
1496 NEXT II
1498 LOCATE 1,27:PRINT AG$
1500 IF PN=0 THEN 1560
1510 IF (BUYVOWEL=0) THEN FUNDS(PLAYER)=FUNDS(PLAYER)+SPIN:CS=CS-1
1520 LOCATE 6,(PN)*2-1:PSGPLAY O,"T180A20":PRINT MID$(PUZZLE$,PN,1)
1525 PSGPLAY O,"T180A20"
1526 FOR II=PN+1 TO LEN(PUZZLE$)
1527 IF MID$(PUZZLE$,II,1)=LTR$ THEN PN=II:GOTO 1540
1528 NEXT II
1529 PN=0:REM NOT FOUND
1530 REM PN=INSTR(PN+1,PUZZLE$,LTR$)
1540 IF PN<>0 THEN 1510
1550 IF CS>0 OR BUYVOWEL THEN 1360:GOSUB 1620
1560 PSGPLAY O,"T180A20":PSGPLAY O,"T180A20"
1570 PLAYER=PLAYER+1:IF PLAYER>NU THEN PLAYER=1
1580 GOTO 1340
1590 REM SPIN WHEEL ROUTINE ************************************
1595 REM DISABLE F1-F5 SO THEY DON'T GET BUFFERED/CACHED UP
1600 REM FOR I=1 TO 3:KEY(I) OFF:NEXT
1610 IF CS>0 THEN 1640
1620 LOCATE 18,1:COLOR 15
1630 PRINT"THERE ARE NO MORE CONSONANTS IN THE PUZZLE";:GOTO 1770
1640 FOR I=1 TO 24
1641 SLEEP 2:LOCATE 21,33:PRINT "          ":LOCATE 21,33
1642 YY=(ROUND*11+10)*RND(RR)-ROUND
1643 IF YY<0 THEN SPIN=INT(-1):GOTO 1650
1644 SPIN=INT(YY)*50
1650 PSGPLAY O,"T180A20":PSGPLAY O,"T360A40"
1660 IF SPIN=50 AND ROUND=3 THEN SPIN=5000
1670 IF SPIN>0 THEN PRINT SPIN;
1680 IF SPIN=0 THEN PRINT"LOSE TURN";
1685 IF SPIN<0 THEN PRINT"BANKRUPT";
1690 NEXT
1700 IF SPIN>0 THEN GOTO 1410
1710 IF SPIN<0 THEN FUNDS(PLAYER)=0:PSGPLAY O,"T180A20"
1720 GOTO 1560
1725 REM =========================================
1730 REM BUY VOWEL ROUTINE
1740 REM FOR I=1 TO 3:KEY(I) OFF:NEXT
1750 IF VOWELSBOUGHT<5 THEN GOTO 1780
1760 LOCATE 18,1:COLOR 15:PRINT"ALL THE VOWELS HAVE BEEN BOUGHT";
1770 COLOR 7:PSGPLAY O,"T180A20":PSGPLAY O,"T360A40":GOTO 1360
1780 IF FUNDS(PLAYER)>=250 THEN 1800:LOCATE 18,1:COLOR 15
1790 PRINT"YOU NEED AT LEAST $250 TO BUY A VOWEL";:GOTO 1770
1800 VOWELSBOUGHT=VOWELSBOUGHT+1
1810 FUNDS(PLAYER)=FUNDS(PLAYER)-250:BUYVOWEL=1:GOTO 1410
1820 REM GUESS THE PUZZLE ROUTINE
1830 REM FOR I=1 TO 3:KEY(I) OFF:NEXT:GUESS$=""
1835 GUESS$=""
1840 LOCATE 18,1:PRINT"WHAT IS YOUR GUESS ? ";
1850 GET A$:IF A$="" THEN 1850
1855 A=ASC(A$)
1860 IF A=13 THEN 1920:REM ENTER, 20 IS BACKSPACE
1870 IF A=20AND LEN(GUESS$)>0 THEN GUESS$=MID$(GUESS$,1,LEN(GUESS$)-1):GOTO1900
1880 IF (A$>="\XC1") AND (A$<="\XDA") THEN A$=CHR$(ASC(A$)-32)
1890 IF (A$>="A") OR (A$<="Z") OR (A$=" ") THEN 1892
1891 GOTO 1850
1892 GUESS$=GUESS$+A$
1900 LOCATE 18,22:PRINT GUESS$+" ";:LOCATE 18,POS(0)-1
1910 GOTO 1850
1920 IF GUESS$<>PUZZLE$ THEN 2010:REM ELSE PLAY"MB"
1930 TT(PLAYER)=TT(PLAYER)+FUNDS(PLAYER):COLOR 15:PRINT:PRINT
1940 FOR I=1 TO 10:PSGPLAY O,"T180A20":PSGPLAY O,"T180A20":NEXT
1950 PRINT"CONGRADULATIONS!! ";PNAME$(PLAYER);
1960 PRINT" YOU HAVE WON ";:COLOR 5:PRINT "$";FUNDS(PLAYER):COLOR 15
1970 PRINT:PRINT"STRIKE ANY KEY TO CONTINUE...":ROUND=ROUND+1
1980 GET A$:IF A$="" THEN 1980
1985 IF ROUND=4 THEN 2030:PSGPLAY O,"T180A20"
1990 PLAYER=PLAYER+1:IF PLAYER>NU THEN PLAYER=1
2000 GOTO 1230
2010 LOCATE 18,1:PRINT "                                                  "
2020 LOCATE 18,1:PRINT"SORRY, THAT'S NOT IT.";:SLEEP 50:GOTO 1560
2030 CLS:LOCATE 9,1:PRINT"AFTER 3 ROUNDS THE STANDINGS ARE:":MMAX=1
2040 FOR I=2 TO NU:IF TT(I)>TT(MMAX) THEN MMAX=I
2050 NEXT
2060 FOR I=1 TO NU:COLOR 15
2070 LOCATE 12,I*15-14:PRINT PNAME$(I):IF I=MMAX THEN COLOR 14
2080 LOCATE 13,I*15-14:PRINT "$";TT(I):NEXT:PSGPLAY O,"T180A20"
2090 REM   FOR I=7 TO 24:FOR J=0 TO I:OUT 66,J:OUT 66,J:OUT 97,(INP(97) OR 3)
2100 PSGPLAY O,"T180A20"
2110 LOCATE 16,1:COLOR 15:PRINT PNAME$(MMAX);" WINS!!!":COLOR 7
2120 LOCATE 18,1:PRINT"PLAY AGAIN (Y/N) ?";
2130 GET A$:IF A$="" THEN 2130
2135 PSGPLAY O,"T180A20"
2140 IF A$="\XCE" OR A$="N" THEN CLS:PRINT"THANK YOU FOR PLAYING!":END
2145 IF A$="\XD9" OR A$="Y" THEN GOTO 2150
2146 GOTO 2130
2150 FOR I=1 TO NU:TT(I)=0:NEXT
2160 ROUND=1:PLAYER=1:GOTO 1230
2170 CLS:PRINT"THIS IS A GAME, FOR 1 TO 5 PLAYERS, SIMILAR TO THE WHEEL OF"
2180 PRINT"FORTUNE TV GAME SHOW. EACH GAME CONSISTS OF THREE ROUNDS OF"
2190 PRINT"PUZZLES TO SOLVE AND WHOEVER HAS WON THE MOST MONEY AFTER THREE"
2200 PRINT"ROUNDS WINS THE GAME.":PRINT
2210 PRINT"EACH ROUND CONSISTS OF SOLVING A PUZZLE GIVEN THE HINT ON THE"
2220 PRINT"TYPE OF PUZZLE IT IS, SUCH AS A QUOTE OR A PERSONS' NAME ETC.":PRINT
2230 PRINT"WHEN YOUR NAME IS IN REVERSE VIDEO IT IS YOUR TURN AND"
2240 PRINT"YOU CAN 'SPIN THE WHEEL', BUY A VOWEL, OR GUESS THE PUZZLE.":PRINT
2250 PRINT"AFTER SPINNING THE WHEEL YOU THEN GUESS A CONSONANT. IF THE"
2260 PRINT"CONSONANT YOU GUESS IS IN THE PUZZLE YOU WILL GET THE AMOUNT OF"
2270 PRINT"MONEY ON THE WHEEL FOR EACH OCCURANCE IN THE PUZZLE AND IT WILL"
2280 PRINT"STILL BE YOUR TURN. IF IT IS NOT YOU WILL LOSE YOUR TURN AND IT"
2290 PRINT"WILL BECOME THE NEXT PLAYERS TURN. IF YOU ENTER A VOWEL OR A LETTER"
2300 PRINT"THAT HAS PREVIOUSLY BEEN GUESSED, THE COMPUTER WILL BEEP AND WAIT"
2310 PRINT"FOR YOU TO ENTER ANOTHER LETTER. IF ALL THE CONSONANTS IN THE"
2320 PRINT"PUZZLE HAVE BEEN GUESSED THEN A MESSAGE WILL BE DISPLAYED.":PRINT
2330 PRINT:PRINT"STRIKE ANY KEY TO CONTINUE...";
2340 GET A$:IF A$="" THEN 2340
2345 CLS
2350 PRINT"BUYING A VOWEL IS SIMILAR TO GUESSING A CONSONANT, EXCEPT IT WILL"
2360 PRINT"COST YOU $250 TO BUY A VOWEL, NO MATTER HOW MANY TIMES IT IS IN"
2370 PRINT"THE PUZZLE.":PRINT
2380 PRINT"IF YOU THINK YOU KNOW THE ANSWER, YOU CAN GUESS WHAT THE PUZZLE"
2390 PRINT"IS. YOU MUST ENTER THE ENTIRE ANSWER, AND SPELLING DOES COUNT."
2400 PRINT"IN ADDITION, YOU SHOULD ENTER ONLY 1 BLANK BETWEEN WORDS. IF YOU"
2410 PRINT"GUESS CORRECTLY YOU WILL WIN THE ROUND. IF YOU DON'T THEN YOU WILL"
2420 PRINT"LOSE YOUR TURN AND PLAY WILL CONTINUE WITH THE NEXT PLAYER."
2430 PRINT:PRINT"STRIKE ANY KEY TO START THE GAME...";
2440 GET A$:IF A$="" THEN 2440
2442 CLS:RETURN
