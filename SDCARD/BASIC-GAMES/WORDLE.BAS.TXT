10 REM WORDLE
20 CLS
30 REM SETUP GAME
40 GOSUB 40000: REM SETUP STRINGS AND VARIABLES
50 GOSUB 41000: REM TITLE SCREEN
60 GOSUB 42000: REM LOAD DATA
70 GOSUB 46000: REM PICK RANDOM WORD
80 GOTO 41500
500 PRINT "MENU STOP..."
1000 END

30000 REM -------------- GAME SCREEN ---
30010 R=INT(RND(1)*NW)
30020 REM IA=DS+36*INT(R/5)+(R-INT(R/5)*5)*6
30030 REM IF R=INT(R/5)*5 THEN IA=IA-6
30040 REM POKE 66,INT(IA/256):POKE 65,IA-(INT(IA/256)*256)
30050 REM READ PW$
30060 PW$=WD$(R)
30070 GOSUB 37000 : REM CLEAR AND DRAW SCREEN
30500 REM ---------------- MAIN LOOP ---
30510 FOR AT=1 TO 6: REM ATTEMPT NUM
30511 REM CLEAR THE PLAYER INPUT
30512 LET GS$=""
30513 REM CLEAR THE CALCULATED GA$
30515 FOR I=1 TO 5: GA$(I)="": NEXT I
30520 FOR C=0 TO 5
30540 GOSUB 33000 : REM UPDATE LINE
30550 GOSUB 33200 : REM HILIGHT LINE
30560 GOSUB 32000 : REM READ GUESS
30561 GOSUB 31600 : REM CHECK GUESS
30565 NEXT C
30570 NEXT AT
30580 GOTO 38000 : REM CHECK GUESS
30590 PRINT "END": END

31000 REM --------------- DEL CHAR
31010 X=C-1
31020 GS$=LEFT$(GS$,X)
31030 VPOKE 1,$B000+(LN*256)+(X*6)+28,32
31040 C=X
31050 RETURN

31300 REM ------------- ACCEPT CHAR ---
31310 IF C>=5 THEN RETURN
31320 LET GS$=GS$+K$
31330 VPOKE 1,$B000+(LN*256)+(C*6)+28,ASC(K$)-64
31340 C=C+1
31350 RETURN

31600 REM --- FIND WORD ---
31610 REM PRINT CHR$($13);TAB(39)"FIND WORD"
31620 C=6
31630 GOSUB 34000 : REM PROCESS GUESS
31640 GOSUB 35000 : REM PROCESS GUESS COLOURS
31650 IF GS$=PW$ THEN GOTO 44000
31660 RETURN

32000 REM --- READ KEY ---
32005 REM PRINT CHR$($13);TAB(39)"READ KEY"
32010 IF C>5 THEN RETURN 
32020 GET K$ : IF K$="" THEN GOTO 32020
32029 REM IF K$ = DELETE 
32030 IF K$=CHR$(20) AND C>0 AND C<=5 THEN GOSUB 31000
32035 REM IF K$ = RETURN 
32040 IF K$=CHR$(13) AND C=5 THEN RETURN
32045 REM PROCESS KEYS A-Z
32050 IF K$>="A" AND K$<="Z" AND C<=5 THEN GOSUB 31300
32070 GOTO 32010

33000 REM --- UPDATE LINE ---
33010 VPOKE 1,$B000+(LN*256)+22,32
33020 LN=(AT*3)
33030 RETURN

33200 REM --- HILIGHT CURR LINE ---
33210 VPOKE 1,$B000+(LN*256)+22,90
33220 VPOKE 1,$B001+(LN*256)+22,103
33230 RETURN

34000 REM --- PROCESS GUESS ---
34010 REM PRINT CHR$($13);TAB(39)"PROCESS GUESS"
34015 LET GA$="     "
34020 FOR Z=1 TO 5
34030 FOR ZB=1 TO 5
34031 REM CHECK A LETTER IN GUESS WITH EACH LETTER IN ANSWER
34032 REM PRINT "TEST=", MID$(GS$,Z,1), "VS", MID$(PW$,ZB,1),"GA=",GA$(Z)
34040 IF MID$(GS$,Z,1)=MID$(PW$,ZB,1) THEN GA$(Z)="-"
34050 NEXT ZB
34060 IF MID$(GS$,Z,1)=MID$(PW$,Z,1) THEN GA$(Z)="+"
34070 NEXT Z
34080 RETURN

35000 REM --- PROCESS GUESS COLOURS ---
35010 FOR Z=1 TO 5
35020 CL=106 : REM DEFAULT COLOUR RED
35030 IF GA$(Z)="+" THEN CL=101 : REM COLOUR GREEN
35040 IF GA$(Z)="-" THEN CL=103 : REM COLOUR YELLOW
35050 GOSUB 36000 : REM DRAW UPDATED COLOUR
35060 NEXT Z
35070 RETURN

35500 REM --- UNIT-TEST GUESS COLOURS ---
35510 GA$="+++++" : LN = 3
35520 GOSUB 35000
35530 GA$="-----" : LN = 6
35540 GOSUB 35000
35550 GA$="     " : LN = 9
35560 GOSUB 35000
35570 GA$="+- +-" : LN = 12
35590 GOSUB 35000
35600 RETURN

36000 REM --- HILIGHT BOXES ---
36001 REM Z IS SET BEFORE FUNCTION IS CALLED. Z IS CHAR BOX TO DRAW
36002 REM PRINT "COLOUR=",CL
36010 LET TM=0: FOR TM=0 TO 6 STEP 2
36020 VPOKE 1,$B001+((LN-1)*256)+(Z*6)+20+TM,CL
36030 NEXT TM
36040 LET TM=0: FOR TM=0 TO 6 STEP 2
36050 VPOKE 1,$B001+(LN*256)+(Z*6)+20+TM,CL
36060 NEXT TM
36070 LET TM=0: FOR TM=0 TO 6 STEP 2
36080 VPOKE 1,$B001+((LN+1)*256)+(Z*6)+20+TM,CL
36090 NEXT TM
36100 RETURN

37000 REM --- DRAW TITLE AND CHAR BOXES
37010 CLS
37020 PRINT CHR$($93);TAB(15);CHR$($9E);"W O R D L E"
37030 PRINT CHR$($05)
37040 FOR T=1 TO 6
37050 PRINT TAB(13);A$;A$;A$;A$;A$
37060 PRINT TAB(13);B$;B$;B$;B$;B$
37070 PRINT TAB(13);C$;C$;C$;C$;C$
37080 NEXT T
37090 RETURN 

37500 REM --- UNIT-TEST DRAW TITLE AND CHAR BOXES
37510 GOSUB 40000 : REM SET GFX STRINGS
37520 GOSUB 37000 : REM DRAW SCREEN
37530 RETURN

38000 REM --- CHECK IF PLAYER HAS WON OR LOST
38010 IF GS$=PW$ THEN GOTO 44000
38020 IF GS$<>PW$ THEN GOTO 43000 
38030 END

40000 REM ------------- SETUP STRINGS AND VARIABLES ---
40010 X=RND(-TI): REM RANDOM 
40020 A$="uci": REM GRAPHICS TOP
40030 B$="b b": REM GRAPHICS MIDDLE
40040 C$="jck": REM GRAPHICS BOTTOM
40050 DIM GA$(5): REM PLAYERS GUESS ARRAY
40060 DIM GS$(5): REM PLAYERS GUESS STRING
40070 RETURN

41000 REM ------------- TITLE SCREEN ---
41010 SCREEN $03: REM SET SCREEN MODE 40X30 TEXT
41020 PRINT TAB(15);CHR$($9E);"W O R D L E"
41030 PRINT CHR$($9B);"ddddddddddddddddddddddddddddddddddddddd"
41040 PRINT "         COMMANDER X16 CONVERSION"
41045 PRINT "            BY JUSTIN BALDOCK"
41050 PRINT CHR$($9B);"         ORIGINAL BY JOSH WARDLE"
41080 PRINT CHR$($9B);"ddddddddddddddddddddddddddddddddddddddd"
41090 RETURN

41500 REM ------------- GAME MENU ---
41505 CLS
41510 PRINT: PRINT TAB(15);CHR$($11);CHR$($05);"HOW TO PLAY"
41520 PRINT CHR$($11);CHR$($9B)" GUESS THE ";CHR$(9E);"WORDLE";CHR$($98);" IN 6 TRIES."
41530 PRINT CHR$($11);" AFTER EACH GUESS, THE COLOUR OF THE"
41540 PRINT" TILES WILL CHANGE TO SHOW HOW CLOSE"
41550 PRINT" YOUR GUESS WAS TO THE WORD."
41560 PRINT CHR$($9B);"ddddddddddddddddddddddddddddddddddddddd"
41570 PRINT CHR$($9F)" PRESS RETURN TO START, SPACE FOR HELP"
41580 GET KP$:IF KP$="" THEN GOTO 41580
41590 IF KP$=CHR$(13) THEN GOTO 30000
41600 REM IF KP$=" " THEN PRINT CHR$($91);"                                       ";
41610 IF KP$=" " THEN PRINT CHR$($91):GOSUB 45000:GOTO 41500
41620 GOTO 41580

42000 REM ------------- DATA LOADER ---
42010 REM FIRST PIECE OF DATA IS NUMBER OF TOTAL WORDS IN LIST, NW
42020 REM USE A TEMP VAR TM TO UPDATE A DISPLAY
42030 REM CREATE ARRAY WD$ WITH ALL THE WORDS
42035 REM NW = NUMBER OF WORDS
42040 PRINT TAB(12);CHR$($11);CHR$($11);CHR$($9F);"   LOADING DATA...";
42050 TM=1
42060 READ NW:DIM WD$(NW)
42070 FOR W=0 TO NW
42080 READ WD$(W)
42090 IF TM=1 THEN PRINT CHR$($9D);CHR$($9D);CHR$($9D);CHR$($9B);"...";
42100 IF TM=2 THEN PRINT CHR$($9D);CHR$($9D);CHR$($9D);CHR$($98);"...";
42110 IF TM=3 THEN PRINT CHR$($9D);CHR$($9D);CHR$($9D);CHR$($97);"...";
42120 IF TM=4 THEN TM=0
42130 TM=TM+1
42140 NEXT W
42145 PRINT CHR$($05);
42150 RETURN

43000 REM ----------------- END GAME LOSE ---
43010 PRINT CHR$($13);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
43020 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
43030 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
43040 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
43050 PRINT TAB(7);CHR$($9E);"SORRY, THE WORD WAS ";CHR$($96);PW$;CHR$($9E);"."
43060 GOTO 44500

44000 REM ----------------- END GAME WIN ---
44010 PRINT CHR$($13);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
44020 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
44030 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
44040 PRINT CHR$($11);CHR$($11);CHR$($11);CHR$($11);CHR$($11);
44050 PRINT TAB(12);CHR$($9F);"HUZZAH!! YOU WIN!";CHR$($9B)
44060 GOTO 44500

44500 REM --- PROMPT PLAYER START AGAIN
44510 PRINT"WOULD YOU LIKE TO PLAY AGAIN? (Y/N)";
44520 GET KP$: IF KP$="" THEN GOTO 44520
44530 IF KP$="{F1}" THEN GOSUB 1200
44540 IF KP$="N" THEN END
44550 IF KP$="Y" THEN GOTO 30000
44560 GOTO 70

45000 REM ----------------- HOW TO PLAY SCREEN ---
45010 PRINT"    ";CHR$($1E);A$;CHR$($05);A$;A$;A$;A$
45020 PRINT"    ";CHR$($1E);"bWb";CHR$($05);"bEbbAbbRbbYb"
45030 PRINT CHR$($1E);"    ";C$;CHR$($05);C$;C$;C$;C$
45040 PRINT CHR$($05);" THE LETTER ";CHR$($1E);"W";CHR$($05);" IS IN THE WORD"
45050 PRINT" AND IN THE CORRECT SPOT."
45060 PRINT CHR$($05)"    ";A$;A$;CHR$($9E);A$;CHR$($05);A$;A$
45070 PRINT CHR$($05)"    bPbbIb";CHR$($9E);"bLb";CHR$($05);"bObbTb"
45080 PRINT CHR$($05)"    ";C$;C$;CHR$($9E);C$;CHR$($05);C$;C$
45090 PRINT CHR$($05)" THE LETTER ";CHR$($9E);"L";CHR$($05);" IS IN THE WORD"
45100 PRINT" BUT AT WRONG SPOT."
45120 PRINT CHR$($05);"    ";A$;A$;A$;CHR$($96);A$;CHR$($05);A$
45130 PRINT CHR$($05)"    bVbbAbbGb";CHR$($96);"bUb";CHR$($05);"bEb          "
45140 PRINT CHR$($05)"    ";C$;C$;C$;CHR$($96);C$;CHR$($05);C$
45150 PRINT CHR$($05)" THE LETTER ";CHR$($96);"U";CHR$($05);" IS NOT IN "
45160 PRINT " THE WORD."
45170 PRINT " PRESS ANY KEY TO CONTINUE."
45180 GET KP$:IF KP$="" THEN GOTO 45170
45190 RETURN

46000 REM ----------------- PICK WORD ---
46010 X = RND(-TI)
46020 X = INT(RND(1)*NW)
46030 PW$ = WD$(X)
46040 RETURN

50000 REM THE GAME DATA
50005 DATA 1510
50010 DATA ABBEY,ABBOT,ABHOR,ABIDE,ABODE
50011 DATA ABORT,ABOUT,ABOVE,ABYSS,ACORN
50012 DATA ACTOR,ADEPT,ADMIN,ADOPT,ADORE
50015 DATA ADORN,ADULT,AFIRE,AFOOT,AFOUL
50020 DATA AFTER,AGAIN,AGONY,AGREE,AHEAD
50025 DATA AIDED,AISLE,ALARM,ALBUM,ALIEN
50030 DATA ALIGN,ALLOW,ALOFT,ALOHA,AMAZE
50035 DATA AMUSE,ANGEL,ANVIL,APPLE,ARGON
50040 DATA ARSON,AUDIO,AUDIT,AVOID,AWAKE
50045 DATA BACON,BADGE,BAGEL,BAGGY,BAKER
50050 DATA BALLS,BANJO,BARGE,BASIC,BASIN
50055 DATA BASIS,BATCH,BAWDY,BEACH,BEAST
50060 DATA BEATS,BEGAN,BEGAT,BELCH,BELLY
50065 DATA BELOW,BERET,BERRY,BEVVY,BIGOT
50070 DATA BIKER,BIMBO,BINGE,BLACK,BLADE
50075 DATA BLAME,BLAND,BLANK,BLAST,BLAZE
50080 DATA BLOOD,BLOOM,BLOWN,BLUFF,BLUNT
50085 DATA BLURB,BLURT,BLUSH,BOARD,BOGUS
50090 DATA BONER,BOOBS,BOOTH,BORED,BOSSY
50095 DATA BOUND,BRACE,BRAID,BRAIN,BRAKE
50100 DATA BRAND,BRASH,BRASS,BRAVE,BRAWL
50105 DATA BREAD,BREAK,BRICK,BRIDE,BRIEF
50110 DATA BROOK,BRUTE,BUGGY,BULGE,BUNCH
50115 DATA BUNNY,BURLY,BUTCH,BUXOM,BYLAW
50120 DATA CABAL,CABIN,CABLE,CACHE,CADET
50125 DATA CAMEL,CANAL,CANDY,CANOE,CANON
50130 DATA CAPER,CARAT,CARRY,CARVE,CATCH
50135 DATA CATER,CAULK,CAUSE,CEASE,CEDAR
50140 DATA CELLO,CHAIR,CHALK,CHAMP,CHANT
50145 DATA CHAOS,CHARM,CHASE,CHASM,CHEAP
50150 DATA CHEAT,CHECK,CHEEK,CHEER,CHICK
50155 DATA CHIEF,CHILD,CHIME,CHIRP,CHOKE
50160 DATA CHORD,CHORE,CHUNK,CHUTE,CIVIC
50165 DATA CIVIL,CLAMP,CLASH,CLASP,CLASS
50170 DATA CLAWS,CLEAN,CLEAR,CLICK,CLIFF
50175 DATA CLIMB,CLOAK,CLOCK,CLONE,CLOTH
50180 DATA CLOUD,CLOWN,CLUMP,COACH,COAST
50185 DATA COBRA,CODEX,COLON,COMIC,COPSE
50190 DATA CORAL,COUCH,COUGH,COULD,COUNT
50195 DATA CRACK,CRAFT,CRAMP,CRANE,CRANK
50200 DATA CRASH,CRASS,CRATE,CRAVE,CRAWL
50205 DATA CREAM,CREEK,CREEP,CRIME,CRISP
50210 DATA CROAK,CROFT,CROSS,CROWD,CROWN
50215 DATA CRUDE,CRUEL,CRUMB,CRUSH,CRUST
50220 DATA CRYPT,CURIO,CYCLE,CYNIC,DAILY
50225 DATA DAIRY,DAISY,DANCE,DANDY,DATUM
50230 DATA DAUNT,DEATH,DEBIT,DEBUG,DEBUT
50235 DATA DECAL,DECAY,DECOR,DECOY,DELAY
50240 DATA DELTA,DENIM,DENSE,DEPOT,DEPTH
50245 DATA DEVIL,DIARY,DIGIT,DILDO,DINER
50250 DATA DIRGE,DIRTY,DISCO,DITCH,DITTO
50255 DATA DODGE,DOGMA,DONOR,DOUBT,DOUGH
50260 DATA DOUSE,DOZEN,DRAFT,DRAIN,DRAMA
50265 DATA DRANK,DRAPE,DRAWL,DRAWN,DREAD
50270 DATA DRIFT,DRILL,DRINK,DRIVE,DRONE
50275 DATA DROOL,DROOP,DROVE,DRUID,DWARF
50280 DATA DWELL,EAGER,EAGLE,EARLY,EARTH
50285 DATA EASEL,EBONY,EDICT,EDIFY,EERIE
50290 DATA EIGHT,EJECT,ELBOW,ELDER,ELECT
50295 DATA ELITE,EMAIL,EMBED,EMBER,EMPTY
50300 DATA ENACT,ENEMA,ENEMY,ENJOY,ENTER
50305 DATA ENTRY,ENVOY,EPOCH,EPOXY,EQUAL
50310 DATA EQUIP,ERASE,ERECT,ERROR,ERUPT
50315 DATA ESSAY,EVADE,EVENT,EVICT,EVOKE
50320 DATA EXACT,EXILE,EXIST,FABLE,FACET
50325 DATA FAINT,FAIRY,FALSE,FANCY,FANNY
50330 DATA FATAL,FEAST,FEIGN,FELON,FENCE
50335 DATA FERAL,FERRY,FETCH,FEVER,FEWER
50340 DATA FIGHT,FILTH,FINAL,FLAIL,FLAIR
50345 DATA FLAKE,FLAME,FLANK,FLARE,FLASH
50350 DATA FLASK,FLEET,FLESH,FLINT,FLIRT
50355 DATA FLOAT,FLOCK,FLOOD,FLOOR,FLOUR
50360 DATA FLUFF,FLUID,FLUKE,FLUSH,FLUTE
50365 DATA FOCUS,FOLIO,FORAY,FORCE,FORGE
50370 DATA FORGO,FORTH,FOUND,FRAIL,FRAME
50375 DATA FRAUD,FREAK,FRESH,FRILL,FRISK
50380 DATA FROCK,FROST,FROTH,FROWN,FROZE
50385 DATA FRUIT,FUDGE,FUNGI,FUSSY,FUTON
50390 DATA GABLE,GAMMA,GAMUT,GAUDY,GAUGE
50395 DATA GAUNT,GAVEL,GEESE,GENIE,GENRE
50400 DATA GHOST,GHOUL,GIANT,GIZMO,GLADE
50405 DATA GLAND,GLASS,GLAZE,GLEAM,GLEAN
50410 DATA GLINT,GLOAT,GLOBE,GLOOM,GLOSS
50415 DATA GLOVE,GLYPH,GNASH,GNOME,GOOSE
50420 DATA GRAFT,GRAIL,GRAIN,GRAND,GRAPH
50425 DATA GRASP,GRASS,GRATE,GRAVE,GRAVY
50430 DATA GRAZE,GREAT,GREED,GREEN,GREET
50435 DATA GRIEF,GRILL,GRIME,GRIND,GROAN
50440 DATA GROOM,GRUNT,GUARD,GUESS,GUEST
50445 DATA GUIDE,GUILD,GUILE,GUILT,GUISE
50450 DATA GYPSY,HABIT,HAIKU,HAIRY,HAPPY
50455 DATA HARDY,HAREM,HARPY,HARSH,HASTE
50460 DATA HASTY,HAUNT,HAVEN,HAVOC,HEADY
50465 DATA HEARD,HEART,HEAVE,HEAVY,HEDGE
50470 DATA HEFTY,HEIST,HIKER,HINGE,HIPPO
50475 DATA HIPPY,HITCH,HOARD,HOBBY,HOIST
50480 DATA HONEY,HORDE,HORNS,HORNY,HORSE
50485 DATA HOTEL,HOUND,HOUSE,HOVEL,HOVER
50490 DATA HOWDY,HUMAN,HUMID,HUNCH,HURRY
50495 DATA HYDRO,HYENA,IDEAL,IDIOM,IDIOT
50500 DATA IGLOO,IMAGE,IMBUE,IMPEL,IMPLY
50505 DATA INANE,INCUR,INDEX,INDIE,INEPT
50510 DATA INERT,INFER,INGOT,INKED,INLAY
50515 DATA INLET,INNER,INSET,IRATE,IRONY
50520 DATA ISSUE,IVORY,JADED,JAUNT,JEANS
50525 DATA JELLY,JETTY,JEWEL,JIFFY,JOINT
50530 DATA JOIST,JOKER,JOLLY,JOULE,JOUST
50535 DATA JOWLS,JUDGE,JUICE,JUMBO,JUROR
50540 DATA KANJI,KARAT,KARMA,KAYAK,KEBAB
50545 DATA KHAKI,KINKY,KIOSK,KLUTZ,KNACK
50550 DATA KNAVE,KNEAD,KNEED,KNEEL,KNELL
50555 DATA KNIFE,KNOCK,KNOLL,KNOWN,KOALA
50560 DATA KRONE,KUDOS,LABEL,LABIA,LACES
50565 DATA LADEN,LADLE,LAGER,LAIRD,LAMER
50570 DATA LANCE,LANKY,LAPEL,LAPSE,LARGE
50575 DATA LARVA,LASER,LATCH,LATER,LATEX
50580 DATA LATHE,LATTE,LAUGH,LAYER,LEAKY
50585 DATA LEARN,LEASE,LEASH,LEAST,LEAVE
50590 DATA LEDGE,LEECH,LEERY,LEGAL,LEGIT
50595 DATA LEMON,LEMUR,LEPER,LEVEE,LEVEL
50600 DATA LEVER,LIBEL,LIEGE,LIGHT,LILAC
50605 DATA LIMBO,LIMIT,LINEN,LINGO,LITHE
50610 DATA LIVEN,LIVER,LIVID,LLAMA,LOAMY
50615 DATA LOATH,LOBBY,LOCAL,LOCUS,LODGE
50620 DATA LOGIC,LOLLY,LONER,LOOSE,LORRY
50625 DATA LOSER,LOTUS,LOUSE,LOVER,LOWER
50630 DATA LOWLY,LOYAL,LUCID,LUCKY,LUMEN
50635 DATA LUNAR,LUNCH,LUNGE,LUPUS,LURCH
50640 DATA LURID,LYNCH,LYRIC,MACHO,MACRO
50645 DATA MADAM,MAFIA,MAGIC,MAGMA,MAGUS
50650 DATA MAIZE,MAJOR,MANGA,MANGE,MANGO
50655 DATA MANIA,MANIC,MANOR,MANSE,MANTA
50660 DATA MARRY,MARSH,MASON,MATCH,MATTE
50665 DATA MAVEN,MAXIM,MAYBE,MAYOR,MEANT
50670 DATA MECCA,MEDAL,MEDIA,MEDIC,MELEE
50675 DATA MELON,MERCY,MERGE,MERIT,MERRY
50680 DATA MESSY,METAL,METRO,MICRO,MIGHT
50685 DATA MIMIC,MINCE,MINOR,MINUS,MIRTH
50690 DATA MODAL,MODEL,MODEM,MOGGY,MOGUL
50695 DATA MOIST,MONEY,MOOCH,MOODY,MOOSE
50700 DATA MORON,MORPH,MOTEL,MOTIF,MOTOR
50705 DATA MOTTO,MOUND,MOUNT,MOURN,MOUSE
50710 DATA MOUTH,MOVIE,MUCUS,MUDDY,MULCH
50715 DATA MURAL,MUSIC,NACHO,NAIVE,NAKED
50720 DATA NANNY,NAPPY,NASAL,NASTY,NAVEL
50725 DATA NERVE,NEVER,NICHE,NIECE,NIGHT
50730 DATA NINJA,NITRO,NODAL,NOISE,NOISY
50735 DATA NOMAD,NOOSE,NORTH,NOTCH,NURSE
50740 DATA NUTTY,NYLON,NYMPH,OAKEN,OASIS
50745 DATA OCCUR,OCEAN,OCTAL,OCTET,OFFAL
50750 DATA OFFER,OFTEN,OMEGA,OOZED,OPERA
50755 DATA OPIUM,OPTIC,ORATE,ORBIT,ORDER
50760 DATA ORGAN,OTHER,OTTER,OUGHT,OUNCE
50765 DATA OVARY,OVERT,OWNER,OXIDE,OZONE
50770 DATA PAGAN,PAGER,PAINT,PALEO,PANDA
50775 DATA PANEL,PANIC,PANSY,PANTS,PAPER
50780 DATA PARTY,PASTA,PASTE,PATCH,PATIO
50785 DATA PAUSE,PEACE,PEACH,PEARL,PEDAL
50790 DATA PENAL,PENCE,PENIS,PERCH,PERIL
50795 DATA PETAL,PETTY,PHASE,PHIAL,PHONE
50800 DATA PHONY,PHOTO,PIANO,PIECE,PILOT
50805 DATA PINCH,PINUP,PIQUE,PITCH,PITHY
50810 DATA PIXEL,PIZZA,PLACE,PLAID,PLAIN
50815 DATA PLAIT,PLANE,PLANK,PLANT,PLATE
50820 DATA PLAZA,PLEAD,PLEAS,PLONK,PLUMB
50825 DATA PLUME,PLUMP,PLUSH,POACH,POINT
50830 DATA POISE,POKER,POKES,POLAR,POLIO
50835 DATA POLKA,POLYP,POOCH,PORCH,PORES
50840 DATA POSSE,POTTY,POUCH,POUND,POWER
50845 DATA PRANK,PRAWN,PREEN,PRESS,PRICE
50850 DATA PRICK,PRIDE,PRIME,PRINT,PRIOR
50855 DATA PRISM,PRIVY,PRIZE,PROBE,PRONE
50860 DATA PRONG,PROOF,PROUD,PROVE,PROWL
50865 DATA PROXY,PRUDE,PULSE,PUNCH,PUPIL
50870 DATA PUREE,PURGE,PURSE,PUSHY,PUSSY
50875 DATA PUTTY,PYGMY,PYLON,QUAKE,QUALM
50880 DATA QUARK,QUART,QUASH,QUASI,QUEER
50885 DATA QUERY,QUEST,QUEUE,QUICK,QUIET
50890 DATA QUIRE,QUIRK,QUIRT,QUITE,QUOTA
50895 DATA QUOTE,RADAR,RADIO,RAINY,RANCH
50900 DATA RANDY,RANGE,RAPID,RATIO,RAVEN
50905 DATA RAZOR,REACH,REACT,REALM,REBEL
50910 DATA REFER,REGAL,REHAB,REIGN,REINS
50915 DATA RELAX,RELAY,RELIC,REMIT,REMIX
50920 DATA REPLY,RESIN,RETRO,RETRY,REUSE
50925 DATA REVEL,REVUE,RHINO,RHYME,RIDER
50930 DATA RIDGE,RIGHT,RIGID,RIGOR,RINSE
50935 DATA RISKY,RIVAL,ROACH,ROAST,ROBED
50940 DATA ROBIN,ROBOT,ROCKS,ROCKY,RODEO
50945 DATA ROGER,ROGUE,ROMAN,ROTOR,ROUGE
50950 DATA ROUGH,ROUND,ROUSE,ROUST,ROUTE
50955 DATA ROYAL,RUBLE,RUDDY,RUGBY,RUINS
50960 DATA RULER,RUNIC,RUNNY,RURAL,RUSTY
50965 DATA SABLE,SAINT,SALAD,SALON,SALSA
50970 DATA SALTY,SALVE,SALVO,SAMBA,SANDY
50975 DATA SARGE,SATAY,SATED,SATIN,SATYR
50980 DATA SAUCE,SAUCY,SAUNA,SAUTE,SAVVY
50985 DATA SCALE,SCALP,SCALY,SCAMP,SCANT
50990 DATA SCARE,SCARF,SCARY,SCENE,SCENT
50995 DATA SCOFF,SCOLD,SCONE,SCOOP,SCOOT
51000 DATA SCOPE,SCORE,SCORN,SCOUR,SCOUT
51005 DATA SCOWL,SCRAM,SCRAP,SCREW,SCRUM
51010 DATA SCUBA,SEDAN,SEMEN,SENSE,SEPIA
51015 DATA SERIF,SERUM,SERVE,SERVO,SETUP
51020 DATA SEVEN,SEVER,SEWER,SHACK,SHADE
51025 DATA SHADY,SHAFT,SHAKE,SHAKY,SHALE
51030 DATA SHALL,SHALT,SHAME,SHANK,SHAPE
51035 DATA SHARD,SHARE,SHARK,SHARP,SHAVE
51040 DATA SHAWL,SHEAF,SHEAR,SHEEN,SHEEP
51045 DATA SHEER,SHEET,SHELF,SHELL,SHINE
51050 DATA SHINY,SHIRE,SHIRT,SHOAL,SHOCK
51055 DATA SHOES,SHOOT,SHORE,SHORT,SHOUT
51060 DATA SHOVE,SHRED,SHREW,SHRUB,SHRUG
51065 DATA SHUNT,SIEGE,SIEVE,SIGHT,SIGMA
51070 DATA SILKY,SILLY,SINCE,SINEW,SINGE
51075 DATA SINUS,SIREN,SIXTY,SKATE,SKEIN
51080 DATA SKINT,SKIRT,SKULK,SKULL,SKUNK
51085 DATA SLACK,SLAIN,SLANG,SLANT,SLASH
51090 DATA SLATE,SLEEK,SLEEP,SLEET,SLEPT
51095 DATA SLICE,SLICK,SLIDE,SLIME,SLING
51100 DATA SLOOP,SLOPE,SLOSH,SLOTH,SLUMP
51105 DATA SLUNG,SLUSH,SLUTS,SMACK,SMALL
51110 DATA SMART,SMASH,SMEAR,SMELL,SMILE
51115 DATA SMIRK,SMITE,SMITH,SMOCK,SMOKE
51120 DATA SNACK,SNAIL,SNAKE,SNARE,SNEAK
51125 DATA SNEER,SNIPE,SNOOP,SNORE,SNORT
51130 DATA SNOUT,SOLAR,SOLID,SOLVE,SONAR
51135 DATA SONIC,SOOTH,SORRY,SOUND,SPACE
51140 DATA SPADE,SPANK,SPARE,SPARK,SPASM
51145 DATA SPAWN,SPEAK,SPEAR,SPECK,SPEED
51150 DATA SPELL,SPEND,SPENT,SPERM,SPICE
51155 DATA SPICY,SPIEL,SPILL,SPINE,SPIRE
51160 DATA SPITE,SPOIL,SPOKE,SPOOF,SPOOK
51165 DATA SPOOL,SPOON,SPORE,SPORT,SPOUT
51170 DATA SPRAY,SPREE,SPRIG,SPUNK,SPURN
51175 DATA SPURT,SQUAD,SQUAT,SQUID,STACK
51180 DATA STAFF,STAGE,STAIN,STAIR,STAKE
51185 DATA STALE,STALK,STALL,STAMP,STAND
51190 DATA STARE,STARK,START,STASH,STATE
51195 DATA STEAK,STEAL,STEAM,STEED,STEEL
51200 DATA STEEP,STEER,STENT,STERN,STICK
51205 DATA STIFF,STILL,STILT,STING,STINK
51210 DATA STINT,STOAT,STOCK,STOIC,STOKE
51215 DATA STOLE,STOMP,STONE,STONY,STOOD
51220 DATA STOOL,STOOP,STORE,STORK,STORM
51225 DATA STORY,STOUT,STOVE,STRAP,STRAW
51230 DATA STRAY,STRUM,STRUT,STUCK,STUDY
51235 DATA STUFF,STUMP,STUNG,STUNT,STYLE
51240 DATA SUAVE,SUEDE,SUGAR,SUITE,SUNNY
51245 DATA SUPER,SURGE,SURLY,SUSHI,SWAMP
51250 DATA SWARM,SWEAR,SWEAT,SWEDE,SWEEP
51255 DATA SWEET,SWELL,SWEPT,SWIFT,SWILL
51260 DATA SWINE,SWING,SWIPE,SWIRL,SWISH
51265 DATA SWOON,SWOOP,SWORD,SWORE,SWORN
51270 DATA SYNTH,SYRUP,TABOO,TAFFY,TAINT
51275 DATA TAKEN,TALON,TANGO,TANGY,TAPAS
51280 DATA TARDY,TASTE,TASTY,TAUNT,TEACH
51285 DATA TEASE,TEDDY,TEMPO,TEMPT,TENET
51290 DATA TENOR,TENSE,TENTH,TEPID,TERSE
51295 DATA THEFT,THEIR,THEME,THERE,THESE
51300 DATA THETA,THICK,THIEF,THIGH,THING
51305 DATA THINK,THIRD,THONG,THORN,THOSE
51310 DATA THREE,THREW,THROB,THROE,THROW
51315 DATA THUMB,THYME,TIARA,TIBIA,TIDAL
51320 DATA TIGER,TIGHT,TIMID,TITAN,TITHE
51325 DATA TITLE,TITTY,TOAST,TODAY,TONER
51330 DATA TONGS,TONIC,TONNE,TOOLS,TOOTH
51335 DATA TOPAZ,TOPIC,TORCH,TORSO,TOTAL
51340 DATA TOUCH,TOUGH,TOWEL,TOWER,TOXIC
51345 DATA TOXIN,TRACE,TRACK,TRACT,TRADE
51350 DATA TRAIL,TRAIN,TRAIT,TRAMP,TRAWL
51355 DATA TREAD,TREAT,TREND,TRESS,TRIAD
51360 DATA TRIAL,TRIBE,TROLL,TROUT,TROVE
51365 DATA TRUCE,TRUCK,TRUNK,TRUSS,TRUST
51370 DATA TRUTH,TRYST,TULIP,TUNER,TUNIC
51375 DATA TURBO,TURDS,TUTOR,TWEAK,TWEED
51380 DATA TWICE,TWINE,TWIST,UDDER,ULCER
51385 DATA ULTRA,UNCAP,UNCLE,UNCUT,UNDER
51390 DATA UNDUE,UNIFY,UNION,UNITE,UNLIT
51395 DATA UNTIE,UNTIL,UNZIP,UPPER,UPSET
51400 DATA URBAN,USAGE,USUAL,USURP,USURY
51405 DATA UTTER,VAGUE,VALET,VALID,VALUE
51410 DATA VALVE,VAPID,VAULT,VAUNT,VENOM
51415 DATA VENUE,VERGE,VERSE,VERVE,VICAR
51420 DATA VIDEO,VIGIL,VIGOR,VILLA,VINYL
51425 DATA VIOLA,VIPER,VIRAL,VIRUS,VISIT
51430 DATA VISOR,VISTA,VITAL,VIVID,VIXEN
51435 DATA VOCAL,VODKA,VOGUE,VOMIT,VOUCH
51440 DATA VOWEL,VULVA,WAFER,WAGER,WAGES
51445 DATA WAGON,WAIST,WAIVE,WALTZ,WASTE
51450 DATA WATCH,WATER,WAXEN,WEARY,WEAVE
51455 DATA WEDGE,WEIGH,WEIRD,WELSH,WENCH
51460 DATA WHACK,WHALE,WHARF,WHEAT,WHEEL
51465 DATA WHELM,WHERE,WHICH,WHIFF,WHILE
51470 DATA WHINE,WHIRL,WHITE,WHOLE,WHORE
51475 DATA WHOSE,WIDOW,WIDTH,WIELD,WINCE
51480 DATA WINCH,WITCH,WITTY,WOMAN,WOMEN
51485 DATA WOODS,WOODY,WORLD,WORRY,WORSE
51490 DATA WORST,WORTH,WOULD,WOUND,WOVEN
51495 DATA WRATH,WREAK,WRECK,WREST,WRING
51500 DATA WRIST,WRITE,WRONG,WURST,XENON
51505 DATA YACHT,YODEL,YOUNG,YOUTH,YUMMY
51510 DATA ZEBRA
