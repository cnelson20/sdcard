#CONTROLCODES 1
#SYMFILE "@:HANGMANB.SYM"

STARTUP:
PRINT "{ISO OFF}{UPPER}":GOSUB SETREGVARS:GOSUB THINFONT
POKE C.REG,1
SYS SET.MEM
LAST.BANK = PEEK(A.REG):IF LAST.BANK=0 THEN LAST.BANK=256
LAST.BANK = LAST.BANK - 1
BANKS.AVAILABLE = LAST.BANK-10
NEXT.BANK=2

## HERE I'M GETTING THE COMMUNICATION SIGNATURES FROM MY
## SUB MODULES.
GOSUB GETZPSTRING
I = PEEK($400):X = PEEK($401):C = PEEK($402):J=PEEK($403)
FILESCHECKED= (I=$FF) AND (X=$EC) AND (C=$FF) AND (J=$FE)

## FILESCHECKED=TRUE MEANS I'VE BEEN LOADED BACK FROM
## FILECHECKER R15H IS A BIT ENCODED VARIABLE
## TELLING ME WHAT FILES ARE PRESENT
IF FILESCHECKED THEN FFLAGS=PEEK(R15H)
## R15L WILL HOLD COUNT OF WORD FILES
IF FILESCHECKED THEN FC%=PEEK(R15L)

## ONLY SET THE GRAPHICS MODE ON THE FIRST RUN
DOMODE%=LEFT$(ZP.STRING$,3)<>"ZSM" AND (NOT FILESCHECKED)
IF DOMODE% THEN SCREEN $80:RECT 0,0,319,239,$C1

IF NOT FILESCHECKED THEN PRINT "{YEL} LOADING FILE CHECKER{WHT}":PRINT
IF NOT FILESCHECKED THEN F$="CHECKFILES":GOTO LOADER

F$="HANGMAN.INI"
PASS2=LEFT$(ZP.STRING$,3)="ZSM"

IF (NOT DOMODE%) AND PASS2 THEN MAIN
CLS:PRINT "{CR}{YEL} F1{GRN} TO EDIT {WHT}";F$:SLEEP 180
GET X$:IF ASC(X$)=133 THEN SCREEN 1:GOSUB EDITINI:SCREEN $80:RECT 0,0,319,239,$C2
GOTO MAIN

BITMAPS:
  DATA 128,64,32,16,8,4,2,1,0

CONTROLS:
  DATA 32,40,192,48,32,55,238,62,32,72,143,80,32,88,143,96,32,104,214,110
  DATA 28,155,62,170,201,195,219,207,226,195,244,207


MAIN:
  PRINT
  IF NOT PASS2 THEN PRINT "{GRN}";FC%;"{WHT} WORD FILES":PRINT
  IF NOT PASS2 THEN PRINT " FFLAGS: ";"{GRN}";BIN$(FFLAGS):PRINT "{WHT}"

  IF DOMODE% THEN LOCATE 1,1
  DIM NV$(15), NN$(15),NV%,FV%

  IF NOT PASS2 THEN PRINT "{CR}{YEL} INITIALIZATION{GRN}...";RPT$(32,10);"{CR}":MOUSE 1
  PRINT "{CR} {YEL}READING: {WHT}";F$;"{GRN}";:GOSUB READINI
  I$="USEZSM":GOSUB GETINIVALUE
  IF I$="OFF" THEN UZSM%=0:GOTO MAIN2
  GOSUB GETZPSTRING:DOLOAD%=LEFT$(ZP.STRING$,3)<>"ZSM":IF DOLOAD% THEN DOZSMLOAD
  UZSM%=-1
  IF ZP.STRING$="ZSMNOTFOUND" THEN UZSM%=0
  GOTO MAIN2

DOZSMLOAD:
  POKE R15L, FC%:POKE R15H, FFLAGS:F$="ZSMLOADER":GOTO LOADER

MAIN2:
  IF NOT UZSM% THEN SKIPEGG
  F$="EASTEREGG.ZCM":GOSUB FILEEXISTS
  ZCVOLUME=15:IF FE% THEN ZCVOLUME=15:GOSUB PLAYZCMIMMEDIATE:SLEEP 30
SKIPEGG:
  PF$="DPAL.BIN":I$="PALFILE":GOSUB GETINIVALUE
  F$=I$:GOSUB FILEEXISTS:IF FE% THEN PF$=I$

  DIM TC%(2,8),AL%(4,26),PC%(8,4),MC$(5),DI$(3),PL%(2,1),PL$(2),WN%(2),DCHAR%(8)
  DIM MUSIC.NAME$(3),ZNUM%(3),USING.LST%(3),ZSM.RANDOM%(3),MUSIC.PAUSED%(3)
  LS%=-1

## CLEAR OUR FLAGS STUFF OUT OF GOLDENRAM
  FOR I=$400 TO $40A:POKE I, 0:NEXT I
  GOSUB INITMUSICFILES
  PRINT "{CR}{CR}{YEL} INITIALIZING VARIABLES{GRN}..";:GOSUB VARINIT:PRINT "{GRN}DONE{WHT}":PRINT
  PRINT "{CR}{WHT} BASIC RAM FREE:";"{GRN}";FRE(0);"{WHT}":PRINT "{CR}{GRN}";BANKS.AVAILABLE;"{WHT} BANKS OF HI-RAM AVAILABLE":SLEEP 120
  IF UZSM% THEN ZS=1:MSGMODE=3:GOSUB PLAYSONG:LOOP.ON=0:GOSUB SETZSMLOOP
  GOSUB CLSGRAPH:GOSUB SETDEFPAL:GOSUB SETDRAWPAL:GOSUB HIDEGRAPHICS:GOSUB INTROPAL:GOSUB BLOCKFROWN
  DC$=CHR$(255):GOSUB HBANNER:DM$="{YEL}<{WHT}ESC{YEL}>{WHT} SKIPS INTRO":GOSUB PLAINMSG
  GOSUB PALANIMST
  FOR I=1 TO 150
    GOSUB PALANIM1:GET X$:IF X$=CHR$(27) THEN I=150
  NEXT I
  IF X$=CHR$(27) THEN ENDINTRO
  IF (FFLAGS OR %00010000) <> FFLAGS THEN RECT 0,0,XLIMIT,YLIMIT,$C3:GOTO SKIPBACK
  BVLOAD "SPLASHBACK.IMG",8,0,0
SKIPBACK:
  P1=8:P2=$DF:GOSUB COPYPALET:GOSUB SHOWGRAPHICS:SLEEP 30:GET X$:IF X$=CHR$(27) THEN ENDINTRO
  GOSUB HIDETEXT:GOSUB CLRMSG
  IF (FFLAGS OR %00100000) <> FFLAGS THEN SKIPSPLASH1
  F$="SPLASH.IMG":X1=0:Y1=0:PUTIMAGE=-1:BLITWIDTH=320:BLITLINES=240:GOSUB BITBLT:GOTO ENDSPLASH
SKIPSPLASH1:
  GOSUB DRAWGALLOWS
  P1=$C0:P2=$F2:GOSUB COPYPALET:P1=$C3:P2=$C0:GOSUB COPYPALET:GOSUB DRAWNOOSE
  DM$="":GOSUB PLAINMSG
ENDSPLASH:
  SLEEP 45:GOSUB SHOWTEXT

STARTINTRO:
  GOSUB PALANIMST:GOSUB PALANIM1:X=TI+2200

CONTINTRO:
   GET X$:SLEEP 5:GOSUB PALANIM1
   IF X$="{ESC}" THEN ENDINTRO
   IF X>TI THEN CONTINTRO

SCROLL:
  GOSUB FLUSHKEYS
  FOR X=1 TO 31
    LOCATE 1,1:PRINT CHR$(145):SLEEP 10:GET X$:IF X$="{ESC}" THEN X=31
  NEXT X

ENDINTRO:
  GOSUB CLSGRAPH:GOSUB SETDRAWPAL:GOTO GAMEMENU

HBANNER:
  X=5:Y=5:UC=1:WD$="H"+CHR$(193)+CHR$(206)+CHR$(199):GOSUB BIGWORD:X=8:Y=14:WD$="M"+CHR$(193)+CHR$(206):GOSUB BIGWORD:RETURN

PALANIMST:
    P1=13
PALANIM1:
    IF P1=2 THEN P2=13:GOTO SKIP1
    P2=P1-1
SKIP1:
    GOSUB SWAPPALET:P1=P1-1:IF P1<2 THEN P1=13
    RETURN

INTROPAL:
    P1=$58:P2=13
PLOOP1:
  GOSUB COPYPALET:P2=P2-1:P1=P1-1:IF P2>1 THEN PLOOP1
  P1=$34:P2=10:GOSUB COPYPALET:P1=1:P2=11:GOSUB COPYPALET:P1=$C7:P2=12:GOSUB COPYPALET
  FOR I=2 TO 9:TC%(2,I-1)=0:TC%(1,I-1)=I:NEXT I:RETURN

BIGWINANIM:
  GOSUB CLSGRAPH:MOUSE 0:GOSUB BLOCKSMILE
  F$="ANIMS/ANIMPAL.BIN":GOSUB FILEEXISTS:F=FE%
  F$="ANIMS/ANIM16.BIN":GOSUB FILEEXISTS:F2=FE%
  F$="ANIMS/ANIM50.BIN":GOSUB FILEEXISTS:F3=FE%
  F1%= F AND F2 AND F3
  IF F1% THEN BVLOAD "ANIMS/ANIMPAL.BIN",8,1,$FA00
  P1=$10:R%=0:G%=0:B%=0:GOSUB RGBPAL:RECT 0,0,319,239,$10:R%=13:G%=13:B%=13:P1=15:GOSUB RGBPAL
  P1=7:R%=13:G%=0:B%=3:GOSUB RGBPAL:P1=252:R%=6:G%=8:B%=2:GOSUB RGBPAL
  P1=253:R%=5:G%=11:GOSUB RGBPAL:RECT 0,234,319,239,252:RECT 0,175,319,233,153:UC=0:DC$=CHR$(255):COLOR 7,0
  X=1:Y=3:WD$=DM$:GOSUB BIGWORD:X=1:Y=12:WD$=D2$:GOSUB BIGWORD
  GET X$:IF X$="{ESC}" THEN ANIMSTOP
  I=1:GOSUB FIREWORKS:IF NOT F1% THEN ANIMSTOP
  IF ENDANIM THEN ANIMSTOP
  CHAR 4,158,156,CHR$($0C)+"A REPRIEVE !!!":CHAR 4,170,15,CHR$($0C)+"LETS GET OUT OF HERE....":CHAR 160,170,156, CHR$($0C)+" .. EXIT .. STAGE RIGHT !!"
ANIMLOOP:
 FOR I=1 TO 50
   F$=STR$(I):F$=RIGHT$(F$,LEN(F$)-1):F$="ANIMS/ANIM"+F$+".BIN":BVLOAD F$,8,0,$DAC0
   GET X$:IF X$="{ESC}" THEN I=50
 NEXT I
##   GOTO ANIMLOOP
ANIMSTOP:
  GOSUB HIDEGRAPHICS:GOSUB CLSGRAPH:GOSUB SETDEFPAL:GOSUB SETDRAWPAL:MOUSE 1:GOSUB SHOWGRAPHICS:RETURN

FIREWORKS:
STARTLOOP:
  FOR WF=0 TO 6
GETRANDS:
  X=INT(RND(.)*300+15):Y=INT(RND(.)*90+30):DE=2:L1=X-2:L2=Y-2:L3=318-X:L4=146-Y:SIZE=INT(RND(.)*25)
  IF SIZE>L1 OR SIZE>L2 OR SIZE>L3 OR SIZE>L4 THEN GETRANDS
  ZCM.NUMBER=3:GOSUB ZCMPLAY
  FOR UP=172 TO Y STEP -1
    RECT X, UP,X+1,UP+1, $3F:FOR I=0 TO DE:NEXT I:RECT X, UP,X+1,UP+1, $10
  NEXT UP

##    PSET X,Y, 7
  CD=TI+210:ZCM.NUMBER=4:GOSUB ZCMPLAY
  FOR O=0 TO 32 STEP 4
    FOR CX = 0 To 6 STEP 2
      B1=INT(RND(.)*SIZE)+O:B2=INT(RND(.)*SIZE)+O
      X1=X-INT(RND(.)*B1):Y1=Y-RND(.)*B2:GOSUB ADJUST
      RECT X1,Y1,X1+1,Y1+1,CC:X1=X+RND(.)*B1:Y1=Y+RND(.)*B2
      GOSUB ADJUST:PSET X1%,Y1%,CC:X1=X+RND(.)*B1:Y1 =Y-RND(.)*B2
      GOSUB ADJUST:RECT X1,Y1,X1+1,Y1+1,CC
      X1=X-RND(.)*B1:Y1=Y+RND(.)*B2:GOSUB ADJUST:RECT X1,Y1,X1+1,Y1+1,CC
    NEXT CX
  NEXT O
  RECT 0,0,319,155,$10
  GET X$:IF X$="{ESC}" THEN WF=6
WAITZCM:
  IF TI<CD THEN WAITZCM
  NEXT WF
  ENDANIM=FALSE
  IF X$="{ESC}" THEN ENDANIM=TRUE
RETURN

ADJUST:
  IF X1>318 THEN X1=318
  IF X1<0 THEN X1=0
  IF Y1<0 THEN Y1=0
  IF Y1>170 THEN Y1=170
  CC=INT(RND(.)*254)+1:RETURN

BLOCKFROWN:
  VPOKE 1,$F2F0,$3C:VPOKE 1,$F2F1,$7E:VPOKE 1,$F2F2,$DB:VPOKE 1,$F2F3,$FF
  VPOKE 1,$F2F4,$C3:VPOKE 1,$F2F5,$BD:VPOKE 1,$F2F6,$7E:VPOKE 1,$F2F7,$3C:RETURN

BLOCKSMILE:
  VPOKE 1,$F2F4,$BD:VPOKE 1,$F2F5,$C3:RETURN

GAMEMENU:
  GOSUB HIDEGRAPHICS:GOSUB SETDEFPAL:GOSUB SETDRAWPAL:DM$="HANGMAN GAME MENU":GOSUB GMENUBACK:GOSUB SHOWGRAPHICS
  IF CH<1 OR CH>5 THEN CH=1
  X=5:Y=4:DI=2
    FOR I=1 TO 5
      Y=Y+2:LOCATE Y,X:COLOR 1,0:PRINT RIGHT$(STR$(I),1);:PRINT ":{PUR}"MC$(I);
    NEXT I

    W=34:H=14:C1=1:C2=$17:C3=$C2:C4=$1E:L$="ENTER":X=28:Y=155
    GOSUB DRAWBTN:LOCATE 21,9:PRINT "{PUR}TO CONFIRM CHOICE";
    LOCATE 24,20:PRINT "{PUR}DIFFICULTY:";:PRINT "{WHT};DI$(DI);
    W=18:H=12:L$="<-":X=201:Y=195:GOSUB DRAWBTN:X=226:L$="->":GOSUB DRAWBTN
    LOCATE 17,5:PRINT "{BLUE}DEMO MODE ALSO CREATES";:LOCATE 18,5:PRINT "ALL THE GAME BITMAP FILES.";:GOSUB FLUSHKEYS
    GOSUB ZSMGETSTATE:IF ZSM.IS.PLAYING THEN LOOP.ON=0:GOSUB SETZSMLOOP

MENUSHOW:
  IF CH=5 THEN Y=14
  IF CH=4 THEN Y=12
  IF CH=3 THEN Y=10
  IF CH=2 THEN Y=8
  IF CH=1 THEN Y=6
  LOCATE Y,5:COLOR 7,4:PRINT CHR$($7A);" ";
  PRINT"{WHT}";MC$(CH);RPT$(32,27-LEN(MC$(CH)));
  COLOR 1,0:LOCATE 24,31:PRINT RPT$(32,10);:LOCATE 24,31:PRINT DI$(DI);

MENUGET:
  GET X$:IF MB<>1 THEN SKIPMOUSE3
  CX=MX:CY=MY:I=1:MC%=0

CHKMOUSE:
  MC%=(CX>=PC%(I,1)) AND (CX<=PC%(I,3)) AND (CY>=PC%(I,2)) AND (CY<=PC%(I,4))
  IF NOT MC% THEN I=I+1:IF I>8 THEN X$="Z":GOTO ENDMOUSE
  IF NOT MC% THEN CHKMOUSE
  IF I<6 THEN X$=STR$(I):X$=RIGHT$(X$,1)
  IF I=6 THEN X$=CHR$(13)
  IF I=7 THEN X$=CHR$(157)
  IF I=8 THEN X$=CHR$(29)
ENDMOUSE:
  IF MB<>0 THEN ENDMOUSE

SKIPMOUSE3:
  IF X$="" THEN MENUGET
  K=ASC(X$):IF K<>29 AND K<>157 THEN GOSUB CLICK
  LOCATE Y,5:COLOR 1,0:PRINT RIGHT$(STR$(CH),1);
  PRINT ":{PUR}"MC$(CH);RPT$(32,28-LEN(MC$(CH)));
  IF CH=5 AND K=27 THEN K=13
  IF K=27 THEN CH=5
  IF K>=49 AND K<=53 THEN CH=VAL(X$)
  IF K=145 THEN CH=CH-1
  IF K=17 THEN CH=CH+1
  IF K=157 THEN DI=DI-1:X=201:L$="<-"
  IF K=29 THEN DI=DI+1:X=226:L$="->"
  IF K=29 OR K=157 THEN Y=195:W=18:H=12
  IF K=13 THEN L$="ENTER":W=34:H=14:X=28:Y=155
  IF K=29 OR K=157 OR K=13 THEN GOSUB PUSHBTN
  IF K=13 THEN CLS:GOTO STARTGAME
  IF CH>5 THEN CH=1
  IF CH<1 THEN CH=5
  IF DI>3 THEN DI=1
  IF DI<1 THEN DI=3
  GOTO MENUSHOW

STARTGAME:
    X=RND(-RND(0))
    IF CH=5 THEN CLEANUP
    IF CH=4 THEN SKIPNAMES
    GOSUB PLAYERNAMES
SKIPNAMES:
    ROUND=1:CP%=1:WN%(1)=0:WN%(2)=0
LETSGO:
    ZS=2:MSGMODE=1:GOSUB PLAYSONG
    NE%=-1:COLOR 1,0:CLS
    IF CH=1 OR CH=2 THEN PLAYCOMP
    IF CH=3 THEN HEAD2HEAD
    IF CH=4 THEN GOSUB PLAYDEMO:GOSUB CLSGRAPH:CH=4:GOTO GAMEMENU

PLAYCOMP:
    GOSUB WORDFROMFILE:GOTO HANGMAN
HEAD2HEAD:
    GOSUB PLAYERWORD:GOSUB HIDETEXT:COLOR 1,0:GOSUB CLSGRAPH:GOSUB SHOWTEXT

HANGMAN:
    IF COMPLETE.DRAW THEN GOSUB HIDETEXT
    GOSUB SCORECARD:GOSUB GAMEBOARD:GOSUB FLUSHKEYS

HANDLEINPUT:
    GOSUB MANAGEMUSIC
    GET X$:HF%=0
    IF X$=CHR$(27) THEN GOSUB EXITCHECK:GOSUB FLUSHKEYS
    IF HF% THEN CLS:GOTO GAMEMENU
    IF MB=1 THEN HNDLMOUSE
    IF X$="" THEN HANDLEINPUT
    GOTO SKIPMOUSE2
HNDLMOUSE:
    CX=MX:CY=MY:X=1:H1=0:X$="":IF CY>131 THEN X=15
NEXTBTN:
    X1=AL%(3,X):X2=X1+14:Y1=AL%(4,X):Y2=Y1+14
    IF CX>=X1 AND CX<=X2 AND CY>=Y1 AND CY<=Y2 THEN X$=CHR$(AL%(2,X)):H1=1
    IF H1=0 AND X<26 THEN X=X+1:GOTO NEXTBTN
RELEASEMOUS:
    IF MB<>0 THEN RELEASEMOUS
    IF H1 THEN SKIPMOUSE2
    LOCATE 1,1:GOSUB BOING:GOTO HANDLEINPUT


SKIPMOUSE2:
    LL=ASC(X$):IF LL<65 OR LL>90 THEN LOCATE 1,1:GOSUB BOING:GOTO HANDLEINPUT
    A=LL-64
    IF AL%(1,A)=1 THEN LOCATE 1,1:GOSUB BOING:GOTO HANDLEINPUT
    CL$=CHR$(LL):H=14:W=14:X=AL%(3,A):Y=AL%(4,A):L$=CHR$(LL)
    GOSUB PUSHBTN:HF%=0
    GOSUB CHECKLETTER:IF HF% THEN GOSUB WORDBOX:GOSUB DRUMROLL
    GOSUB FLUSHKEYS
    IF HF% THEN IF HW$=DW$ THEN COMPLETE.DRAW=FALSE:GOTO WONROUND
    IF NOT HF% THEN NE%=0:GOSUB BODYPART:GOSUB PARTSCOUNT
    GOSUB FLUSHKEYS
    IF LF% THEN COMPLETE.DRAW=FALSE:GOTO LOSTROUND
    SLEEP 10:GOSUB DISABLEBTN
    GOTO HANDLEINPUT

WONROUND:
  WN%(CP%)=WN%(CP%)+1:PL%(CP%,1)=PL%(CP%,1)+1
  IF WN%(CP%)/5=INT(WN%(CP%)/5) THEN DM$=RIGHT$(STR$(WN%(CP%)),LEN(STR$(WN%(CP%)))-1)+" "+CHR$(201)+CHR$(206):D2$=CHR$(193)+" R"+CHR$(207)+CHR$(215):GOSUB BIGWINANIM:GOTO WON2
  IF NE% THEN DM$="FLAW":D2$=" LESS":WN%(CP%)=0:GOSUB BIGWINANIM:GOTO WON2
  GOSUB GAMEMSG:COLOR 7,4:LOCATE 16,21:PRINT "YOU WON {WHT}!":
  GOSUB PLAYFANFARE:GOSUB SUNSMILE
WON1:
  IF ZTIMER > TI THEN WON1
  SLEEP 60:ZTIMER=0:GOSUB POPSONG
WON2:
  GOSUB FLUSHKEYS:GOSUB ADVROUND:GOTO LETSGO

LOSTROUND:
  DW$=HW$:GOSUB WORDBOX:IF NOT UZSM% THEN FMDRUM 7,38:SLEEP 10:FMDRUM 7,43:SLEEP 15:FMDRUM 7,55:SLEEP 20:FMDRUM 7,49
  WN%(CP%)=0:GOSUB LOSSSCREEN:GOSUB ADVROUND:GOSUB SWAPSONGBACK:GOTO LETSGO

ADVROUND:
  LF%=0:HF%=0
  IF CH=1 THEN ROUND=ROUND+1:RETURN
  IF CP%=2 THEN ROUND=ROUND+1
  IF CP%=2 THEN CP%=1:RETURN
  IF CP%=1 THEN CP%=2
  RETURN

EXITCHECK:
  GOSUB GAMEMSG
  COLOR 1,4
  LOCATE 15,19:PRINT "BACK TO MENU";
  LOCATE 18,23:PRINT "{YEL}Y";:PRINT "{WHT}/{YEL}N {GRN}?{WHT}";
CHKLOOP:
  GET X$:GOSUB MANAGEMUSIC:IF X$="" THEN CHKLOOP
  IF X$="Y" THEN HF%=-1
  IF X$="Y" OR X$="N" OR X$=CHR$(27) THEN ENDCHK
  GOSUB BOING:GOTO CHKLOOP
ENDCHK:
  COLOR 0,0:X=16:Y=12:H=11:W=18:GOSUB TEXTBOX:COLOR 1,0:RETURN

GAMEMSG:
  COLOR 4,6:X=16:Y=12:H=11:W=18:GOSUB TEXTBOX
  COLOR 7,4:X=17:Y=13:H=9:W=16:GOSUB TEXTBOX:RETURN

BODYPART:
  GOSUB BOING:LF%=0:GOSUB DRAWMASK
  ON BP GOSUB DRWLEFTLEG, DRWRIGHTLEG, DRWLEFTARM, DRWRIGHTARM, DRWTORSO, DRAWFACE, DRAWNOOSE, DRAWGALLOWS
  GOTO SHWCLUE:
SHWROUND:
  LOCATE 26,25:PRINT "ROUND:{YEL}";:PRINT ROUND;
SHWCLUE:
  GOSUB SHOWCLUE
  BP=BP-1:IF BP=0 THEN LF%=-1
  GOSUB FLUSHKEYS:GOSUB ENDCHK:RETURN

DRAWMASK:
  COLOR 6,0:LOCATE 12,17:H=11:W=18:Y=12:PRINT RPT$($AF,W-2)
  FOR I=1 TO H-2
    Y=Y+1:LOCATE Y,16:PRINT CHR$($A7);RPT$($76,16);CHR$($A5);
  NEXT I
  COLOR 7,4:LOCATE 16,20:PRINT "DRAWING  !!";:COLOR 4,0:RETURN

GAMEBOARD:
  IF COMPLETE.DRAW THEN COLOR 1,0:GOSUB SETDRAWPAL
  IF DI=1 THEN BP=8
  IF DI=2 THEN BP=7
  IF DI=3 THEN BP=6
  GOSUB BACKGROUND
  IF DI>1 THEN GOSUB DRAWGALLOWS
  IF DI=3 THEN GOSUB DRAWNOOSE
  DW$=""
  FOR I=1 TO LEN(HW$)
    X$=MID$(HW$,I,1)
    IF ASC(X$)>=65 AND ASC(X$)<=90 THEN DW$=DW$+CHR$($7A):GOTO SKIP5
    DW$=DW$+X$
SKIP5:
  NEXT I
  GOSUB WORDBOX:GOSUB PARTSCOUNT:IF COMPLETE.DRAW THEN GOSUB SHOWTEXT
  GOSUB LETTERPANL:GOSUB SHOWCLUE:RETURN

WORDBOX:
  COLOR 4,6:X=17+(7-(LEN(HW$)/2)):Y=5:H=3:W=LEN(HW$)+2:GOSUB TEXTBOX:COLOR 1:LOCATE 6,18+(7-(LEN(HW$)/2)):PRINT DW$;:RETURN

SHOWCLUE:
  COLOR 1,0:IF NOT UC% THEN GOSUB CLRMSG:RETURN
  IF LEN(HC$)>38 THEN HC$=LEFT$(HC$,38)
  DM$=HC$:GOSUB PLAINMSG:RETURN

CHECKLETTER:
     L=LEN(HW$):X$=""
     FOR X=1 TO L
        IF MID$(HW$,X,1)=CL$ THEN X$=X$+CL$:HF%=-1:GOTO SKIP8
        X$=X$ + MID$(DW$,X,1)
SKIP8:
     NEXT X
     DW$=X$:RETURN

SCORECARD:
     IF COMPLETE.DRAW THEN CLS
     LOCATE 1,14:PRINT "{BLUE}";PL$(CP%);
     PRINT "{PUR} PLAYING";:LOCATE 2,14
     PRINT "{PUR} WINS:";:PRINT "{RED}";PL%(CP%,1);"  ";
     PRINT "{PUR}LOSSES:";:PRINT "{RED}";(ROUND-1)-PL%(CP%,1);
     LOCATE 26,25:PRINT "{WHT}ROUND:";:PRINT "{YEL}";ROUND;
     RETURN

PLAYDEMO:
   FORCEDRAW=TRUE
   CP%=1:PL$(1)="DEMO HANGMAN":GOSUB CLSGRAPH
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB SKIPLOAD:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWGALLOWS:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWNOOSE:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRAWFACE:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWTORSO:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWRIGHTARM:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWLEFTARM:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWRIGHTLEG:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB DRWLEFTLEG:GOSUB MANAGEMUSIC
   GET X$:IF X$="{ESC}" THEN DONEDEMO
   GOSUB LOSSSCREEN
   GOSUB SWAPSONGBACK:GOSUB PAUSEMUSIC
   FOR I=1 TO 3:MUSIC.PAUSED%(I)=0:NEXT I
DONEDEMO:
   FORCEDRAW=FALSE
   PL$(1)="":RETURN

DRUMROLL:
  ZCM.NUMBER = 0
  GOSUB ZCMPLAY
  RETURN

PLAYERWORD:
     COMPLETE.DRAW=TRUE
     GOSUB CLSGRAPH
     IF CP%=1 THEN AP%=2
     IF CP%=2 THEN AP%=1
     DM$="GAME WORD AND CLUE FOR YOUR OPPONENT":GOSUB GMENUBACK
     LOCATE 7,3:PRINT "{LIGHT GREEN};PL$(AP%);
     LOCATE 8,3:PRINT "{PUR}ENTER THE FOLLOWING.";
     IY=12:IX=8:IT=1:LOCATE 10,5:PRINT "{PUR}ENTER WORD TO PLAY";
     LOCATE 12,4:COLOR 1,0:PRINT "==";:PRINT "{RED}> ";
PENTERWORD:
     ML=15:COLOR 7,4:GOSUB STRINGGET
     IF LEN(IS$)<>0 THEN GOTAWORD
     GOSUB BOING:LOCATE 25,4:PRINT "{GRN}*{YEL} MUST ENTER WORD {GRN}*";:GOTO PENTERWORD
GOTAWORD:
     COLOR 4,0:CLS:LOCATE 7,3:PRINT "{LIGHT GREEN};PL$(AP%);
     LOCATE 8,3:PRINT "{PUR}ENTER THE FOLLOWING.";
     HW$=IS$:LOCATE 10,5:PRINT "{PUR}HANG WORD IS: ";
     PRINT "{WHT};RPT$($7A,LEN(HW$));
     LOCATE 14,5:PRINT "{PUR}ENTER CLUE FOR WORD. {GRN}({WHT}OPTIONAL{GRN})";
     ML=34:IY=17:IX=3:IT=4:COLOR 7,4:GOSUB STRINGGET:HC$=IS$
     RETURN

PLAYERNAMES:
    COMPLETE.DRAW = TRUE
    DM$="PLAYER NAMES"
    GOSUB GMENUBACK
    IY=8:IX=8:IT=4:ML=19:LOCATE 6,5:COLOR 4,0:PRINT "{PUR}PLAYER 1 NAME";
    LOCATE 8,4:PRINT "{WHT}==";:PRINT "{RED}> ";
    COLOR 7,4:GOSUB STRINGGET:IF IS$="" THEN IS$="PLAYER 1"
    PL$(1)=IS$:PL%(1,1)=0
    COLOR 4,0:LOCATE 14,6:PRINT "{PUR}PLAYER 1 IS: ";:PRINT "{YEL}";PL$(1);
    IF CH=1 THEN NAMESDONE
    LOCATE 6,5:PRINT "{PUR}PLAYER 2 NAME";
    LOCATE 8,4:PRINT "{WHT}==";:PRINT "{RED}> ";
    COLOR 7,4:GOSUB STRINGGET:IF IS$="" THEN IS$="PLAYER 2"
    COLOR 4,0:PL$(2)=IS$:PL%(2,1)=0
    LOCATE 16,6:PRINT "{PUR}PLAYER 2 IS: ";:PRINT "{YEL}";PL$(2);
NAMESDONE:
    SLEEP 90:CLS
    RETURN


WORDFROMFILE:
 DM$="{YEL}GETTING{WHT} WORD {LIGHT GREEN}"
 RETRY:
   IF FC%=1 THEN F=1:GOTO SKIPRF
   F=INT(RND(X)*FC%)+1
   IF FC%>1 AND (F=SF%) THEN RETRY
   SF%=F
SKIPRF:
   S$=RIGHT$(STR$(F),LEN(STR$(F))-1):WF$="HWORDS"+S$+".DAT":F$="WORDS/"+WF$+",S,R":OPEN 4,8,4,F$

   INPUT#4,NL$:X=VAL(NL$):IF X<>0 THEN GETAWORD

   CLOSE 4:DM$=" INVALID WORD FILE: "+"WORDS/HWORDS"+S$+".DAT":GOTO ERRORHALT

GETAWORD:
  TL=INT(RND(X)*X)+1:DM$=DM$+" FROM{WHT} HWORDS{LIGHT GREEN}"+RIGHT$(STR$(F),LEN(STR$(F))-1)+"{WHT}":GOSUB PLAINMSG
  IF TL=1 THEN SKIP7
  TL=(TL-1)*2:POKE A.REG,4:POKE Y.REG,INT(TL/256):POKE X.REG, TL-PEEK(782)*256:SYS $650:X=ST
  IF X<>0 THEN CLOSE 4:GOTO RETRY
SKIP7:
  INPUT#4, HW$:LINPUT#4, HC$
  S$=HC$:GOSUB UCASE:HC$=S$
  IF LEN(HC$)>38 THEN HC$=LEFT$(HC$,38)
  CLOSE 4:REM GOSUB CLSGRAPH
  RETURN

GMENUBACK:
  RECT 0,0,XLIMIT,YLIMIT,$C4:RECT 4,4,XLIMIT-4,YLIMIT-4,$10:GOSUB FRAMESCRN
  RECT 10,15,307,214,6:FRAME 13,27,304,211,12:RECT 15,29,302,209,12:CHAR 18,25,15,CHR$($0C)+DM$:RETURN

FRAMESCRN:
  X1=0:Y1=0:X2=XLIMIT:Y2=YLIMIT:CC=$1A
  FOR J=1 TO 5:FRAME X1,Y1,X2,Y2,CC:X1=X1+1:X2=X2-1:Y1=Y1+1:Y2=Y2-1:CC=CC-2:NEXT J
  RETURN


PARTSCOUNT:
  COLOR 4,6:X=15:Y=9:H=3:W=20:GOSUB TEXTBOX:COLOR 1:LOCATE 10,16:PRINT"{WHT}BODY PARTS LEFT:";:PRINT"{YEL}";BP;:RETURN

PUSHBTN:
  C1=$17:C2=1:C3=$61:C4=$10:GOSUB DRAWBTN:GOSUB CLICK:SLEEP 5:C1=1:C2=$17:C3=$C2:C4=$1E:GOSUB DRAWBTN:RETURN

DISABLEBTN:
  X=AL%(3,A):Y=AL%(4,A):W=14:H=14:C1=$14:C2=$12:C3=$11:C4=$10:L$=CHR$(LL):GOSUB DRAWBTN:AL%(1,A)=1:RETURN

LETTERPANL:
  DM$="LETTER PANEL":GOSUB DRAWMSG
  L=65:X=131:Y=100:W=14:H=14:C1=1:C2=$17:C3=$C2:C4=$1E:FRAME 123,93,261,174,$10:RECT 124,94,260,173,$C0:RECT 126,97,258,171,$06:SX=X
  FOR CY=1 TO 3
    FOR J=1 TO 7:L$=CHR$(L):GOSUB DRAWBTN:I=L-64:AL%(1,I)=0:AL%(2,I)=L:AL%(3,I)=X:AL%(4,I)=Y:X=X+W+4:L=L+1:NEXT J
    Y=Y+W+4:X=SX
  NEXT CY
  X=SX:FOR CY=1 TO 5:L$=CHR$(L):GOSUB DRAWBTN:I=L-64:AL%(1,I)=0:AL%(2,I)=L:AL%(3,I)=X:AL%(4,I)=Y:X=X+W+4:L=L+1:NEXT CY:RETURN

DRAWBTN:
     IF H=0 THEN H=W
     X1=X+1:XW=X+W:YW=Y+H:Y1=Y+1:Y5=YW-1:X5=XW-1:LINE X,Y,XW,Y,C1:LINE X1,Y1,X5,Y1,C1:LINE XW,Y,XW,YW,C1:LINE X5,Y1,X5,Y5,C1
     LINE X,Y,X,YW,C2:LINE X1,Y1,X1,Y5,C2:LINE X,YW,X,YW,C2:LINE X,YW,X5,YW,C2:LINE X,Y5,XW-2,Y5,C2:RECT X+2,Y+2,XW-2,YW-2,C3:AX=4
## POSITION BUMPS TO CENTER THESE LETTERS ON THE BTN
     IF L$="M" THEN AX=3
     IF L$="W" THEN AX=2
     IF L$="F" OR L$="L" THEN AX=5
     IF L$="I" THEN AX=6
## DRAW THE LETTER ON THE BTN
     CHAR X+AX,YW-3,C4,L$
## DRAW A SERIF ON I AND J FOR APPEARANCE SAKE
     IF L$="I" THEN LINE X+6,YW-9,X+8,YW-9,C4:LINE X+6,YW-3,X+8,YW-3,C4
     IF L$="J" THEN LINE X+7,YW-9,X+9,YW-9,C4
     IF H=W THEN H=0
    RETURN

RESETCIRCLE:
  XSQUISH=1:YSQUISH=1:Q1=1:Q2=1:Q3=1:Q4=1:FILL=0:RETURN

  TEXTBOX:
     LOCATE Y,X:PRINT CHR$(DCHAR%(1));RPT$(DCHAR%(3),W-2);CHR$(DCHAR%(2));
     IF H=2 THEN SKIPMID
     FOR RA=1 TO H-2:LOCATE Y+RA,X:PRINT CHR$(DCHAR%(4));RPT$(32,W-2);CHR$(DCHAR%(5));:NEXT RA
SKIPMID:
     LOCATE Y+H-1,X:PRINT CHR$(DCHAR%(6));RPT$(DCHAR%(8),W-2);CHR$(DCHAR%(7));:RETURN

SETISOMODE:
  FONTFILE$="CP437.PF"
  PRINT "{ISO ON}"
  BVLOAD FONTFILE$,8,1,$F000
  GOSUB ISODRAW
  RETURN

ISODRAW:
  DCHAR%(1)=$C9
  DCHAR%(2)=$BB
  DCHAR%(3)=$CD
  DCHAR%(4)=$BA
  DCHAR%(5)=$BA
  DCHAR%(6)=$C8
  DCHAR%(7)=$BC
  DCHAR%(8)=$BB
  RETURN

PETDRAW:
  DCHAR%(1)=$6F
  DCHAR%(2)=$70
  DCHAR%(3)=$A3
  DCHAR%(4)=$A5
  DCHAR%(5)=$A7
  DCHAR%(6)=$6C
  DCHAR%(7)=$BA
  DCHAR%(8)=$A4
  RETURN

LOSSSCREEN:
  IF FORCEDRAW THEN SKIPL
  GOSUB THICKFONT:P1=$3B:P2=$0A:GOSUB COPYPALET
  FOR I=0 TO 7:TC%(1,I)=$0A:TC%(2,I)=0:NEXT I
  UC=1:GOSUB BLOCKFROWN:WD$="L":X=22:Y=14:GOSUB BIGWORD
  NEWZS=3:ZSM.RANDOM%(3)=TRUE:GOSUB SWAPSONGTO:GOSUB DEADFACE
SKIPL:
REM EYE PULSING WHILE WAIT FOR KEY
  DM$=" HIT ANY KEY TO CONTINUE ":GOSUB PLAINMSG:P1=$FE:G%=0:B%=0:GOSUB FLUSHKEYS
KEYLIMITOOP2:
  GOSUB MANAGEMUSIC:GET X$
GETMUS2:
  IF MB<>0 THEN X$="A"
EYESDONE:
  IF X$<>"" THEN ENDLOST
  R%=RND(1)*15+1:B%=RND(1)*6
  IF MB<>0 THEN X$="A":GOTO EYESDONE
  J=3:GOSUB PALFADE:GOTO KEYLIMITOOP2
ENDLOST:
  CLS:GOSUB THINFONT:RETURN


DEADFACE:
  GOSUB MANAGEMUSIC
  DM$=PL$(CP%)+" HAS DIED":GOSUB PLAINMSG:P1=$24:P2=$FF:J=4:GOSUB PAL2PALFADE:P1=$3B:P2=$FE:J=4:GOSUB PAL2PALFADE
  P1=$01:J=3:GOSUB PAL2PALFADE:P1=$3A:J=1:GOSUB PAL2PALFADE:P1=$15:J=3:GOSUB PAL2PALFADE
  P1=$3B:J=4:GOSUB PAL2PALFADE:P1=$23:P2=$FF:J=6:GOSUB PAL2PALFADE:P1=$B8:J=5:GOSUB PAL2PALFADE
  P1=$B9:GOSUB PAL2PALFADE:P1=$BF:GOSUB PAL2PALFADE:P1=$C7:GOSUB PAL2PALFADE:GOSUB MANAGEMUSIC:RETURN

STRINGGET:
  GET X$:IF X$<>"" THEN STRINGGET
  IS$="":ID=0:GOSUB AREACLR
  IF IS$<> "" THEN PRINT IS$;
GETAKEY:
  GET X$:IF X$="" THEN GETAKEY
  C=ASC(X$):AC=0
  ## IF WL=ML THEN

  IF (IT<>2 OR IT=3) AND C>=65 AND C<=90 THEN AC=TRUE
  IF (IT=2 OR IT=4 OR IT=3) AND C>=48 AND C<=57 THEN AC=TRUE
  IF (IT=2) AND C=46 AND ID=0 THEN AC=TRUE:ID=1
  IF (IT=4) AND C=32 THEN AC=TRUE: REM ALLOW SPACES WHEN INPUTTING A LINE
  IF (IT=4) AND (C>=35 AND C<=47) THEN AC=TRUE
  IF (IT=4) AND (C>=58 AND C<=63) THEN AC=TRUE  : REM ALLOW PUNCS IN STRING.
  IF C=13 THEN RETURN
  IF AC AND LEN(IS$)<ML THEN PRINT CHR$(C);:IS$=IS$+CHR$(C):GOSUB CLICK:GOTO GETAKEY
  IF C<>20 THEN TOLONG
  IF LEN(IS$)=0 THEN GOSUB BOING
  IF LEN(IS$)=1 OR LEN(IS$)=0 THEN IS$="":GOSUB AREACLR:GOTO GETAKEY
  I=LEN(IS$)-1
  IF RIGHT$(IS$,1)="." AND IT=2 THEN ID=0
  IS$=LEFT$(IS$,I)
  GOSUB AREACLR:PRINT IS$;:GOSUB CLICK
  GOTO GETAKEY
TOLONG:
  GOSUB BOING:GOTO GETAKEY

AREACLR:
  LOCATE IY,IX:PRINT RPT$(32,ML);:LOCATE IY,IX:RETURN

CLICK:
  IF UZSM% THEN ZCM.NUMBER=2:GOSUB ZCMPLAY:RETURN
  FMDRUM 7,26:RETURN

BOING:
  IF UZSM% THEN ZCM.NUMBER=1:GOSUB ZCMPLAY:RETURN
  FMDRUM 7,87:RETURN

FLUSHKEYS:
   GET X$:IF X$<>"" THEN FLUSHKEYS
   RETURN

BACKGROUND:
     F$="HANGBG.DAT":GOSUB FILEEXISTS
     IF NOT FE% THEN SKIPLOAD
     IF COMPLETE.DRAW THEN GOSUB HIDEGRAPHICS
     BVLOAD F$,8,0,0:GOSUB SETDRAWPAL
     IF COMPLETE.DRAW THEN GOSUB SHOWGRAPHICS
     GOTO BGEND
SKIPLOAD:
     J=3:XLIMIT=319:YLIMIT=239:XSQUISH=1:YSQUISH=1
     P1=$C0:P2=$F2:GOSUB COPYPALET
     R%=0:G%=0:B%=1:GOSUB RGBPAL
     DM$="THE SKY":GOSUB DRAWMSG
     RECT 0,0, XLIMIT, YLIMIT, $C0
     P1=$F2:P2=$C0:GOSUB PAL2PALFADE
     P1=$C0:P2=$10:GOSUB COPYPALET
     P1=7:P2=$F1:GOSUB COPYPALET
     P1=$C0:P2=7:GOSUB COPYPALET
     CX=5:CY=4:CC=$10:FILL=1:XSQUISH=1:YSQUISH=1:Q1=1:Q2=1
     Q3=1:Q4=1:RA=32:GOSUB BCIRCLE
     CC=$07:CX=5:CY=4:RA=30:FILL=1
     RA=30:GOSUB BCIRCLE:FILL=0:C=7

     LINE 2,37,2,44,C:LINE 6,37,6,44,C:LINE 9,37,10,44,C
     LINE 12,36,14,43,C:LINE 15,35,17,42,C:LINE 18,34,21,41,C
     LINE 20,33,24,39,C:LINE 22,31,27,37,C:LINE 24,28,29,35,C
     LINE 27,25,32,32,C:LINE 30,23,35,29,C:LINE 32,21,37,26,C
     LINE 33,19,39,23,C:LINE 35,17,41,20,C:LINE 36,15,43,17,C
     LINE 37,12,44,14,C:LINE 38,10,45,11,C:LINE 39,7,45,8,C
     LINE 39,4,45,5,C:LINE 39,1,46,2,C

     P1=0:P2=$10:J=3:GOSUB PAL2PALFADE:P1=$F1:P2=7:GOSUB PAL2PALFADE

     GOSUB DRWCLOUDS:GOSUB DRWCLOUDS:GOSUB DRWCLOUDS

     DM$="GRASS":GOSUB DRAWMSG
     P1=105:P2=$F2:GOSUB COPYPALET:P2=P1:P1=$C0:GOSUB COPYPALET
     P1=$85:P2=$F3:GOSUB COPYPALET:P2=P1:P1=$C0:GOSUB COPYPALET
     RECT 0,180,XLIMIT,YLIMIT,$85
     FOR Y=161 TO 179
         LINE 0,Y,40,179,105
     NEXT Y
     FOR Y=180 TO 150 STEP -1
         LINE 288,179,XLIMIT,Y,105
     NEXT Y
     J=5:P2=$85:P1=$F3:GOSUB PAL2PALFADE
     P2=105:P1=$F2:GOSUB PAL2PALFADE
     COLOR 1

     FOR I=1 TO 550
         X1=INT(RND(.)*310)+5
       NEWY:
         Y1=INT(RND(.)*60)+180:IF Y1>YLIMIT THEN NEWY
         GOSUB GRASSCLUMP
     NEXT I
     IF NOT LS% THEN BGEND
     F$="BACKGROUND SCREEN":GOSUB SAVESCREEN
BGEND:
     RETURN

SAVESCREEN:
    DM$="SAVING: "+F$:GOSUB PLAINMSG
    IF F$="BACKGROUND SCREEN" THEN FF$="@:HANGBG.DAT,P,W":GOTO DOIT
    FF$="@:"+F$ + ",P,W"
DOIT:
    OPEN 2,8,2,FF$
    POKE $30C,2:POKE$9F25,0:POKE$9F20,0:POKE$9F21,0:POKE$9F22,$10
    SYS$700
    CLOSE 2:RETURN

GRASSCLUMP:
     GOSUB GRASSCOLOR
     LINE X1,Y1,X1-4,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1-3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+4,Y1-5,GC
     RETURN
GRASSCOLOR:
     GC=INT(RND(.)*24)+$60:RETURN

DRWCLOUDS:
     DM$="CLOUDS":GOSUB DRAWMSG
     H=INT(RND(.)*(200))+ 45
     HL=INT(RND(.)*30)+30
     V=INT(RND(.)*30) + 6
     VB=INT(RND(.)*11) + 7
     FOR Y=V TO V+VB STEP 3
         FOR X=H TO H+HL STEP 4
           TRYCLOUD:
             RA=INT(RND(.)*5)+3
             CC=INT(RND(.)*4)+ $1C
             CX=X
             CY=INT(RND(.)*4)+(Y-4):IF (CY-RA)<0 THEN TRYCLOUD
             GOSUB CCIRCLE
         NEXT X
     NEXT Y
     RETURN

CCIRCLE:
     WR=RA:G=0: D=2*(1-RA):W=INT(2*320/240)
REM  WHILE WR<0
CCLOOP:
       IF WR < 0 THEN CCDONE
         LINE CX-G,CY-WR,CX+G,CY-WR,CC
         LINE CX-G,CY+WR,CX+G,CY+WR,CC
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF G>D THEN G=G+1: D=D+2*G+1
       GOTO CCLOOP
REM WEND
CCDONE:
    RETURN

DRAWGALLOWS:
     DM$="THE GALLOWS":GOSUB DRAWMSG
     FRAME 189,195,285,208,$10:FRAME 188,194,285,211,$10:LINE  189,210,285,210,$10
     PSET  189,209,$10:PSET  284,206,$10:PSET  284,209,$10
     FRAME 275,21,285,207,$10:FRAME 276,22,284,205,$10:FRAME 67,20,285,30,$10:FRAME 68,21,284,29,$10
     RECT  71,30,79,33,$10:RECT  190,196,283,209,$53:RECT  277,23,283,209,$53:RECT  69,22,283,28,83
     RETURN

DRAWNOOSE:
  DM$="THE NOOSE":GOSUB DRAWMSG:F$="NOOSE.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:GOSUB NOOSEFACEBLIT:RETURN
  LINE 73,33,73,53,16:LINE 77,33,77,53,16:RECT 74,34,76,53,87:FOR Y=38 TO 53 STEP 3:LINE 73,Y,77,Y-3,$10:NEXT Y
  FRAME 71,53,80, 68,16:RECT 72,54,79,68,87:FOR Y=56 TO 68 STEP 4:LINE 72,Y,79, Y-4,BLACK2:NEXT Y
  FILL=0:XSQUISH=1:RA=24:CX=75:CY=79:CC=BLACK2:YSQUISH=.38:Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=.38
  GOSUB BCIRCLE:RA=25:GOSUB BCIRCLE:RA=24:GOSUB BCIRCLE:RA=23:GOSUB BCIRCLE:RA=19:GOSUB BCIRCLE:RA=18:GOSUB BCIRCLE
  CC=87:FOR RA=20 TO 23 STEP .6:GOSUB BCIRCLE:NEXT RA:PUTIMAGE=FALSE:GOSUB NOOSEFACEBLIT:RETURN

DRAWFACE:
  DM$="A TROUBLED FACE":GOSUB DRAWMSG:F$="FACE.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:GOSUB NOOSEFACEBLIT:RETURN
  CX=58:CY=60:RA=6:XSQUISH=.4:YSQUISH=1:CC=BLACK2:Q1=1:Q2=0:Q3=1:Q4=0
  GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:FILL=1:CC=$25:GOSUB BCIRCLE
  FILL=0:CX=90:RA=6:CC=BLACK2:Q1=0:Q2=1:Q3=0:Q4=1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:FILL=1:CC=$25:GOSUB BCIRCLE
  LINE 93,58,93,62,BLACK2:Q1=1:Q2=Q1:Q3=Q1:Q4=Q1:LINE 67,76,67,84,$10:LINE 66,76,66,84,BLACK2
  LINE 67,87,67,90,BLACK2:LINE 66,87,66,90,$10:LINE 82,76,82,84,BLACK2:LINE 83,76,83,84,BLACK2:LINE 82,87,82,90,$10:LINE 83,87,83,90,BLACK2
  RECT 68,76,81,84,$25:RECT 68,89,81,90,$25:RA=20:XSQUISH=.8:FILL=1:CC=BLACK2:CX=74:CY=63
  GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:RA=RA-2:CY=CY+1:CC=$FF:FILL=1:GOSUB BCIRCLE:XSQUISH=1:YSQUISH=XSQUISH:CY=CY-6:CX=CX-6:RA=3:CC=$FE:GOSUB CCIRCLE
  PSET CX+4,CY,$FF:GOSUB PUPILS:CX=CX+12:GOSUB CCIRCLE:PSET CX+4,CY,$FF:GOSUB PUPILS:CX=74:CY=63:LINE CX-1,CY,CX-2,CY+6,$10:LINE CX+1,CY,CX+2,CY+6,$10
  LINE CX,CY,CX-1,CY+6,$23:LINE CX,CY,CX+1,CY+6,$23:LINE CX,CY+3,CX,CY+6,$22
  Q3=0:Q4=0:CC=$10:CY=CY+13:YSQUISH=.35:RA=6:FILL=0:GOSUB BCIRCLE
  CY=CY+1:CC=$31:GOSUB BCIRCLE:CY=CY+1:CC=$10:GOSUB BCIRCLE:Q3=1:Q4=1:YSQUISH=1:PUTIMAGE=FALSE:GOSUB NOOSEFACEBLIT:RETURN
PUPILS:
  PSET CX,CY,$10:PSET CX-1,CY,$10:PSET CX,CY+1,$10:PSET CX-1,CY+1,$10:RETURN

NOOSEFACEBLIT:
  BLITWIDTH=57:BLITLINES=75:X1=46:Y1=33:X2=102:Y2=107:GOSUB BITBLT:RETURN

DRWTORSO:
  DM$="TORSO":GOSUB DRAWMSG
  LINE 82,90,105,93,BLACK2:LINE 83,91,105,94,BLACK2:LINE 68,90,42,93,BLACK2:LINE 69,91,42,94,BLACK2:LINE 67,90,74,105,BLACK2:LINE 83,90,74,105,BLACK2
  FOR X=81 TO 68 STEP -1:LINE 75,103,X,90, $25:NEXT X
  LINE 74,105,72,110,BLACK2:LINE 72,110,72,141,BLACK2:LINE 72,141,53,144,BLACK2:LINE 53,144,53,108,BLACK2
  RECT 71,140,54,103,8:LINE 54,141,68,141,8:LINE 54,142,62,142,8:LINE 54,143,56,143,8
  PSET 65,91,$10:LINE 72,102,72,108,8:LINE 73,104,73,106,8:PSET 74,109,8:RECT 47,107,68,94,8:RECT 69,96,69,99,8
  RECT 42,107,48,95,8:LINE 56,93,67,93,8:PSET 55,92,$10:PSET 64,91,BLACK2:LINE 65,92,67,92, 8:RECT 68,100,71,102,8:LINE 70,98,70,100,8
  RECT 73,110,91,140,8:RECT 75,105,91,109,8:RECT 88,94,101,106,8:RECT 81,95,105,106,8:LINE 80,96,80,105,8
  LINE 79,98,79,105,8:LINE 78,100,78,105,8:LINE 77,101,77,105,8:LINE 76,103,76,105,8:LINE 74,103,75,103,BLACK2
  PSET 76,101,BLACK2:PSET 73,101,$10:PSET 72,99,$10:PSET 71,97,BLACK2:PSET 70,95,BLACK2:PSET 69,93,$10:PSET 81,92,$10:PSET 80,94,BLACK2
  PSET 80,96,BLACK2:PSET 78,97,$10:PSET 77,99,$10:LINE 76,100,76,103,$10:RECT 82,94,93,93, 8:LINE 83,92,86,92,8
  LINE 74,107,74,108,8:PSET 73,109,$10:PSET 73,141,$10:LINE 76,141,91,141,8:LINE 81,142,91,142,8:LINE 89,143,91,143,8:LINE 74,141,92,144,$10:LINE 92,144,92,107,$10:PSET 73,141,$10
  GOSUB RESETCIRCLE:CC=16:RA=1.2:CX=75:FILL=1:FOR CY=113 TO 143 STEP 8:GOSUB BCIRCLE:NEXT CY
  LINE 58,108,68,108,BLACK2:LINE 58,108,58,116,BLACK2:LINE 68,108,68,116,BLACK2:Q1=0:Q2=0:Q3=1:Q4=1:RA=4.5:XSQUISH=1:YSQUISH=.6
  CX=63:CY=116:CC=$10:FILL=0:GOSUB BCIRCLE:X$="P-"+RIGHT$(STR$(CP%),LEN(STR$(CP%))-1):CHAR 57,106,$10,X$:RETURN

DRWLEFTARM:
  DM$="LEFT ARM":GOSUB DRAWMSG:F$="LEFTARM.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:X1=91:Y1=92:BLITWIDTH=22:BLITLINES=57:GOSUB BITBLT:RETURN
  Q1=0:Q2=1:Q3=0:Q4=0:FILL=0:XSQUISH=.52:RA=16:CC=$10:CY=116:CX=92
  GOSUB BCIRCLE:RA=RA+.6:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE:XSQUISH=.28:CX=106:CY = 105:FILL=0:GOSUB BCIRCLE
  FOR L=1 TO 3:RA=RA+.5:GOSUB BCIRCLE:NEXT L
  CC=8:Q1=0:Q2=1:Q3=0:Q4=0:FILL=1:RA=RA-2.5:GOSUB BCIRCLE:RECT CX-1,CY-3,CX+2,CY-7,8
  LINE 99,113,99,133,BLACK2:LINE 111,102,111,133,BLACK2:RECT 100,102,110,133,8:LINE 99,107,99,109,8:LINE 98,108,101,108,8:LINE 97,107,100,107,8 :PSET 94,107,$C0
  LINE 99,134,111,134,BLACK2:LINE 101,134,101,143,BLACK2:LINE 101,143,103,143,BLACK2:LINE 102,142,102,139,$25:LINE 103,144,103,140,BLACK2
  LINE 103,145,105,145,BLACK2:LINE 104,144,104,139,$25:LINE 105,146,105,140,BLACK2:LINE 105,146,107,146,BLACK2:LINE 106,145,106,139,$25:LINE 107,146,107,140,BLACK2
  LINE 107,145,109,145,BLACK2:LINE 108,144,108,139,$25:LINE 109,145,109,134,BLACK2:RECT 102,139,108,135,$25:PUTIMAGE=FALSE:X1=91:Y1=92:X2=112:Y2=148:GOSUB BITBLT:RETURN

DRWRIGHTARM:
  DM$="RIGHT ARM":GOSUB DRAWMSG:F$="RIGHTARM.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN PUTIMAGE=TRUE:X1=33:Y1=92:BLITWIDTH=22:BLITLINES=57:GOSUB BITBLT:RETURN
  Q1=1:Q2=0:Q3=0:Q4=0:FILL=0:XSQUISH=.52:RA=16:CC=$10:CY=118:CX=54:GOSUB BCIRCLE:RA=RA+.6:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
  LINE 54,107,54,109,8:XSQUISH=.43:CX = 42:CY = 105:FILL=0:GOSUB BCIRCLE
  FOR L=1 TO 3:RA=RA+.3:GOSUB BCIRCLE:NEXT L
  CC=8:Q1=1:Q2=0:Q3=0:Q4=0:FILL=1:RA=RA-3:GOSUB BCIRCLE:FILL=0:RECT CX-4,CY-8,CX+3,CY+6,8:RECT CX-4,CY-4,CX,CY+6,8
  LINE 34,102,34,133,BLACK2:LINE 46,115,46,133,BLACK2:RECT 35,102,45,133,8:LINE 34,134,46,134,BLACK2:LINE 46,CY,46,CY+6,8:LINE 47,CY,47,CY+5,8
  RECT 47,CY,49,CY+3,8:LINE 51,CY+4,52,CY+4,$C0:PSET 48,CY+4, 8:LINE 44,134,44,143,$10:LINE 44,143,42,143,$10
  LINE 43,142,43,139,$25:LINE 42,140,42,145,BLACK2:LINE 42,145,40,145,BLACK2:LINE 41,144,41,139,$25
  LINE 40,146,40,140,BLACK2:LINE 40,146,38,146,BLACK2:LINE 39,145,39,139,$25:LINE 38,146,38,140,BLACK2
  LINE 38,145,36,145,BLACK2:LINE 37,144,37,139,$25:LINE 36,145,36,134,BLACK2:RECT 43,139,37,135,$25
  PUTIMAGE=FALSE:X1=33:Y1=92:X2=54:Y2=148:GOSUB BITBLT:RETURN

DRWRIGHTLEG:
  DM$="RIGHT LEG":GOSUB DRAWMSG:F$="RIGHTLEG.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN X1=52:Y1=142:PUTIMAGE=TRUE:BLITWIDTH=24:BLITLINES=79:GOSUB BITBLT:RETURN
  Q1=1:Q2=0:Q3=0:Q4=0:FILL=0:CC=$10:CX=74:CY=159:RA=8:XSQUISH=.43:YSQUISH=.6:GOSUB BCIRCLE
  LINE 70,159,70,192,BLACK2:LINE 55,145,53,192,BLACK2:LINE 53,193,70,193,BLACK2:RECT 56,145,69,192,LEGCOLOR
  LINE 55,157,55,192,LEGCOLOR:LINE 54,181,54,192,LEGCOLOR:LINE 57,144,71,144,LEGCOLOR:RECT 69,142,71,154,LEGCOLOR
  RECT 72,142,73,153,LEGCOLOR:LINE 63,143,68,143,LEGCOLOR:PSET 70,155,LEGCOLOR:Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XSQUISH=.35:CC=$10:FILL=1:CX=62:CY=208
  GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1:XSQUISH=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE:RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
  LINE 57,193,57,202,$10:LINE 66,193,66,202,$10:RECT 58,194,65,200,$1C:PUTIMAGE=FALSE:X1=52:Y1=142:X2=75:Y2=220:GOSUB BITBLT
  RETURN

DRWLEFTLEG:
  DM$="LEFT LEG":GOSUB DRAWMSG:F$="LEFTLEG.DAT":GOSUB FILEEXISTS
  IF FE% AND (NOT FORCEDRAW) THEN X1=73:Y1=142:PUTIMAGE=TRUE:BLITWIDTH=21:BLITLINES=79:GOSUB BITBLT:RETURN
  Q1=0:Q2=1:Q3=0:Q4=0:FILL=0:CC=$10:CX=74:CY=159:RA=8:XSQUISH=.43:YSQUISH=.6:GOSUB BCIRCLE
  LINE 77,157,77,192,BLACK2:LINE 90,145,92,192,BLACK2:LINE 77,193,92,193,BLACK2:RECT 78,145,89,192,LEGCOLOR
  LINE 90,157,90,192,LEGCOLOR:LINE 91,181,91,192,LEGCOLOR:LINE 76,144,88,144,LEGCOLOR:RECT 76,143,79,154,LEGCOLOR
  RECT 74,142,76,153,LEGCOLOR:LINE 75,143,82,143,LEGCOLOR:LINE 77,154,77,156,LEGCOLOR:Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XSQUISH=.35:CC=$10:FILL=1
  CX=85:CY=208:GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1:XSQUISH=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE:RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
  LINE 80,193,80,202,$10:LINE 89,193,89,202,$10:RECT 81,194,88,200,$1C:PUTIMAGE=FALSE:X1=73:Y1=142:X2=93:Y2=220:GOSUB BITBLT
  RETURN

SUNSMILE:
  COUNT=0:
STARTSMILE:
  F$="SMILEY.DAT":GOSUB FILEEXISTS:IF FE% THEN PUTIMAGE=TRUE:BLITWIDTH=34:BLITLINES=34:X1=0:Y1=0:GOSUB BITBLT:GOTO BLINK
  Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=1:XSQUISH=1:RA=4:CX=17:CY=6:CC=1:FILL=1:GOSUB BCIRCLE
  RA=1:CC=$10:GOSUB BCIRCLE:RA=5:FILL=0:GOSUB BCIRCLE:CX=1:CY=15:YSQUISH=.4:Q1=0:Q2=0:RA=20:GOSUB BCIRCLE:CY=16:GOSUB BCIRCLE:CY=17:GOSUB BCIRCLE:Q1=1:Q2=1
  PUTIMAGE=FALSE:X1=0:Y1=0:X2=33:Y2=33:GOSUB BITBLT
BLINK:
  IF COUNT>0 THEN RETURN
  COUNT=COUNT+1:Q1=1:Q2=1:Q3=1:Q4=1:YSQUISH=1:XSQUISH=1:RA=4:CX=17:CY=6:CC=$53:FILL=1:GOSUB BCIRCLE:GOTO STARTSMILE

BCIRCLE:
  WR=RA:X=0: D=2*(1-RA):W=INT(2*320/240)
##     IF XSQUISH<0 OR XSQUISH>1 THEN XSQUISH=1
##     IF YSQUISH<0 OR YSQUISH>1 THEN YSQUISH=1
BCLOOP:
  IF WR < 0 THEN BCLOOPEND
  DX=X*XSQUISH:DY=WR*YSQUISH
  ZX=CX-DX:ZY=CY-DY:AX=CX+DX:AY=CY+DY
  IF FILL=1 THEN DOFILL
  IF ZX<0 OR ZX>XLIMIT OR ZY<0 OR ZY>YLIMIT OR Q1=0 THEN SKIP4
  PSET ZX, ZY, CC
SKIP4:
  IF AX<0 OR AX>XLIMIT OR ZY<0 OR ZY>YLIMIT OR Q2=0 THEN SKIP2
  PSET AX, ZY, CC
SKIP2:
  IF ZX<0 OR ZX>XLIMIT OR AY<0 OR AY>YLIMIT OR Q3=0 THEN SKIP3
  PSET ZX, AY, CC
SKIP3:
  IF AX<0 OR AX>XLIMIT OR AY<0 OR AY>YLIMIT OR Q4=0 THEN SKIPFILL
  PSET AX, AY, CC:GOTO SKIPFILL
DOFILL:
  X1=ZX:X2=AX
  IF (Q1=0 AND Q2=O) OR X1>XLIMIT THEN FILL2
  IF ZY<0 OR ZY>YLIMIT THEN FILL2
  IF X1<0 THEN X1=0
  IF X2>XLIMIT THEN X2=XLIMIT
  IF Q1=0 THEN X1=CX
  IF Q2=0 THEN X2=CX
  LINE X1,ZY,X2,ZY,CC
FILL2:
  IF (Q3=0 AND Q4=0) OR ZX>XLIMIT THEN SKIPFILL
  IF AY<0 OR AY>YLIMIT THEN SKIPFILL
  IF ZX<0 THEN ZX=0
  IF AX>XLIMIT THEN AX=XLIMIT
  IF Q3=0 THEN ZX=CX
  IF Q4=0 THEN AX=CX
  LINE ZX,AY,AX,AY,CC
SKIPFILL:
  IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
  IF X>D THEN X=X+1:D=D+2*X+1
  GOTO BCLOOP
BCLOOPEND:
  RETURN

MANAGEMUSIC:
  IF NOT UZSM% THEN RETURN
  IF ZTIMER > TI THEN RETURN
  ZTIMER=0:IF MUSIC.PAUSED%(ZS) THEN RETURN
  GOSUB ZSMGETSTATE
  IF ZSM.IS.PLAYING THEN RETURN
  IF USING.LST%(ZS) THEN GOSUB GETZSMFROMLIST
  IF USING.LST%(ZS) THEN GOSUB ZSMCLOSE
  IF USING.LST%(ZS) THEN GOSUB DOLOADZSM:GOTO STARTM
  GOSUB ZSMREWIND
STARTM:
  GOSUB STARTMUSIC:RETURN


SWAPSONGTO:
  GOSUB PUSHSONG:ZS=NEWZS:GOSUB LOADZSM:GOSUB STARTMUSIC:RETURN

PLAYFANFARE:
  F$="FANFARE.DAT":GOSUB FILEEXISTS
  IF NOT FE% THEN GOSUB NFMSG:RETURN
  OPEN 10,8,10,"FANFARE.DAT,S,R"
  INPUT# 10, X$
  I=VAL(X$):X=INT(RND(1)*I)+1
  IF X=1 THEN SKIPFF
  X=X-1:POKE A.REG,10:POKE Y.REG,INT(X/256):POKE X.REG, X-PEEK(Y.REG)*256:SYS $650
SKIPFF:
  INPUT# 10, F$, ZTIMER
  CLOSE 10
  GOSUB FILEEXISTS:IF NOT FE% THEN GOSUB NFMSG:RETURN
  ZTIMER=INT(ZTIMER*60):GOSUB PUSHSONG:ZS=0:ZNUM%(0)=-1
PLAYZCMIMMEDIATE:
  BLOAD F$,8,2,$A000:ZCM.NUMBER=10:ZCM.BANK=2:GOSUB ZCMSETMEM:GOSUB ZCMPLAY:ZTIMER=ZTIMER+TI:RETURN

NFMSG:
 DM$=F$+" NOT FOUND":GOSUB PLAINMSG:RETURN

ZCMSETMEM:
  POKE A.REG, 0:POKE Y.REG, $A0
ZCMSETMEM2:
  BANK ZCM.BANK:POKE X.REG, ZCM.NUMBER:SYS $8C4B:RETURN

PUSHSONG:
  WAS.PAUSED=MUSIC.PAUSED%(ZS)
  GOSUB PAUSEMUSIC:OLDZS=ZS:ZSAVENAME$=ZSMNAME$
  IF USING.LST%(ZS) THEN SAVEZ=CURRENTZSM
  RETURN

SWAPSONGBACK:
  GOSUB PAUSEMUSIC:GOSUB ZSMCLOSE:GOSUB POPSONG:ZTIMER=TI+90:RETURN

POPSONG:
  CK=ZS
  ZS=OLDZS
  ZSMNAME$=ZSAVENAME$
  IF USING.LST%(ZS) THEN CURRENTZSM=SAVEZ
  MUSIC.PAUSED%(ZS)=WAS.PAUSED
  IF ZNUM%(CK)=ZNUM%(ZS) THEN GOSUB DOLOADZSM:GOTO POPSKIP
  BLOAD ZSMNAME$,8,2,$A000
POPSKIP:
  IF NOT MUSIC.PAUSED%(ZS) THEN SLEEP 5:GOSUB STARTMUSIC:SLEEP
  RETURN

PLAYSONG:
  IF NOT UZSM% THEN RETURN
  IF ZS=LASTZS THEN GOSUB MANAGEMUSIC:RETURN
  X=ZS:ZS=LASTZS:GOSUB PAUSEMUSIC:GOSUB ZSMCLOSE:ZS=X
  MUSIC.FILENAME$=MUSIC.NAME$(ZS)
  F$=MUSIC.FILENAME$:GOSUB FILEEXISTS
  IF NOT FE% THEN RETURN
  IF ZS<>LASTZS THEN GOSUB LOADZSM
  GOSUB STARTMUSIC:LASTZS=ZS:RETURN

STARTMUSIC:
   MUSIC.PAUSED%(ZS)=0:ZSM.ON%=-1
   POKE X.REG, ZNUM%(ZS)
   SYS $8C06
   IF USING.LST%(ZS) THEN LOOP.ON=0:GOSUB SETZSMLOOP
   RETURN

SETZSMLOOP:
  IF LOOP.ON THEN LOOP.ON=1
  POKE X.REG, ZNUM%(ZS):POKE C.REG,LOOP.ON:SYS $8C33:RETURN

PAUSEMUSIC:
  MUSIC.PAUSED%(ZS)=-1:POKE X.REG,ZNUM%(ZS):SYS $8C09:RETURN

LOADZSM:
  USING.LST%(ZS)=0:MUSIC.FILENAME$=MUSIC.NAME$(ZS)
  IF RIGHT$(MUSIC.FILENAME$,4)=".LST" THEN GOSUB GETZSMFROMLIST:USING.LST%(ZS)=-1:GOTO DOLOADZSM
  ZSMNAME$=MUSIC.FILENAME$
DOLOADZSM:
  GOSUB LOADZSMMSG:BLOAD ZSMNAME$,8,2,$A000:NEXT.BANK=PEEK(0)+1
## NOW TELL ZSMKIT WHERE THE SONG LIVES
ZSMSETMEM:
  BANK 2:POKE $30C, ZNUM%(ZS):POKE $30D,$00:POKE $30E,$A0
## ZSM-SETMEM
  SYS $8C1E
## NOW WE'RE CLEAR TO START SONG PLAYBACK
  GOSUB CLRLOADMSG
  RETURN

LOADZSMMSG:
  IF USING.LST%(ZS) THEN MSGMODE=4
  DM$="{YEL}LOADING:{WHT}" + ZSMNAME$
  ON MSGMODE GOTO GAMEMESG, GRAPHMESG, TEXTMESG, NOMESG
TEXTMESG:
  PRINT "{CR}";DM$:RETURN
GAMEMESG:
  GOSUB PLAINMSG:RETURN
GRAPHMESG:
  RECT 10,20,200,50,$A8:RECT 13,23,197,47,$C7:CHAR 18,32,$3A,DM$
NOMESG:
  RETURN

CLRLOADMSG:
  RETURN

ZSMCLOSE:
  POKE $30D, ZNUM%(ZS):SYS $8C0F:ZSM.ON%=0:RETURN

GETZSMFROMLIST:
  F$=MUSIC.NAME$(ZS):GOSUB FILEEXISTS:IF NOT FE% THEN RETURN
  MUSIC.FILENAME$=MUSIC.NAME$(ZS)
RETRYLST:
  IF CURRENTZSM<=0 THEN CURRENTZSM=1
  OPEN 10,8,10,MUSIC.FILENAME$ + ",S,R"
  INPUT# 10,ZCNT$
  ZSM.COUNT=VAL(ZCNT$)
  ENDZSM=CURRENTZSM:IF ENDZSM>ZSM.COUNT THEN ENDZSM=ZSM.COUNT

  IF ZSM.RANDOM%(ZS) THEN ENDZSM=INT(RND(1)*ZSM.COUNT)+1

## DEBUG OUTPUT  DM$ = "ZS: "+STR$(ZS)+" "+MUSIC.FILENAME$+" ENDZSM"+STR$(ENDZSM):GOSUB PLAINMSG:SLEEP 450

  IF ENDZSM=1 THEN SKIPZSMLOOP
  FOR I = 1 TO ENDZSM
    INPUT# 10, X$:X=ST
    IF X<>0 THEN I=ENDZSM
  NEXT I
## DEBUG OUTPUT DM$="LIST LOOP DONE":GOSUB PLAINMSG
  IF X<>0 THEN CLOSE 10:CURRENTZSM=0:GOTO RETRYLST
  GOTO SETZSMPOS
SKIPZSMLOOP:
  INPUT# 10, X$
SETZSMPOS:
  CURRENTZSM=CURRENTZSM+1:IF CURRENTZSM > ZSM.COUNT+1 THEN ZSM.RANDOM%(ZS) = -1
  ZSMNAME$=MUSIC.DIR$+X$:CLOSE 10:RETURN

ZSMGETSTATE:
  POKE X.REG, ZNUM%(ZS):SYS $8C2A:ZSM.IS.PLAYING=PEEK(C.REG) AND 1:ZSM.LOOPS=PEEK(PEEK(Y.REG))+PEEK(PEEK(X.REG))*256
  RETURN

ZSMREWIND:
  POKE X.REG, ZNUM%(ZS):SYS $8C0C:RETURN

ZCMPLAY:
  POKE X.REG, ZCM.NUMBER:POKE A.REG,ZCVOLUME:SYS $8C4E:RETURN

## BITBLIT IS CALLED WITH PUTIMAGE = 0 (FALSE) TO GET THE IMAGE
## IMAGE IS BUFFERED TO FILE PASSED IN F$
## SCREEN RECTANGLE TO CAPTURE IS PASSED IN X1, Y1, X2, Y2
## X2 and Y2  ** MUST ** be greater than/= X1,Y1.  IF < FUNNY
## THINGS WILL HAPPEN
##
## BITBLIT IS CALLED WITH PUTIMAGE = -1 (TRUE) TO PUT THE IMAGE
## ON THE SCREEN F$ WILL HOLD FILE NAME.
## PUT STARTING AT X1, Y1
##
## BLITWIDTH AND BLITLINES NEED TO BE SET TO THE WIDTH AND HEIGHT
## OF THE IMAGE RESPECTIVELY, NO SUCH INFO IS IN THE FILE.
##
## IF IMMEDIATELY FOLLOWING A BITBLIT GETIMAGE CALL THEN
## BLITWIDTH AND BLITLINES WILL ALREADY BE SET.
## IN THAT INSTANCE ONLY X1, Y1 NEED TO BE CHANGED
##
## X1 AND Y1 CAN BE LEFT UNCHANGED IF THE USE IS SAVING A
## SECTION OF SCREEN TO RESTORE AFTER SOMETHING ELSE IS DRAWN
##
##
## FOR THAT CASE CALL BITBLIT WITH PUTIMAGE = FALSE
## DO YOUR SCREEN STUFF
## SET PUTIMAGE TO TRUE,  CALL BITBLIT AGAIN TO RESTORE ALREADY


BITBLT:
  IF PUTIMAGE THEN X$="":S$=",S,R":GOTO SKIPPER
  IF X1>X2 THEN X=X1:X1=X2:X2=X
  IF Y1>Y2 THEN X=Y1:Y1=Y2:Y2=X
  S$=",P,W":X$="@:"
SKIPPER:
  FF$=X$+F$+S$
  IF PUTIMAGE THEN SET.IO=CHKIN:DO.FILEIO=MACPTR:GOTO DOSCREENBLIT
  SET.IO=CHKOUT:DO.FILEIO=MCIOUT:BLITWIDTH=(X2-X1)+1:BLITLINES=(Y2-Y1)+1
DOSCREENBLIT:
  IF Y1+BLITLINES > YLIMIT THEN BLITLINES=YLIMIT-Y1
  OPEN 2,8,2,FF$:POKE X.REG, 2:SYS SET.IO
  IF X1 > 255 THEN BYTESPLIT
  POKE R0L, X1:POKE R0H, 0:GOTO POKEY
BYTESPLIT:
  POKE R0L, X1 AND $00FF:POKE R0H, INT(X1/256)
POKEY:
  POKE R1L, Y1:POKE R1H, 0:SYS FB.CURSOR.POSITION

  FOR Y= 1 TO BLITLINES
    I=0
    FOR X=1 TO 3 STEP 0
      BYTES=BLITWIDTH-I:IF BYTES>$FF THEN BYTES=$FF
      POKE A.REG, BYTES:POKE X.REG, $23:POKE Y.REG, $9F:POKE C.REG, $01:SYS DO.FILEIO:I=I+PEEK(X.REG):IF I>=BLITWIDTH THEN X=3
    NEXT X
    SYS FB.CURSOR.NEXTLINE
  NEXT Y
  CLOSE 2:SYS CLRCHN:RETURN

FILEEXISTS:
  X$=F$:FE%=0:X$=X$+",S,R":OPEN 2,8,2,X$:CLOSE 2:OPEN 15,8,15:INPUT#15,X,X$:CLOSE 15:FE%=(X<=20):RETURN

DRAWMSG:
  DM$=" DRAWING: "+DM$
PLAINMSG:
  IF LEN(DM$)>38 THEN DM$=LEFT$(DM$,38)
  COLOR 1,0:GOSUB CLRMSG:LOCATE 29,2:PRINT DM$;:RETURN

CLRMSG:
  LOCATE 29,2:PRINT RPT$(32,38);:RETURN

BIGWORD:
  L=LEN(WD$):SX=X
  OB=PEEK(1):BANK PEEK(0),6
  FOR K=1 TO L
    CC=ASC(MID$(WD$,K,1))
    IF (CC>=64 AND CC<=90) OR (CC>=193 AND CC<=218) THEN AA=64:GOTO SKIP0
    AA=0
SKIP0:
    CA=$C000+8*(CC-AA)
    FOR I=1 TO 8
      CM(I)=PEEK(CA+(I-1)):CM$(I)=""
    NEXT I
    IF DC$="" THEN DC$=CHR$(CC)
    TD=8:IF CC=32 THEN TD=5
    FOR J=1 TO TD
        RESTORE BITMAPS
        FOR CT=1 TO 8
            READ CP
            IF (CP AND CM(J)) THEN CM$(J)=CM$(J)+DC$:GOTO SKIPSPC
            CM$(J)=CM$(J)+CHR$(32)
      SKIPSPC:
        NEXT CT
        LOCATE Y+(J-1),X:IF UC=1 THEN COLOR TC%(1,J),TC%(2,J)
        PRINT CM$(J);
    NEXT J
    IF DC$=CHR$(CC) THEN DC$=""
    X=X+TD
  NEXT K
  X=SX:BANK PEEK(0),OB:RETURN

HIDEGRAPHICS:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) AND LAYER0.OFF:RETURN

SHOWGRAPHICS:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) OR LAYER0.ON:RETURN

HIDETEXT:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) AND LAYER1.OFF:RETURN

SHOWTEXT:
  POKE VERA.DCVIDEO, PEEK(VERA.DCVIDEO) OR LAYER1.ON:RETURN

RGBPAL:
  A=P1*2:GB%=(G%*16)+B%:A=A+$FA00:SLEEP:VPOKE 1,A,GB%:A=A+1:SLEEP:VPOKE 1,A,R%:RETURN

READPAL:
  A1=$FA00+(P1*2):R%=VPEEK(1,A1+1):GB%=VPEEK(1,A1):G%=GB%/16:B%=GB% AND $0F:RETURN

PAL2PALFADE:
  GOSUB READPAL:P3=P1:P1=P2:GOSUB PALFADE:P1=P3:RETURN

PALFADE:
  DR%=R%:DG%=G%:DB%=B%:GOSUB READPAL
  RI=1:IF DR%<R% THEN RI=-1
  GI=1:IF DG%<G% THEN GI=-1
  BI=1:IF DB%<B% THEN BI=-1
  FOR X= 1 TO 3 STEP 0
    IF DR%<>R% THEN R%=R%+RI
    IF DG%<>G% THEN G%=G%+GI
    IF DB%<>B% THEN B%=B%+BI
    GOSUB RGBPAL:SLEEP J
    IF DR%=R% AND DG%=G% AND DB%=B% THEN X=3
  NEXT X
FADEDONE:
  RETURN

SWAPPALET:
  A1=$FA00+(P1*2):A2=$FA00+(P2*2):B1=VPEEK(1,A1):B2=VPEEK(1,A1+1):B3=VPEEK(1,A2):B4=VPEEK(1,A2+1)
  VPOKE 1,A1,B3:VPOKE 1,A1+1, B4:VPOKE 1,A2,B1:VPOKE 1,A2+1,B2:RETURN

COPYPALET:
  VPOKE 1,$FA00+(P2*2),VPEEK(1,$FA00+(P1*2)):VPOKE 1,$FA00+(P2*2)+1,VPEEK(1,$FA00+(P1*2)+1):RETURN

SETDEFPAL: 
  BVLOAD PF$,8,1,$FA00:RETURN

VARINIT:
  GOSUB PETDRAW:GOSUB SETREGVARS:FMINIT:XLIMIT=319:YLIMIT=239:UC%=-1:ZCVOLUME=15:ZVOLUME=0

  I$="SHOWCLUE":GOSUB GETINIVALUE
  IF I$="OFF" THEN UC%=0

  Q1=1:Q2=1:Q3=1:Q4=1:XSQUISH=1:YSQUISH=1:MW%=20:MC%=35:GOSUB INITALPHA
  TRUE=-1:FALSE=0:PUTIMAGE=FALSE:LEGCOLOR=$46:BLACK2=$10
##  SPIN$(1)=CHR$(100):SPIN$(2)=CHR$(109):SPIN$(3)=CHR$(103):SPIN$(4)=CHR$(47)
  MC$(1)="PLAYER VS COMPUTER":MC$(2)="2 PLAYER. COMPUTER PICKS":MC$(3)="HEAD TO HEAD":MC$(4)="DRAWING DEMO"
  MC$(5)="EXIT TO COMMANDER X16":DI$(1)="EASY":DI$(2)="MEDIUM":DI$(3)="HARD"

  RESTORE CONTROLS
  FOR I=1 TO 8:FOR C=1 TO 4:READ PC%(I,C):NEXT C:NEXT I

SETDRAWPAL:
  P1=$25:P2=$FF:GOSUB COPYPALET:P1=1:P2=$FE:GOSUB COPYPALET
  P1=$3B:P2=$FD:GOSUB COPYPALET:P1=$10:P2=4:GOSUB COPYPALET:P1=$1A:P2=12:GOSUB COPYPALET:RETURN


INITALPHA:
  FOR I=65 TO 90:AL%(1,I-64)=0:AL%(2,I-64)=I:NEXT I:RETURN

INITMUSICFILES:
  PRINT "{CR}{CR}{YEL} GETTING MUSIC INFO..{WHT}..";
  I$="MUSICDIR":GOSUB GETINIVALUE
  IF I$<> "" THEN GOSUB ADDSLASH
  MUSIC.DIR$=I$
  I$="INTROSONG":GOSUB GETINIVALUE:I=1:GOSUB FIXMUSIC:ZNUM%(I)=0
  I$="BACKGROUNDTRACK":GOSUB GETINIVALUE:I=2:GOSUB FIXMUSIC:ZNUM%(I)=0
  I$="LOSSMUSIC":GOSUB GETINIVALUE:I=3:GOSUB FIXMUSIC:ZNUM%(I)=0
  PRINT " DONE{CR}"
  FOR I=1 TO 3:PRINT "{CR}{GRN} SONG:{YEL}";I;" {WHT}";MUSIC.NAME$(I);:ZCM.RANDOM%(I)=0:NEXT I
  ZCM.RANDOM%(3)=-1
  RETURN

FIXMUSIC:
  IF I$<>"" THEN I$=MUSIC.DIR$+I$
  F$=I$:GOSUB FILEEXISTS
  IF FE% THEN MUSIC.NAME$(I)=I$
  RETURN

ADDSLASH:
  IF RIGHT$(I$,1)<>"/" THEN I$=I$+"/"
  RETURN

SETREGVARS:
  A.REG=$030C:X.REG=$030D:Y.REG=$030E:C.REG=$030F
  SET.MEM=$FF99
  R0L=$02:R0H=$03:R0=R0L
  R1L=$04:R1H=$05:R1=R1L
  R15L=$20:R15H=$21:R15=R15L
  CHKOUT=$FFC9:CHKIN=$FFC6:CLRCHN=$FFCC:MCIOUT=$FEB1:MACPTR=$FF44:
  VERA.DCVIDEO=$9F29
  LAYER0.ON=16:LAYER0.OFF=239:LAYER1.ON=32:LAYER1.OFF=223
  FB.CURSOR.POSITION=$FEFF:FB.CURSOR.NEXTLINE=$FF02
  RETURN

GETZPSTRING:
  X=$500:I=X+32
  ZP.STRING$=""
ZLOOP:
  Z=PEEK(X)
  IF Z=0 THEN RETURN
  ZP.STRING$=ZP.STRING$ + CHR$(Z):X=X+1
  IF X < I THEN ZLOOP
  RETURN

LOADER:
  ZP.STRING$="HANGMAN.PRG":GOSUB PUTZPSTRING:LOAD F$
  RETURN

PUTZPSTRING:
   IF LEN(ZP.STRING$)<32 AND LEN(ZP.STRING$)>0 THEN DOZPUT
   IF LEN(ZP.STRING$)=0 THEN POKE R0, 0:RETURN
   PRINT ZP.STRING$
   PRINT "STRING TO LONG FOR ZERO REGISTERS (MAX 31 CHARS)":END
DOZPUT:
   ZP1 = $500
   FOR I=1 TO LEN(ZP.STRING$)
       POKE ZP1, ASC(MID$(ZP.STRING$,I,1))
       ZP1=ZP1 + 1
   NEXT I
   POKE ZP1, 0
   RETURN

GETINIVALUE:
   I=1
GETILOOP:
   IF NN$(I)=I$ THEN I$=NV$(I):RETURN
   I=I+1:IF I>NV% THEN I$="":RETURN
   GOTO GETILOOP

READINI:
  NV%=0:FV%=0
  GOSUB FILEEXISTS:IF NOT FE% THEN RETURN
  FF$=F$+ ",S,R":OPEN 4,8,4,FF$
  DOINILOOP:
    LINPUT#4, S$
    IF LEFT$(S$,2)="##" THEN CONTLOOP
    IF LEFT$(S$,3)="REM" THEN CONTLOOP
    GOSUB TRIM:GOSUB UCASE
    IF LEFT$(S$,4)="REM " OR LEFT$(S$,2)="##" OR S$="" OR S$="REM" OR S$="##" THEN CONTLOOP
    PRINT ".";
    GOSUB GETINIVAR
    IF TN$="" THEN CONTLOOP
    NV%=NV%+1:NN$(NV%)=TN$:NV$(NV%)=TV$
  CONTLOOP:

  IF ST=0 AND NV%<15 THEN DOINILOOP
  CLOSE 4
  FV%=(NV%>0):PRINT:RETURN

GETINIVAR:
  SC$="":TN$="":TV$="":J=1
DOINIPARSE:
  X$=MID$(S$,J,1):IF X$="=" THEN GOTINI
  SC$=SC$+X$:J=J+1:IF J>LEN(S$) THEN ENDINI
  GOTO DOINIPARSE
GOTINI:
  J=J+1:TT$=S$:S$=SC$:SC$=""
  GOSUB RTRIM:TN$=S$:S$=TT$
VALUEPARSE:
  T$=MID$(S$,J,1):SC$=SC$+T$
  J=J+1:IF J>LEN(S$) THEN ENDINI
  GOTO VALUEPARSE
ENDINI:
  TT$=S$:S$=SC$:GOSUB TRIM:TV$=S$:S$=TT$
  RETURN

UCASE:
  IF S$="" THEN RETURN
  X$=""
  FOR I=1 TO LEN(S$)
    T$=MID$(S$,I,1):X=ASC(T$)
    IF X>=97 AND X<=122 THEN X=X-32
    X$=X$+CHR$(X)
 NEXT I
 S$=X$:RETURN

TRIM:
  GOSUB LTRIM:GOSUB RTRIM:RETURN

LTRIM:
   IF S$="" THEN RETURN
   I=1:C1=0
DOLOOP:
   T$=MID$(S$,I,1):X=ASC(T$):IF X > 32 THEN LSTR
   C1=C1+1:I=I+1:IF I>LEN(S$) THEN LSTR
   GOTO DOLOOP
LSTR:
   IF C1=LEN(S$) THEN S$=""
   IF C1>0 AND C1<LEN(S$) THEN S$=MID$(S$,C1+1,LEN(S$)-C1)
   RETURN

RTRIM:
  IF S$="" THEN RETURN
  I=LEN(S$):C1=0
DOLOOPR:
  T$=MID$(S$,I,1):X=ASC(T$):IF X>32 THEN RSTR
  C1=C1+1:I=I-1:IF I<1 THEN RSTR
  GOTO DOLOOPR
RSTR:
  IF C1=LEN(S$) THEN S$=""
  IF C1>0 AND C1<LEN(S$) THEN S$=MID$(S$,1,LEN(S$)-C1)
  RETURN

EDITINI:
  EDIT F$:RETURN

CLSGRAPH:
  COLOR 1,0:CLS:GOSUB HIDEGRAPHICS:RECT 0,0,XLIMIT,YLIMIT,BLACK2:GOSUB SHOWGRAPHICS:RETURN

ERRORHALT:
  GOSUB CLSGRAPH:P1=5:R%=0:G%=14:B%=2:GOSUB RGBPAL:P1=15:R%=2:G%=2:GOSUB RGBPAL
  PRINT "{CR}{CR}{YEL}  ERROR ENCOUNTERED{WHT}!":PRINT ":{GRN} {CR}";DM$;"{WHT}{CR}":END

CLEANUP:
  GOSUB CLEARMEM:GOSUB SETDEFPAL:LOAD "CREDITS.PRG"

THINFONT:
 RETURN
 POKE A.REG,4:SYS $FF62:GOSUB BLOCKFROWN:RETURN

THICKFONT:
 RETURN
 POKE A.REG,2:SYS $FF62:GOSUB BLOCKFROWN:RETURN

CLEARMEM:
  FOR A=R0L TO R15H:POKE A,0:NEXT A
  FOR A=$400 TO $40A:POKE A,0:NEXT A
  RETURN
