   SCREEN $80:COLOR $C:PRINT "GAME INITIALIZATION":MOUSE 1
   REM LOAD ASSEMBLY BVSAVE ROUTINE FROM XARK
   F$="BVSAVE700.BIN":GOSUB FILEEXISTS
   IF FE% THEN IF LS%=0 THEN LS%=1:LOAD "BVSAVE700.BIN",8,1
   DIM TC%(2,8),AL%(4 ,26),MC$(4),DI$(3),PL%(2,2)

   GOSUB VARINIT

   COLOR 1,0:CLS
   RECT 0,0,319,239,$3B
   RECT 3,3,316,236,$10:GOSUB INTROPAL
   DC$=CHR$(228):GOSUB HBANNER
   DC$=CHR$(162):GOSUB HBANNER
   DC$=CHR$(113):GOSUB HBANNER
   CHAR 75,165,$A9,CHR$($0C)+"THE CLASSIC WORD GAME"
   CHAR 62,180,$AB,CHR$($0C)+"NOW ON THE COMMANDER X16"
   CHAR 4,222,1,"CODED IN BASIC"
   CHAR 4,232,1,"2023..   ANTHONY HENRY"
   CHAR 185,222,1,"MUSIC CONTRIBUTED BY"
   CHAR 205,232,1,"MOOINGLEMUR"
   GOSUB STICKMAN
   GOSUB PALANIMST:GOSUB PALANIM1

MUSTART:
    RESTORE FUNMARCH:GW=0
MUCONT:
    READ PS$,DL
    IF DL=0 THEN MUSTART
    FMCHORD 0,PS$
    FOR I=1 TO DL
      GET X$
      IF X$ <>"" OR MB<>0 THEN GW=1
      GOSUB PALANIM1
    NEXT I
    IF GW=1 THEN GW=0:GOTO SCROLL
    GOTO MUCONT

SCROLL:
   FOR X=1 TO 31
        LOCATE 1,1:PRINT CHR$(145)
REPREAD:
        READ PS$,DL:IF DL=0 THEN RESTORE FUNMARCH:GOTO REPREAD
        FMCHORD 0,PS$
        FOR I = 1 TO DL:GOSUB PALANIM1:NEXT I
        GET X$:IF X$=CHR$(27) THEN X=31
    NEXT X

    IF X$=CHR$(27) THEN ENDINTRO
FINPLAY:
    READ PS$,DL
    IF DL=0 THEN ENDINTRO
    FMCHORD 0,PS$
    SLEEP DL
    GET X$:IF X$=CHR$(27) THEN ENDINTRO
    GOTO FINPLAY
ENDINTRO:
    FMINIT
    GOSUB CLRGRAPH:GOSUB SETDRAWPAL:GOTO GAMEMENU

HBANNER:
    X=5:Y=2:UC=1:WD$="H"+CHR$(193)+CHR$(206)+CHR$(199)
    GOSUB BIGWORD
    X=8:Y=11:WD$="M"+CHR$(193)+CHR$(206)
    GOSUB BIGWORD
    RETURN

REM PALLETTE ANIMATION FOR TITLE SCREEN
PALANIMST:
    P1=13
PALANIM1:
    IF P1=2 THEN P2=13:GOTO SKIP1
    P2=P1-1
SKIP1:
    GOSUB SWAPPALET
    P1=P1-1:IF P1<2 THEN P1=13
    RETURN

REM DRAW STICK FIGURE HANGMAN
STICKMAN:
    LINE 180,210,300,210,$53:LINE 180,209,299,209,$53
    LINE 300,210,300,17,$53:LINE 299,209,299,18,$53
    LINE 300,17,100,17,$53:LINE 299,18,100,18,$53
    LINE 102,19,102,38,$57
    FILL=0:CC=$57:YS=.4:XS=1:RA=13:CX=102:CY=45
    GOSUB BCIRCLE
    YS=1:XS=.8:CY=35:RA=10:CC=$25:FILL=1:GOSUB BCIRCLE
    CX=CX-3:RA=2:CY=CY-2:CC=2:GOSUB BCIRCLE:CX=CX+6:GOSUB BCIRCLE
    LINE 102,44,102,49,$25:LINE 102,51,102,78,$25
    LINE 102,53,88,64,$25:LINE 102,53,116,64,$25
    LINE 102,78,87,99,$25:LINE 102,78,117,99,$25
    RETURN

REM SETUP PALLETTE COLORS FOR INTRO SCREEN
INTROPAL:
    R%=15:G%=0:B%=2
    P1=$3B:P2=10:GOSUB COPYPALET
    P1=1:P2=11:GOSUB COPYPALET
    P1=$C7:P2=12:GOSUB COPYPALET
    P1=$A0:P2=13:GOSUB COPYPALET
    FOR I= 2 TO 9
         TC%(2,I-1) = 0
         TC%(1,I-1) = I
         P1=I:GOSUB RGBPAL:R%=R%-1
         IF I/3 = INT(I/3) THEN R%=R%-1:G%=G%+1
    NEXT I
    RETURN


GAMEMENU:
    GOSUB SETDEFPAL:GOSUB SETDRAWPAL
    DM$="HANGMAN GAME PARAMETERS":GOSUB GMENUBACK

    X=5:Y=4:CH=4:DI=2
    FOR I = 1 TO 4
        Y=Y+2:LOCATE Y,X
        COLOR 1,0:PRINT RIGHT$(STR$(I),1);
        COLOR 4:PRINT ":"MC$(I);
    NEXT I

    LOCATE 16,5:COLOR 4,0:PRINT "<";
    COLOR 1,4:PRINT "ENTER";:COLOR 4,0:PRINT ">";
    COLOR 4,0:PRINT "  STARTS THE GAME";
    LOCATE 18,5:COLOR 4:PRINT "(";:COLOR 7
    PRINT "CURRENTLY UNDER DEVELOPMENT.";
    LOCATE 19,6:PRINT "WILL DO QUICK PLAY FOR";
    LOCATE 20,6:PRINT "ALL CHOICES";
    COLOR 4:PRINT ")";
    COLOR 4:LOCATE 24,20:PRINT "DIFFICULTY:";
    COLOR 1:PRINT DI$(DI);
    COLOR 1,4:LOCATE 25,26:PRINT "<-";:LOCATE 25,30:PRINT "->";:COLOR 4,0

FLUSHKEY1:
    GET X$:IF X$<> "" THEN FLUSHKEY1

MENUSHOW:
    IF CH=4 THEN Y=12
    IF CH=3 THEN Y=10
    IF CH=2 THEN Y=8
    IF CH=1 THEN Y=6
    LOCATE Y,5:COLOR 7,4:PRINT CHR$($7A);" ";
    COLOR 1:PRINT MC$(CH);RPT$(32,27-LEN(MC$(CH)));
    COLOR 1,0:LOCATE 24,31:PRINT RPT$(32,10);:LOCATE 24,31:PRINT DI$(DI);

MENUGET:
    GET X$
    LOCATE 26,4:PRINT RPT$(32,30);
    IF MB=1 THEN LOCATE 26,4:COLOR 1:PRINT "MX:";MX;"    ";"MY:";MY;
    IF X$="" THEN MENUGET
    K=ASC(X$):IF K=27 THEN END
    LOCATE Y,5:COLOR 1,0:PRINT RIGHT$(STR$(CH),1);
    COLOR 4:PRINT ":"MC$(CH);RPT$(32,28-LEN(MC$(CH)));:FMDRUM 7,26
    IF K>=49 AND K<=52 THEN CH=VAL(X$)
    IF K=145 THEN CH=CH-1
    IF K=17 THEN CH=CH+1
    IF K=157 THEN DI=DI-1
    IF K=29 THEN DI=DI+1
    IF K=13 THEN GOSUB CLRGRAPH:GOTO STARTGAME
    IF CH>4 THEN CH=1
    IF CH<1 THEN CH=4
    IF DI>3 THEN DI=1
    IF DI<1 THEN DI=3
    GOTO MENUSHOW

STARTGAME:
    GOSUB PLAYERWORD
    GOSUB GAMEBOARD

HANDLEINPUT:
    GET X$
    IF X$=CHR$(27) THEN CLEANUP
    IF MB=1 THEN HNDLMOUSE
    IF X$="" THEN HANDLEINPUT
    GOTO SKIPMOUSE2

REM MOUSE EVALUATION CODE
HNDLMOUSE:
    CX=MX:CY=MY:X=1:H1=0:X$="":IF CY>131 THEN X=15
NEXTBTN:
    X1=AL%(3,X):X2=X1+14:Y1=AL%(4,X):Y2=Y1+14
    IF CX>=X1 AND CX<=X2 AND CY>=Y1 AND CY<=Y2 THEN X$=CHR$(AL%(2,X)):H1=1
    IF H1=0 AND X<26 THEN X=X+1:GOTO NEXTBTN
RELEASEMOUS:
    IF MB<>0 THEN RELEASEMOUS
    IF H1 THEN SKIPMOUSE2
    LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT

SKIPMOUSE2:
    LL=ASC(X$)
    IF LL<65 OR LL>90 THEN LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT
    A=LL-64
    IF AL%(1,A)=1 THEN LOCATE 1,1:FMDRUM 7,87:GOTO HANDLEINPUT
    CL$=CHR$(LL)
    GOSUB PUSHBTN
    GOTO HANDLEINPUT

     GOTO CLEANUP

GAMEBOARD:
     COLOR 1,0:CLS:GOSUB SETDRAWPAL
     IF DI=1 THEN BP=7
     IF DI=2 THEN BP=6
     IF DI=3 THEN BP=5
     GOSUB BACKGROUND
     IF DI>1 THEN GOSUB DRAWGALLOWS
     IF DI=3 THEN GOSUB DRAWNOOSE
     DW$=RPT$($7A,LEN(HW$))
     COLOR 5,6:X=17+(7-(LEN(HW$)/2)):Y=5:H=3:W=LEN(HW$)+2
     GOSUB TEXTBOX:COLOR 1
     LOCATE 6,18+(7-(LEN(HW$)/2)):PRINT DW$;
     GOSUB PARTSCOUNT
     GOSUB SHOWCLUE
     GOSUB LETTERPANL
     RETURN

SHOWCLUE:
     DM$=HC$:GOSUB PLAINMSG
     RETURN

REM SELECT WORD AND CLUE
REM FILE I/O WILL GO HERE TOO WHEN WRITTEN
PLAYERWORD:
     DM$="GET GAME WORD AND CLUE":GOSUB GMENUBACK
     IY=8:IX=8:IT=1
     LOCATE 6,5:COLOR 4,0:PRINT "ENTER WORD TO PLAY";
     LOCATE 8,4:COLOR 1,0:PRINT "==";:COLOR 2,0:PRINT "> ";
PENTERWORD:
     ML=15:COLOR 7,4:GOSUB STRINGGET
     IF LEN(IS$)<>0 THEN GOTAWORD
     PRINT CHR$(7):LOCATE 25,4:PRINT "* MUST ENTER WORD *";:GOTO PENTERWORD
GOTAWORD:
     COLOR 4,0:CLS:HW$=IS$:LOCATE 8,8:PRINT "HANG WORD IS: ";
     COLOR 1:PRINT RPT$($7A,LEN(HW$));
     LOCATE 13,5:COLOR 4,0:PRINT "ENTER CLUE FOR WORD. (OPTIONAL)";
     ML=34:IY=15:IX=3:IT=4:COLOR 7,4:GOSUB STRINGGET
     HC$=IS$
     RETURN

GMENUBACK:
     RECT 0,0,XLIMIT,YLIMIT,$3A
     FRAME 0,0,XLIMIT,YLIMIT,5
     RECT 4,4,XLIMIT-4,YLIMIT-4, $10
     RECT 10,15,307,214,6
     FRAME 13,27,304,211,12
     RECT 15,29,302,209,12
     CHAR 18,25,15,CHR$($0C)+DM$
     RETURN

PARTSCOUNT:
     COLOR 5,6:X=15:Y=8:H=3:W=20:GOSUB TEXTBOX
     COLOR 1:LOCATE 9,16:PRINT "BODY PARTS LEFT:";
     COLOR 7:PRINT BP;
     RETURN

PUSHBTN:
     X=AL%(3,A):Y=AL%(4,A):W=14:C1=$17:C2=1:C3=$61:C4=$10
     L$=CHR$(LL):GOSUB DRAWBTN:FMDRUM 7,26:SLEEP 5
     C1=1:C2=$17:C3=$C2:C4=$1E:GOSUB DRAWBTN
     REM AL%(1,A)=1
     RETURN

LETTERPANL:
     L=65
     X=131:Y=97:W=14:C1=1:C2=$17:C3=$C2:C4=$1E
     FRAME 123,90,261,171,$10
     RECT 124,91,260,170,$C0:RECT 126,94,258,168,$06
     SX=X
     FOR KK = 1 TO 3
         FOR J = 1 TO 7
           L$=CHR$(L)
           GOSUB DRAWBTN
            I=L-64
            AL%(1,I)=0
            AL%(2,I)=L
            AL%(3,I)=X
            AL%(4,I)=Y
           X = X + W + 4
           L=L+1
       NEXT J
      Y = Y + W + 4:X=SX
     NEXT KK
     X = SX
     FOR JK = 1 TO 5
        L$=CHR$(L)
        GOSUB DRAWBTN
            I=L-64
            AL%(1,I)=0
            AL%(2,I)=L
            AL%(3,I)=X
            AL%(4,I)=Y
        X = X + W + 4
        L=L+1
     NEXT JK
    RETURN

REM X,Y BUTTON POSITION
REM W CURRENTLY HEIGHT AND WIDTH MAY CHANGE FOR SEPERATE HEIGHT VARIABLE
REM C1, C2 3D BORDER COLORS
REM C3 CENTER COLOR
REM C4 TEXT LABEL COLOR
REM L$ THE TEXT LABEL
REM DRAW 3-D BUTTON WITH LABEL L$ AT X,Y
DRAWBTN:
     X1=X+1:XW=X+W:YW=Y+W:Y1=Y+1
     Y5=YW-1:X5=XW-1
     LINE X,Y,XW,Y,C1
     LINE X1,Y1,X5,Y1,C1
     LINE XW,Y,XW,YW,C1
     LINE X5,Y1,X5,Y5,C1
     LINE X,Y,X,YW,C2
     LINE X1,Y1,X1,Y5,C2
     LINE X,YW,X,YW,C2
     LINE X,YW,X5,YW,C2
     LINE X,Y5,XW-2,Y5,C2
     RECT X+2,Y+2,XW-2,YW-2,C3
     CHAR X+3,YW-3,C4,L$
    RETURN

REM BRESHNAHM CIRCLE
REM EXTRA CODE TO ENABLE TURNING ON AND OFF QUARTERS
REM X AND Y SCALING(XS & YS) AND CLIPPING AT EDGE OF SCREEN
REM TOOK OUT ERROR CHECKING FOR YS AND XS.  * MAKE SURE CORRECT IN MAIN CODE *
BCIRCLE:
     WR=RA:X=0: D=2*(1-RA):W=INT(2*320/240)
REM  WHILE WR<0
BCLOOP:
       IF WR < 0 THEN BCLOOPEND
       DX=X*XS:DY=WR*YS
       ZX=CX-DX
       ZY=CY-DY
       AX=CX+DX
       AY=CY+DY
       IF FILL=1 THEN DOFILL
         IF ZX<0 OR ZX>XL OR ZY<0 OR ZY>YL OR Q1=0 THEN SKIP4
         PSET ZX, ZY, CC
SKIP4:
         IF AX<0 OR AX>XL OR ZY<0 OR ZY>YL OR Q2=0 THEN SKIP2
         PSET AX, ZY, CC
SKIP2:
         IF ZX<0 OR ZX>XL OR AY<0 OR AY>YL OR Q3=0 THEN SKIP3
         PSET ZX, AY, CC
SKIP3:
         IF AX<0 OR AX>XL OR AY<0 OR AY>YL OR Q4=0 THEN SKIPFILL
         PSET AX, AY, CC
       GOTO SKIPFILL
DOFILL:
       X1=ZX:X2=AX
       IF (Q1=0 AND Q2=O) OR X1>XL THEN FILL2
       IF ZY<0 OR ZY>YL THEN FILL2
       IF X1<0 THEN X1=0
       IF X2>XL THEN X2=XL
       IF Q1=0 THEN X1=CX
       IF Q2=0 THEN X2=CX
          LINE X1,ZY,X2,ZY,CC
FILL2:
       IF (Q3=0 AND Q4=0) OR ZX>XL THEN SKIPFILL
       IF AY<0 OR AY>YL THEN SKIPFILL
       IF ZX<0 THEN ZX=0
       IF AX>XL THEN AX=XL
       IF Q3=0 THEN ZX=CX
       IF Q4=0 THEN AX=CX
          LINE ZX,AY,AX,AY,CC
SKIPFILL:
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF X>D THEN X=X+1:D=D+2*X+1
       GOTO BCLOOP
BCLOOPEND:
       RETURN


REM STRIPPED BRESHNAHM CIRCLE ALGORITHM FOR CLOUD DRAWING
REM STRIPPED FOR SPEED.
CCIRCLE:
     WR=RA:G=0: D=2*(1-RA):W=INT(2*320/200)
REM  WHILE WR<0
CCLOOP:
       IF WR < 0 THEN CCDONE
         LINE CX-G,CY-WR,CX+G,CY-WR,CC
         LINE CX-G,CY+WR,CX+G,CY+WR,CC
       IF (D+RA)>0 THEN WR=WR-1:D=D-W*WR-1
       IF G>D THEN G=G+1: D=D+2*G+1
       GOTO CCLOOP
REM WEND
CCDONE:
    RETURN

REM DRAW TEXT BOX
TEXTBOX:
     LOCATE Y,X
     PRINT CHR$($6F);RPT$($A3,W-2);CHR$($70);
     IF H=2 THEN SKIPMID
     FOR II = 1 TO H-2
         LOCATE Y+II,X
         PRINT CHR$($A5);RPT$(32,W-2);CHR$($A7);
     NEXT II
SKIPMID:
     LOCATE Y+H-1,X
     PRINT CHR$($6C);RPT$($A4,W-2);CHR$($BA);
     RETURN

LOSSSCRN:
     GOSUB DEADFACE
REM EYE PULSING WHILE WAIT FOR KEY
     P1=$FE:G%=0:B%=0
KEYFLUSH2:
     GET X$:IF X$<>"" THEN KEYFLUSH2
KEYLOOP2:
     GET X$
     IF MB<>0 THEN X$="A"
EYESDONE:
     IF X$<>"" THEN RETURN
     R%=RND(1)*15+1
     B%=RND(1)*6
     IF MB<>0 THEN X$="A":GOTO EYESDONE
     GOSUB PALFADE
     GOTO KEYLOOP2

DEADFACE:
     P1=$24:P2=$FF:J=4:GOSUB PAL2PALFADE
     P1=$3B:P2=$FE:J=4:GOSUB PAL2PALFADE
     P1=$01:J=3:GOSUB PAL2PALFADE
     P1=$3A:J=1:GOSUB PAL2PALFADE
     P1=$15:J=3:GOSUB PAL2PALFADE
     P1=$3B:J=4:GOSUB PAL2PALFADE
     P1=$23:P2=$FF:J=6:GOSUB PAL2PALFADE
     P1=$B8:J=5:GOSUB PAL2PALFADE
     P1=$BF:GOSUB PAL2PALFADE
     P1=$C0:GOSUB PAL2PALFADE
     RETURN


REM IY    LOCATE Y LOCATION
REM IX    LOCATE X LOCATION
REM ML    MAX LENGTH OF STRING TO GET (1 TO 80)
REM IT    TYPE OF INPUT 1=ALPHA ONLY, 2=NUMERIC(INCLUDE 1 AND ONLY 1 DECIMAL
REM                                                                  POINT)
REM                     3=ALPHANUMERIC, 4=LINE INPUT(CAN INCLUDE SPACE AND
REM                                                  TAB AND PUNCTUATION MARKS)
REM IS$   THE STRING TO RETURN
REM AC    ADD CHAR 1=YES
REM ID    DECIMAL DONE 1=YES
REM SPECIALIZED INPUT ROUTINE
STRINGGET:
     GET X$:IF X$<>"" THEN STRINGGET
     IS$="":ID=0
REM BEFORE INPUT BEGINS
     GOSUB AREACLR
GETAKEY:
     GET X$:IF X$="" THEN GETAKEY
     C=ASC(X$)
     IF WL=ML THEN
     AC=0
     IF (IT<>2 OR IT=3) AND C>=65 AND C<=90 THEN AC=1
     IF (IT=2 OR IT=4 OR IT=3) AND C>=48 AND C<=57 THEN AC=1
     IF (IT=2) AND C=46 AND ID=0 THEN AC=1:ID=1
     IF (IT=4) AND C=32 THEN AC = 1: REM ALLOW SPACES WHEN INPUTTING A LINE
     IF (IT=4) AND (C>=35 AND C<=47) THEN AC=1
     IF (IT=4) AND (C>=58 AND C<=63) THEN AC=1  : REM ALLOW PUNCS IN STRING.
     IF C=13 THEN RETURN
     IF AC=1 AND LEN(IS$)<ML THEN PRINT CHR$(C);:IS$=IS$+CHR$(C):FMDRUM 7,26
     IF AC=1 AND LEN(IS$)<ML THEN GETAKEY
     IF C<>20 THEN TOLONG
     IF LEN(IS$) = 0 THEN FMDRUM 7,87
     IF LEN(IS$)=1 OR LEN(IS$)=0 THEN IS$="":GOSUB AREACLR:GOTO GETAKEY
     I = LEN(IS$)-1
     IF RIGHT$(IS$,1)="." AND IT=2 THEN ID=0
     IS$=LEFT$(IS$,I)
     GOSUB AREACLR:PRINT IS$;:FMDRUM 7,26:GOTO GETAKEY
TOLONG:
     FMDRUM 7,87:GOTO GETAKEY

AREACLR:
     LOCATE IY,IX:FOR I = 1 TO ML+1:PRINT " ";:NEXT I
     LOCATE IY,IX
     RETURN

REM THE SKY
REM CHECK IF A BACKGROUND MEMORY IMAGE HAS BEEN SAVED
REM IF SO LOAD IT, OTHERWISE CONTINUE TO DRAW THE SCREEN
BACKGROUND:
     F$="HANGBG.DAT":GOSUB FILEEXISTS
     IF NOT FE% THEN SKIPLOAD
     GOSUB PAL2BLACK:BVLOAD F$,8,0,0
     GOSUB SETDEFPAL:GOSUB SETDRAWPAL:GOTO BGEND
SKIPLOAD:
     DM$="THE SKY":GOSUB DRAWMSG
     RECT 0,0, XLIMIT, YLIMIT, 14
     P1=14:P2=$10:GOSUB COPYPALET
     P1=7:P2=$F1:GOSUB COPYPALET
     P1=14:P2=7:GOSUB COPYPALET
     CX = 5:CY=4:CC=$10:FILL = 1:XS=1:YS=1:RA=32
     GOSUB BCIRCLE
     CC=$07
     FILL=1:RA= 30:GOSUB BCIRCLE:FILL=0

REM BRUTE FORCE, RAYS FROM THE SUN...
REM USING SCRATCH PALLETTE ENTRY AT $F1 SO
REM I CAN USE FADE EFFECT, FINAL COLOR WILL BE THE
REM SAME AS AT DEFAULT PALLETTE $07
     C=7
     LINE 2,37,2,44,C:LINE 6,37,6,44,C:LINE 9,37,10,44,C
     LINE 12,36,14,43,C:LINE 15,35,17,42,C:LINE 18,34,21,41,C
     LINE 20,33,24,39,C:LINE 22,31,27,37,C:LINE 24,28,29,35,C
     LINE 27,25,32,32,C:LINE 30,23,35,29,C:LINE 32,21,37,26,C
     LINE 33,19,39,23,C:LINE 35,17,41,20,C:LINE 36,15,43,17,C
     LINE 37,12,44,14,C:LINE 38,10,45,11,C:LINE 39,7,45,8,C
     LINE 39,4,45,5,C:LINE 39,1,46,2,C

     P1=0:P2=$10:J=3:GOSUB PAL2PALFADE
     P1=$F1:P2=7:GOSUB PAL2PALFADE

REM DRAW CLOUDS AT GOSUB DRWCLOUDS
     GOSUB DRWCLOUDS:GOSUB DRWCLOUDS:GOSUB DRWCLOUDS

     DM$="GRASS":GOSUB DRAWMSG
     RECT 0, 180, XLIMIT, YLIMIT, $85
     FOR Y=161 TO 179
         LINE 0,Y,40,179,105
     NEXT Y
     FOR Y=180 TO 150 STEP -1
         LINE 288,179,XLIMIT,Y,105
     NEXT Y
     COLOR 1

REM DRAW RANDOM GRASS
     FOR I=1 TO 400
         X1=INT(RND(1)*310) + 5
       NEWY:
         Y1=INT(RND(1)*60) + 180:IF Y1 > YLIMIT THEN NEWY
         GOSUB GRASSCLUMP
     NEXT I
     IF LS%<>1 THEN BGEND
     DM$="SAVING BACKGROUND SCREEN":GOSUB PLAINMSG
     OPEN 2,8,2,"@:HANGBG.DAT,P,W"
     POKE$30C,2:POKE$30D,$00:POKE$30E,$96
     POKE$9F25,0:POKE$9F20,$00:POKE$9F21,$00:POKE$9F22,$10:SYS$700
     CLOSE 2
BGEND:
     RETURN

GRASSCLUMP:
     GOSUB GRASSCOLOR
     LINE X1,Y1,X1-4,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1-3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1,Y1-5,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+3,Y1-3,GC:GOSUB GRASSCOLOR
     LINE X1,Y1,X1+4,Y1-5,GC
     RETURN
GRASSCOLOR:
     GC=INT(RND(1)*24)+$60:RETURN

REM SUPPOSED TO BE A CLOUD HERE
DRWCLOUDS:
     DM$="CLOUDS":GOSUB DRAWMSG
     H = INT(RND(1)*(200))+ 45
     HL = INT(RND(1)*30)+30
     V = INT(RND(1)*30) + 6
     VB = INT(RND(1)*11) + 7
     FOR Y = V TO V+VB STEP 3
         FOR X = H TO H + HL STEP 4
           TRYCLOUD:
             RA = INT(RND(1)*5)+3
             CC = INT(RND(1)*4)+ $1C
             CX=X
             CY= INT(RND(1)*4) + (Y-4):IF (CY - RA) < 0 THEN TRYCLOUD
             GOSUB CCIRCLE
         NEXT X
     NEXT Y
     RETURN

REM THE GALLOWS
DRAWGALLOWS:
     DM$="THE GALLOWS":GOSUB DRAWMSG
     FRAME 189,195,285,208,$10
     FRAME 188,194,285,211,$10
     LINE  189,210,285,210,$10
     PSET  189,209,$10
     PSET  284,206,$10
     PSET  284,209,$10
     FRAME 275,21,285,207,$10
     FRAME 276,22,284,205,$10
     FRAME 67,20,285,30,$10
     FRAME 68,21,284,29,$10
     RECT  71,30,79,33,$10
     RECT  190,196,283,209,$53
     RECT  277,23,283,209,$53
     RECT  69,22,283,28,83
     RETURN

REM THE ROPE
DRAWNOOSE:
     DM$="THE NOOSE":GOSUB DRAWMSG
     FILL = 0
     LINE 73,33,73,53,16
     LINE 77,33,77,53,16
     RECT 74,34,76,53,87
     FOR Y = 38 TO 53 STEP 3
         LINE 73, Y, 77, Y - 3, $10
     NEXT Y
     FRAME 71,53,80, 68, 16
     RECT 72,54,79,68,87
     FOR Y = 56 TO 68 STEP 4
         LINE 72,Y,79, Y-4, $10
     NEXT Y

REM THE NOOSE
     RA = 24
     YS = .38
     CX = 75:CY = 79:CC=$10
     GOSUB BCIRCLE:RA=25:GOSUB BCIRCLE
     RA=24:GOSUB BCIRCLE
     RA =23:GOSUB BCIRCLE
     RA = 19:GOSUB BCIRCLE
     RA = 18:GOSUB BCIRCLE
     CC = 87
     FOR X = 20 TO 23 STEP .6
         RA=X:GOSUB BCIRCLE
     NEXT X
     YS=1:RETURN
REM END ROPE

REM THE FACE
DRAWFACE:
     DM$="A TROUBLED FACE":GOSUB DRAWMSG

REM HIS EARS
     CX=58:CY=60:RA=6:XS=.4:CC=$10
     Q1=1:Q2=0:Q3=1:Q4=0
     GOSUB BCIRCLE:RA = RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
     FILL=1:CC=$25:GOSUB BCIRCLE
     FILL=0:CX=90:RA=6:CC=$10
     Q1=0:Q2=1:Q3=0:Q4=1
     GOSUB BCIRCLE:RA = RA-1:GOSUB BCIRCLE:RA=RA-1:GOSUB BCIRCLE
     FILL=1:CC=$25:GOSUB BCIRCLE
     LINE 93,58,93,62,$10
     Q1=1:Q2=Q1:Q3=Q1:Q4=Q1
REM DRAW HIS NECK
     LINE 67,76,67,84,$10
     LINE 66,76,66,84,$10
     LINE 67,87,67,90,$10:LINE 66,87,66,90,$10
     LINE 82,76,82,84,$10
     LINE 83,76,83,84,$10
     LINE 82,87,82,90,$10:LINE 83,87,83,90,$10
     RECT 68,76,81,84,$25
     RECT 68,89,81,90,$25
REM END NECK

     RA=20:XS=.8:FILL=1
     CC=$10:CX=74:CY=63
     GOSUB BCIRCLE:RA = RA - 1:GOSUB BCIRCLE
     RA=RA-2:CY=CY+1:CC=$FF:FILL=1:GOSUB BCIRCLE
     XS = 1:YS = XS

REM RIGHT EYE
     CY=CY-6:CX = CX-6:RA=3:CC=$FE:GOSUB CCIRCLE
     PSET CX+4,CY,$FF:GOSUB PUPILS

REM LEFT EYE
     CX = CX + 12:GOSUB CCIRCLE
     PSET CX+4,CY,$FF:GOSUB PUPILS:CX = 74:CY = 63
     LINE CX-1,CY, CX - 2, CY + 6, $10
     LINE CX+1,CY, CX + 2, CY + 6, $10
     LINE CX,CY,CX -1, CY + 6, $23
     LINE CX,CY,CX + 1, CY + 6, $23
     LINE CX,CY+3,CX,CY+6,$22
     Q3=0:Q4=0:CC=$10:CY = CY + 13:YS=.35:RA=6:FILL=0
     GOSUB BCIRCLE
     CY = CY + 1:CC=$31:GOSUB BCIRCLE
     CY = CY + 1:CC=$10:GOSUB BCIRCLE

REM REMEMBER TO TURN CIRCLE FULLY ON !!!
     Q3=1:Q4=1:YS=1
     RETURN

REM THE PUPILS
PUPILS:
     PSET CX,CY,$10:PSET CX -1,CY,$10
     PSET CX,CY +1,$10:PSET CX - 1, CY + 1 ,$10
     RETURN
REM END FACE


REM THE TORSO
DRWTORSO:
      DM$="TORSO":GOSUB DRAWMSG
      LINE 82,90,105,93,$10
      LINE 83,91,105,94,$10
      LINE 68,90,42,93, $10
      LINE 69,91,42,94, $10
      LINE 67,90,74,105,$10
      LINE 83,90,74,105,$10

REM THIS FOR LOOP FILLS IN THE NECKLINE
      FOR X=81 TO 68 STEP -1
          LINE 75,103,X, 90, $25
      NEXT X
      LINE 74,105,72,110,$10
      LINE 72,110,72,141,$10
      LINE 72,141,53,144,$10
      LINE 53,144,53,108,$10
      RECT 71,140,54,103,$08
      LINE 54,141,68,141,$08
      LINE 54,142,62,142,$08
      LINE 54,143,56,143,$08
      PSET 65,91,$10
      LINE 72,102,72,108,$08
      LINE 73,104,73,106,$08
      PSET 74,109,$08
      RECT 47,107,68,94,$08
      RECT 69,96,69,99,$08
      RECT 42,107,48,95,$08
      LINE 56,93,67,93, $08:PSET 55,92,$10:PSET 64,91,$10
      LINE 65,92,67,92, $08
      RECT 68,100,71,102,$08
      LINE 70,98,70,100, $08
      RECT 73,110,91,140,$08
      RECT 75,105,91,109,$08
      RECT 88, 94, 101, 106, $08
      RECT 81, 95, 105, 106, $08
      LINE 80, 96, 80, 105, $08
      LINE 79,98,79,105, $08
      LINE 78,100,78,105,$08
      LINE 77,101,77,105,$08
      LINE 76,103,76,105,$08
      LINE 74,103,75,103, $10
      PSET 76,101,$10:PSET 73,101,$10:PSET 72,99,$10:PSET 71,97,$10
      PSET 70,95,$10:PSET 69,93,$10:PSET 81,92,$10:PSET 80,94,$10
      PSET 80,96,$10:PSET 78,97,$10:PSET 77,99,$10
      LINE 76,100,76,103,$10
      RECT 82, 94, 93, 93, $08
      LINE 83, 92, 86, 92, $08
      LINE 74, 107,74, 108,$08
      PSET 73,109,$10:PSET 73,141,$10
      LINE 76,141,91,141,$08
      LINE 81,142,91,142,$08
      LINE 89,143,91,143,$08
      LINE 74,141,92,144,$10
      LINE 92,144,92,107,$10
      PSET 73,141,$10

REM SHIRT BUTTONS
      CC = 16:RA = 1.2
      CX=75:FILL=1
      FOR CY=113 TO 143 STEP 8
          GOSUB BCIRCLE
      NEXT CY

REM POCKET AND PRISONER NUMBER
      LINE 58,108,68,108,$10
      LINE 58,108,58,116,$10
      LINE 68,108,68,116,$10
      Q1=0:Q2=0:Q3=1:Q4=1:RA=4.5:XS=1:YS=.6
      CX=63:CY=116:CC=$10:FILL=0
      GOSUB BCIRCLE
      PN$="P-1"
      CHAR 57,106,$10,PN$
      RETURN

REM ARM ON THE RIGHT (LEFT ARM)
DRWLEFTARM:
      Q1=0:Q2=1:Q3=0:Q4=0:FILL = 0
      DM$="LEFT ARM":GOSUB DRAWMSG
      XSQUISH=.52
      RA=16:CC=$10
      CY=116:CX=92
      GOSUB BCIRCLE:RA=RA+.6
      GOSUB BCIRCLE
      RA=RA-1:GOSUB BCIRCLE
      XSQUISH=.28:CX = 106:CY = 105:FILL=0
      GOSUB BCIRCLE
      FOR L=1 TO 3:RA=RA+.5:GOSUB BCIRCLE:NEXT L
      CC=$08:Q1=0:Q2=1:Q3=0:Q4=0:FILL=1:RA=RA-2.5
      GOSUB BCIRCLE
      RECT CX-1,CY-3,CX+2,CY-7,$08
      LINE 99,113,99,133,$10
      LINE 111,102,111,133,$10
      RECT 100,102,110,133,$08
      LINE 99,107,99,109,$08
      LINE 98,108,101,108,$08
      LINE 97,107,100,107,$08 :PSET 94,107,$0E
      LINE 99,134,111,134,$10

REM (THE LEFT HAND)
      LINE 101,134,101,143,$10:LINE 101,143,103,143,$10
      LINE 102,142,102,139,$25:LINE 103,144,103,140,$10
      LINE 103,145,105,145,$10:LINE 104,144,104,139,$25
      LINE 105,146,105,140,$10:LINE 105,146,107,146,$10
      LINE 106,145,106,139,$25:LINE 107,146,107,140,$10
      LINE 107,145,109,145,$10:LINE 108,144,108,139,$25
      LINE 109,145,109,134,$10:RECT 102,139,108,135,$25
      RETURN

REM ARM ON THE LEFT (RIGHT ARM)
DRWRIGHTARM:
      Q1=1:Q2=0:Q3=0:Q4=0:FILL=0
      XSQUISH=.52
      RA=16:CC=$10
      CY=118:CX=54
      DM$="RIGHT ARM":GOSUB DRAWMSG
      GOSUB BCIRCLE:RA=RA+.6
      GOSUB BCIRCLE
      RA=RA-1:GOSUB BCIRCLE
      LINE 54,107,54,109,$08
       XSQUISH=.43:CX = 42:CY = 105:FILL=0
       GOSUB BCIRCLE
       FOR L=1 TO 3:RA=RA+.3:GOSUB BCIRCLE:NEXT L
       CC=$08:Q1=1:Q2=0:Q3=0:Q4=0:FILL=1:RA=RA-3
       GOSUB BCIRCLE:FILL=0
       RECT CX-4,CY-8,CX+3,CY+6,$08
       RECT CX-4,CY-4,CX,CY+6,$08
       LINE 34,102,34,133,$10
       LINE 46,115,46,133,$10
       RECT 35,102,45,133,$08
       LINE 34,134,46,134,$10
       LINE 46,CY,46,CY+6,$08
       LINE 47,CY,47,CY+5,$08
       RECT 47,CY,49,CY+3,$08
       LINE 51,CY+4,52,CY+4,$0E
       PSET 48,CY+4, $08

REM (THE RIGHT HAND)
      LINE 44,134,44,143,$10:LINE 44,143,42,143,$10
      LINE 43,142,43,139,$25:LINE 42,140,42,145,$10
      LINE 42,145,40,145,$10:LINE 41,144,41,139,$25
      LINE 40,146,40,140,$10:LINE 40,146,38,146,$10
      LINE 39,145,39,139,$25:LINE 38,146,38,140,$10
      LINE 38,145,36,145,$10:LINE 37,144,37,139,$25
      LINE 36,145,36,134,$10:RECT 43,139,37,135,$25
      RETURN

REM RIGHT LEG
DRWRIGHTLEG:
      Q1=1:Q2=0:Q3=0:Q4=0
      CC=$10:CX=74:CY=159:RA=8:XQUISH=.10
      DM$="RIGHT LEG":GOSUB DRAWMSG
      GOSUB BCIRCLE
      LINE 70,159,70,192,$10:LINE 55,145,53,192,$10
      LINE 53,193,70,193,$10:RECT 56,145,69,192,$46
      LINE 55,157,55,192,$46:LINE 54,181,54,192,$46
      LINE 57,144,71,144,$46:RECT 69,142,71,154,$46
      RECT 72,142,73,153,$46:LINE 63,143,68,143,$46:PSET 70,155,$46

REM THE FOOT
      Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XS=.35:CC=$10:FILL=1:CX=62:CY=208
      GOSUB BCIRCLE:RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1
      XS=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE
      RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
      LINE 57,193,57,202,$10:LINE 66,193,66,202,$10
      RECT 58,194,65,200,$1C
      RETURN

REM LEFT LEG
DRWLEFTLEG:
      Q1=0:Q2=1:Q3=0:Q4=0
      CC=$10:CX=74:CY=159:RA=8:XQUISH=.10
      DM$="LEFT LEG":GOSUB DRAWMSG
      GOSUB BCIRCLE
      LINE 77,157,77,192,$10:LINE 90,145,92,192,$10
      LINE 77,193,92,193,$10:RECT 78,145,89,192,$46
      LINE 90,157,90,192,$46:LINE 91,181,91,192,$46
      LINE 74,144,88,144,$46:RECT 76,143,79,155,$46
      RECT 74,142,76,153,$46:LINE 74,143,82,143,$46
      LINE 77,154,77,156,$46

REM THE FOOT
      Q1=1:Q2=1:Q3=1:Q4=1:RA=20:XS=.35:CC=$10:FILL=1
      CX=85:CY=208
      GOSUB BCIRCLE
      RA=RA+1:FILL=0:CC=$12:GOSUB BCIRCLE:FILL=1
      XS=.38:CY=204:CC=$1C:RA=11:GOSUB BCIRCLE
      RA = RA+1:CC=$12:FILL=0:GOSUB BCIRCLE
      LINE 80,193,80,202,$10:LINE 89,193,89,202,$10
      RECT 81,194,88,200,$1C
      RETURN

REM CHECK IF A FILE EXISTS
REM NAME PASSED IN F$, VALUE RETURNS IN FE%
FILEEXISTS:
      FF$=F$:FE%=0
      FF$=FF$+",S,R"
      OPEN 2,8,2,FF$
      FE%=(ST=0)
      CLOSE 2
      RETURN

REM PLACE A MESSAGE ON THE BOTTOM OF THE SCREEN
DRAWMSG:
      DM$=" DRAWING: "+DM$
PLAINMSG:
      COLOR 1,0:GOSUB CLRMSG
      LOCATE 30,1:PRINT DM$;
      RETURN
CLRMSG:
      LOCATE 30,1:PRINT RPT$(32,39);
      RETURN

REM BIGWORD ROUTINE..  READS ROM FONT AND BLOWS IT UP
BIGWORD:
      L=LEN(WD$):SX = X
      OB=PEEK(1):BANK PEEK(0),6
      FOR K=1 TO L
         CC=ASC(MID$(WD$,K,1))

REM READ IN A CHARACTER MAP CC
REM AND PRINT IT OUT AT Y,X
         IF (CC>=64 AND CC<=90) OR (CC>=193 AND CC<=218) THEN AA=64:GOTO SKIP0
         AA=0
SKIP0:
         CA=$C000 + 8*(CC-AA)
         FOR I=1 TO 8
             CM(I) = PEEK(CA+(I-1)):CM$(I)=""
         NEXT I
         IF DC$="" THEN DC$=CHR$(CC)
          FOR J=1 TO 8
             RESTORE BITMAPS
             FOR CT=1 TO 8
                READ CP
                IF (CP AND CM(J)) THEN CM$(J)=CM$(J)+DC$:GOTO SKIPSPC
                CM$(J)=CM$(J)+CHR$(32)
          SKIPSPC:
             NEXT CT
             LOCATE Y+(J-1),X:IF UC=1 THEN COLOR TC%(1,J),TC%(2,J)
             PRINT CM$(J);
          NEXT J
          IF DC$=CHR$(CC) THEN DC$=""
          X=X+8
      NEXT K
      X=SX
      BANK PEEK(0),OB
      RETURN

RGBPAL:
      VPOKE 1,$FA00+(P1*2),(G%*16) + B%
      VPOKE 1,$FA00+((P1*2)+1),R%
      RETURN

READPAL:
      A1=$FA00+(P1*2)
      R%=VPEEK(1,A1+1)
      GB%=VPEEK(1,A1)
      G%=GB%/16
      B%=GB% AND $0F
      RETURN

PAL2PALFADE:
      GOSUB READPAL
      P3=P1
      P1=P2
      GOSUB PALFADE
      P1=P3
      RETURN

PALFADE:
      DR%=R%:DG%=G%:DB%=B%
      GOSUB READPAL
      RI=1:IF DR%<R% THEN RI=-1
      GI=1:IF DG%<G% THEN GI=-1
      BI=1:IF DB%<B% THEN BI=-1
INCRGB:
      IF DR%<>R% THEN R%=R%+RI
      IF DG%<>G% THEN G%=G%+GI
      IF DB%<>B% THEN B%=B%+BI
      GOSUB RGBPAL
      SLEEP J
      IF DR%=R% AND DG%=G% AND DB%=B% THEN FADEDONE
      GOTO INCRGB
FADEDONE:
      RETURN

SWAPPALET:
      A1=$FA00+(P1*2):A2=$FA00+(P2*2)
      B1=VPEEK(1,A1):B2=VPEEK(1,A1+1)
      B3=VPEEK(1,A2):B4=VPEEK(1,A2+1)
      VPOKE 1,A1,B3:VPOKE 1,A1+1, B4
      VPOKE 1,A2,B1:VPOKE 1,A2+1, B2
      RETURN

REM COPY P1 PALLETTE ENTRY TO P2.. P1 IS LEFT UNCHANGED.
COPYPALET:
      VPOKE 1,$FA00+(P2*2),VPEEK(1,$FA00+(P1*2))
      VPOKE 1,$FA00+(P2*2)+1,VPEEK(1,$FA00+(P1*2)+1)
      RETURN

REM SET THE PALLETTE TO ALL BLACK
PAL2BLACK:
      FOR A=$FA00 TO $FBFF
          VPOKE 1,A,0
      NEXT A
      RETURN

REM LOOP THROUGH THE PALLETTE ADDRESS SPACE
REM AND POKE THE VERA DEFAULT PALLETTE
SETDEFPAL: 
      F$="DPAL.BIN":GOSUB FILEEXISTS
      IF FE% THEN BVLOAD F$,8,1,$FA00:RETURN
      RESTORE VERAPAL
      FOR PE=$FA00 TO $FBFE STEP 2
          READ R:READ GB
          VPOKE 1,PE,GB:VPOKE 1,PE+1,R
      NEXT PE
      RETURN

REM DEFAULT VERA PALLETTE AS DATA. FROM 0 TO 255 (2 BYTES EACH ENTRY)R,GB
VERAPAL:
      DATA 0,0,15,255,8,0,10,254,12,76,0,197,0,10,14,231,13,133,6,64,15,119,3
      DATA 51,7,119,10,246,0,143,11,187,0,0,1,17,2,34,3,51,4,68,5,85,6,102,7
      DATA 119,8,136,9,153,10,170,11,187,12,204,13,221,14,238,15,255,2,17,4,51
      DATA 6,68,8,102,10,136,12,153,15,187,2,17,4,34,6,51,8,68,10,85,12,102,15
      DATA 119,2,0,4,17,6,17,8,34,10,34,12,51,15,51,2,0,4,0,6,0,8,0,10,0,12,0
      DATA 15,0,2,33,4,67,6,100,8,134,10,168,12,201,15,235,2,17,4,50,6,83,8
      DATA 116,10,149,12,182,15,215,2,16,4,49,6,81,8,98,10,130,12,163,15,195,2
      DATA 16,4,48,6,64,8,96,10,128,12,144,15,176,1,33,3,67,5,100,7,134,9,168
      DATA 11,201,13,251,1,33,3,66,4,99,6,132,8,165,9,198,11,247,1,32,2,65,4
      DATA 97,5,130,6,162,8,195,9,243,1,32,2,64,3,96,4,128,5,160,6,192,7,240,1
      DATA 33,3,67,4,101,6,134,8,168,9,202,11,252,1,33,2,66,3,100,4,133,5,166
      DATA 6,200,7,249,0,32,1,65,1,98,2,131,2,164,3,197,3,246,0,32,0,65,0,97,0
      DATA 130,0,162,0,195,0,243,1,34,3,68,4,102,6,136,8,170,9,204,11,255,1,34
      DATA 2,68,3,102,4,136,5,170,6,204,7,255,0,34,1,68,1,102,2,136,2,170,3
      DATA 204,3,255,0,34,0,68,0,102,0,136,0,170,0,204,0,255,1,18,3,52,4,86,6
      DATA 104,8,138,9,172,11,207,1,18,2,36,3,70,4,88,5,106,6,140,7,159,0,2,1
      DATA 20,1,38,2,56,2,74,3,92,3,111,0,2,0,20,0,22,0,40,0,42,0,60,0,63,1,18
      DATA 3,52,5,70,7,104,9,138,11,156,13,191,1,18,3,36,4,54,6,72,8,90,9,108
      DATA 11,127,1,2,2,20,4,22,5,40,6,42,8,60,9,63,1,2,2,4,3,6,4,8,5,10,6,12
      DATA 7,15,2,18,4,52,6,70,8,104,10,138,12,156,15,190,2,17, 4,35,6,53,8,71
      DATA 10,89,12,107,15,125,2,1,4,19,6,21,8,38,10,40,12,58,15,60,2,1,4,3,6
      DATA 4,8,6,10,8,12,9,15,11

BITMAPS:
      DATA 128,64,32,16,8,4,2,1,0

REM MUSIC FROM MOOINGLEMUR
FUNMARCH:
      DATA "O4V63I0CO3V50I18CV50I18E-V50I18G",60,"O4CO3E-G-B-",45,"O4C",15
      DATA "O4CO3CE-G",60,"RE-G-B-",60,"O4CO3CE-G",60,"O4CO3E-G-B-",45
      DATA "O4C",15,"O4CO3CE-G",60,"RE-G-B-",60,"O4CO3CE-G",60
      DATA "O4CO3E-G-B-",45,"O4C",15,"O4CO3CE-G",60,"O4E-O3E-G-B-",45,"O4D",12
      DATA "E-",3,"O4DO3CE-G",45,"O4C",15,"O4CO3E-G-B-",45,"O4C",15
      DATA "O4CO3CE-G",60,"RE-G-B-",60,"",0

REM SETUP INITIAL VARIABLES
VARINIT:
      XLIMIT=319:YLIMIT=239:X=RND(-TI):FMINIT

REM LOAD DEFAULT PALLETTE INIT VERA
      GOSUB SETDEFPAL

REM SET PRETTY FONT
      POKE$30C,4:SYS$FF62


REM Q1-Q4 ARE INITIALIZED FOR CIRCLE DRAWING ROUTINE AT 3000
      Q1=1:Q2=1:Q3=1:Q4=1
      XS=1:YS=1

REM MAX LENGTH OF WORD AND CLUE
      MW%=20
      MC%=35

REM INITIALIZE THE ALPHABET
      GOSUB INITALPHA

      MC$(1)="PLAYER VS COMPUTER"
      MC$(2)="2 PLAYER. COMPUTER PICKS"
      MC$(3)="HEAD TO HEAD"
      MC$(4)="QUICK PLAY THROUGH"

      DI$(1)="EASY"
      DI$(2)="MEDIUM"
      DI$(3)="HARD"

REM SET MY PALLETTE COLORS I AM GOING TO ANIMATE LATER
REM COPY FLESH COLOR ALSO TO $FF
SETDRAWPAL:
      P1=$25:P2=$FF:GOSUB COPYPALET
REM COPY PURE WHITE ALSO TO $FE
      P1=1:P2=$FE:GOSUB COPYPALET
REM RED TO $FD
      P1=$3B:P2=$FD:GOSUB COPYPALET
REM COPY YELLOW TO $F1 FOR SUN RAYS COLOR IF I VLOAD.
      P1=7:P2=$F1:GOSUB COPYPALET
      P1=$C0:P2=5:GOSUB COPYPALET
REM COPY BLACK TO PAL 4 SO I CAN HAVE
REM NON TRANSPARENT BLACK TEXT
      P1=$10:P2=4:GOSUB COPYPALET
      P1=$1A:P2=12:GOSUB COPYPALET
      RETURN

REM INITIALIZE AN ARRAY TO HOLD THE ALPHABET
REM USING ASCII VALUES INSTEAD OF STRING
REM WITH A FLAG TO SHOW IF THE LETTERS BEEN USED
REM 2 DIMENSIONAL ARRAY 1,X IS FLAG, 2,X IS ASCII CODE 3&4,X is X,Y
REM   FOR BUTTON POSITION ON SCREEN.
INITALPHA:
      FOR I=65 TO 90
        AL%(1,I-64)=0:AL%(2,I-64)=I
      NEXT I
      RETURN

REM FLUSH KEYBOARD BUFFER AND WAIT FOR KEYPRESS
AWAITKEY:
      GET X$:IF X$<>"" THEN AWAITKEY
GETIT:
      GET X$:IF X$="" AND MB=0 THEN GETIT
      RETURN

CLRGRAPH:
      COLOR 1,0:CLS
      RECT 0,0,XLIMIT,YLIMIT,$10
      RETURN

CLEANUP:
     GOSUB AWAITKEY:GOSUB SETDEFPAL:SCREEN 0:END


