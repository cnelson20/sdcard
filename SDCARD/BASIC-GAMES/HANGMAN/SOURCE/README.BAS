#CONTROLCODES 1
#SYMFILE "@:README.SYM"


  GOSUB POKEFASTFWD:GOSUB SETREGVARS
  POKE C.REG,1
  SYS SET.MEM
  LAST.BANK = PEEK(A.REG):IF LAST.BANK=0 THEN LAST.BANK=256
  LAST.BANK = LAST.BANK - 1
  BANKS.AVAILABLE = LAST.BANK-2
  NEXT.BANK=2
  GOSUB GETFROMMENU
  VPOKE 1,$FA00, 2:VPOKE 1,$FA01,0
  PRINT "{ISO ON}"
  SCREEN 1:COLOR 1,0:CLS
  FILE.SECTION = 1
SHOW:
  CLS
  GOSUB SHOWSECTION
WAITKEY:
  GET X$:IF X$="" THEN WAITKEY
  SS=FILE.SECTION
  K=ASC(X$):IF K=27 THEN VPOKE 1,$FA00,0:CLS:GOTO READDONE
  IF K=130 OR K=145 THEN FILE.SECTION = FILE.SECTION-1
  IF K=2 OR K=17 THEN FILE.SECTION=FILE.SECTION + 1
  IF FILE.SECTION < 1 THEN FILE.SECTION=1:FMDRUM 7,87
  IF SS<>FILE.SECTION THEN FMDRUM 7,26:GOTO SHOW
  IF SS=FILE.SECTION THEN FMDRUM 7,87
  GOTO WAITKEY

READDONE:
  IF FROM.MENU THEN LOAD "CONFMENU.PRG"
  END

SHOWSECTION:
 PRINT "{WHT}"
RETRY:
 OPEN 4,8,4,"README.TXT,S,R"
 IF FILE.SECTION = 1 THEN DOLOAD
 TL=(FILE.SECTION*30)-30
 POKE A.REG,4:POKE Y.REG, INT(TL/256):
 POKE X.REG, TL-PEEK(Y.REG)*256:SYS $650
 IF ST<>0 THEN FILE.SECTION=1:CLOSE 4:GOTO RETRY
DOLOAD:
 FOR L=1 TO 30
   LINPUT# 4, X$
   EOF=ST
   LOCATE L,1:PRINT "  ";X$;
   IF L<30 THEN PRINT
   IF EOF THEN L=30
 NEXT L
DONESECTION:
 CLOSE 4
 RETURN

GETFROMMENU:
  OLD.BANK=PEEK(0):S$=""
  ADDR=$BF00:BANK LAST.BANK
  FOR I=0 TO 7:S$=S$+CHR$(PEEK(ADDR+I)):NEXT I
  FROM.MENU = (S$="HANGMENU")
  BANK OLD.BANK:RETURN

POKEFASTFWD:
   RESTORE FASTFWD650
   FOR A=$650 TO $680:READ C:POKE A, C:NEXT A:RETURN

## 49 BYTES
FASTFWD650:
  DATA $E0,$00,$D0,$05,$C0,$00,$F0,$24,$88,$C8,$84,$22,$DA,$AA,$20,$C6,$FF,$7A
  DATA $20,$CF,$FF,$C9,$0D,$D0,$07,$88,$D0,$04,$C6,$22,$F0,$0C,$20,$B7,$FF,$29
  DATA $40,$F0,$EB,$20,$CC,$FF,$38,$60,$20,$CC,$FF,$18,$60

SETREGVARS:
  A.REG = $030C
  X.REG = $030D
  Y.REG = $030E
  C.REG = $030F
  SET.MEM = $FF99
  R0L=$02:R0H=$03:R0=R0L
  R1L=$04:R1H=$05:R1=R1L
  R2L=$06:R2H=$07:R2=R2L
  R3L=$08:R3H=$09:R3=R3L
  R4L=$A:R4H=$B:R4=R4L
  R5L=$C:R5H=$D:R5=R5L
  R6L=$E:R6H=$F:R6=R6L
  R7L=$10:R7H=$11:R7=R7L
  R8L=$12:R8H=$13:R8=R8L
  R9L=$14:R9H=$15:R9=R9L
  R10L=$16:R10H=$17:R10=R10L
  R11L=$18:R11H=$19:R11=R11L
  R12L=$1A:R12H=$1B:R12=R12L
  R13L=$1C:R13H=$1D:R13=R13L
  R14L=$1E:R14H=$1F:R14=R14L
  R15L=$20:R15H=$21:R15=R15L
  CHKOUT=$FFC9:CHKIN=$FFC6:CLRCHN=$FFCC
  MCIOUT=$FEB1:MACPTR=$FF44:
  VERA.DATA0=$9F23:VERA.DATA1=$9F24:VERA.CTRL=$9F25
  VERA.DCVIDEO=$9F29
  LAYER0.ON=16
  LAYER0.OFF=239
  LAYER1.ON=32
  LAYER1.OFF=223
  FB.CURSOR.POSITION=$FEFF
  FB.CURSOR.NEXTLINE=$FF02
  SCREEN.SIZE=$12C00
  RETURN
